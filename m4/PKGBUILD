
pkgname=m4
pkgver=1.4.18
pkgrel=2
pkgdesc="m4 macro processor"
arch=('x86_64')
license=('GPL3')
url="https://www.gnu.org/software/m4"
groups=('base-devel')
depends=('glibc' 'bash')
source=("https://ftp.gnu.org/gnu/m4/${pkgname}-${pkgver}.tar.xz"
        "https://raw.githubusercontent.com/gentoo/gentoo/master/sys-devel/m4/files/m4-1.4.18-glibc228.patch")
md5sums=('730bb15d96fffe47e148d1e09235af82'
         '14c5e9b88e0ce006b0113fbcdc067159')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/m4-1.4.18-glibc228.patch
  
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make prefix=${pkgdir}/usr install
}
