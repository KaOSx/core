
pkgname=wpa_supplicant
pkgver=2.9
pkgrel=1
pkgdesc="A utility providing key negotiation for WPA wireless networks"
arch=('x86_64')
url="https://w1.fi/"
depends=('openssl' 'dbus' 'readline' 'libnl')
license=('GPL')
groups=('base')
backup=('etc/wpa_supplicant.conf')
source=("https://w1.fi/releases/wpa_supplicant-${pkgver}.tar.gz"
        'config'
        "https://w1.fi/security/2017-1/rebased-v2.6-0001-hostapd-Avoid-key-reinstallation-in-FT-handshake.patch")
md5sums=('2d2958c782576dc9901092fbfecb4190'
         '78bc0d6b6db02d4d58982d870e0a0c26'
         'a209fe1510a138c0da3855854c38bf6f')

build() {
  cd ${pkgname}-${pkgver}/
  #patch -p1 -i ${srcdir}/rebased-v2.6-0001-hostapd-Avoid-key-reinstallation-in-FT-handshake.patch
  
  cd ${pkgname}/
  cp ${srcdir}/config ./.config
  sed -i 's@/usr/local@$(PREFIX)@g' Makefile
  
  make PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}/${pkgname}
  make PREFIX=/usr DESTDIR=${pkgdir} install
  
  install -m755 -d ${pkgdir}/etc
  install -m644 wpa_supplicant.conf ${pkgdir}/etc/wpa_supplicant.conf
  install -d -m755 ${pkgdir}/usr/share/man/man{5,8}
  install -m644 doc/docbook/*.5 ${pkgdir}/usr/share/man/man5/
  install -m644 doc/docbook/*.8 ${pkgdir}/usr/share/man/man8/
  rm -f ${pkgdir}/usr/share/man/man8/wpa_{priv,gui}.8

  install -m755 -d ${pkgdir}/usr/share/dbus-1/system-services
  install -m644 dbus/fi.w1.wpa_supplicant1.service ${pkgdir}/usr/share/dbus-1/system-services/

  install -m755 -d ${pkgdir}/etc/dbus-1/system.d
  install -m644 dbus/dbus-wpa_supplicant.conf ${pkgdir}/etc/dbus-1/system.d/wpa_supplicant.conf

  install -d -m755 ${pkgdir}/usr/lib/systemd/system
  install -m644 systemd/*.service ${pkgdir}/usr/lib/systemd/system/
}
