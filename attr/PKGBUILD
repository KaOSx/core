
pkgname=attr
pkgver=2.4.47
pkgrel=1
pkgdesc="Extended attribute support library for ACL support"
arch=('x86_64')
url="http://oss.sgi.com/projects/xfs/"
license=('LGPL')
groups=('base')
depends=('glibc') 
makedepends=('gettext')
options=('!libtool')
source=("http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.src.tar.gz")
md5sums=('84f58dec00b60f2dc8fd1c9709291cc7')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  
  export INSTALL_USER=root INSTALL_GROUP=root
  ./configure --prefix=/usr --libdir=/usr/lib --libexecdir=/usr/lib
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DIST_ROOT="${pkgdir}" install install-lib install-dev

  # tidy up
  rm -f "${pkgdir}"/usr/lib/libattr.a
  chmod 0755 "${pkgdir}"/usr/lib/libattr.so.*.*.*
  # remove conflicting manpages
  rm -rf "${pkgdir}"/usr/share/man/man2
}
