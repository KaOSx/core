
pkgname=libcap
pkgver=2.31
pkgrel=1
pkgdesc="POSIX 1003.1e capabilities"
arch=('x86_64')
url="https://sites.google.com/site/fullycapable/"
license=('GPL')
depends=('glibc' 'attr')
options=('!staticlibs')
source=("https://www.kernel.org/pub/linux/libs/security/linux-privs/libcap2/${pkgname}-${pkgver}.tar.xz")
sha256sums=('c6088de41e1c97fa8047e2e7de0e4ee0cd13e6cc16538022230ae76727a87c46')

build() {
  cd ${pkgname}-${pkgver}
  
  make GOLANG=no
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix=/usr lib=/lib DESTDIR=${pkgdir} RAISE_SETFCAP=no GOLANG=no install

  install -Dm644 pam_cap/capability.conf \
    $pkgdir/usr/share/doc/$pkgname/capability.conf.example
}
