
pkgname=libcap
pkgver=2.46
pkgrel=1
pkgdesc="POSIX 1003.1e capabilities"
arch=('x86_64')
url="https://sites.google.com/site/fullycapable/"
license=('GPL')
depends=('glibc' 'attr')
options=('!staticlibs')
source=("https://www.kernel.org/pub/linux/libs/security/linux-privs/libcap2/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4ed3d11413fa6c9667e49f819808fbb581cd8864b839f87d7c2a02c70f21d8b4')

build() {
  cd ${pkgname}-${pkgver}
  
  make GOLANG=no
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix=/usr lib=/lib DESTDIR=${pkgdir} RAISE_SETFCAP=no GOLANG=no install

  install -Dm644 pam_cap/capability.conf \
    $pkgdir/usr/share/doc/$pkgname/capability.conf.example
}
