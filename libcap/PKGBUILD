
pkgname=libcap
pkgver=2.69
pkgrel=1
pkgdesc="POSIX 1003.1e capabilities"
arch=('x86_64')
url="https://sites.google.com/site/fullycapable/"
license=('GPL')
depends=('glibc' 'pam')
options=('!staticlibs')
source=("https://www.kernel.org/pub/linux/libs/security/linux-privs/libcap2/${pkgname}-${pkgver}.tar.xz")
sha256sums=('f311f8f3dad84699d0566d1d6f7ec943a9298b28f714cae3c931dfd57492d7eb')

build() {
  cd ${pkgname}-${pkgver}

  make GOLANG=no
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix=/usr lib=/lib DESTDIR=${pkgdir} RAISE_SETFCAP=no GOLANG=no install

  install -Dm644 pam_cap/capability.conf \
    $pkgdir/usr/share/doc/$pkgname/capability.conf.example
}
