
pkgname=perl-yaml-syck
_pkgname=YAML-Syck
pkgver=1.34
_pkgver=1.34
pkgrel=1
pkgdesc="Fast, lightweight YAML loader and dumper"
arch=('x86_64')
license=('custom')
url="https://metacpan.org/release/YAML-Syck"
depends=('perl')
checkdepends=('perl-test-pod')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/T/TO/TODDR/${_pkgname}-${_pkgver}.tar.gz")
md5sums=('4aae72f5c06dccd6ed6ea9b029104183')

build() {
  cd ${_pkgname}-${_pkgver}

  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_pkgname}-${_pkgver}
  
  make test
}

package() {
  cd ${_pkgname}-${_pkgver}
  make install DESTDIR=${pkgdir}

  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
