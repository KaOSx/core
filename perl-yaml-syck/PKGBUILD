
pkgname=perl-yaml-syck
_pkgname=YAML-Syck
pkgver=1.31
_pkgver=1.31
pkgrel=2
pkgdesc="Fast, lightweight YAML loader and dumper"
arch=('x86_64')
license=('custom')
url="http://search.cpan.org/dist/${_pkgname}/"
depends=('perl')
checkdepends=('perl-test-pod')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/T/TO/TODDR/${_pkgname}-${_pkgver}.tar.gz")
md5sums=('2394bccf64c01cdc796cd5227bba237f')

build() {
  cd ${_pkgname}-${_pkgver}

  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_pkgname}-${_pkgver}
  
  make test
}

package() {
  cd ${_pkgname}-${_pkgver}
  make install DESTDIR=${pkgdir}

  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
