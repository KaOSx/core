
pkgname=perl-yaml-syck
_realname=YAML-Syck
pkgver=1.28.1
_pkgver=1.28_01
pkgrel=2
pkgdesc="Fast, lightweight YAML loader and dumper"
arch=('x86_64')
license=('custom')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl')
checkdepends=('perl-test-pod')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/T/TO/TODDR/${_realname}-$_pkgver.tar.gz")
md5sums=('426c6777706fd4b0cbb0c1ea03db7e28')

build() {
  cd "${srcdir}/${_realname}-$_pkgver"

  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-$_pkgver"
  
  make test
}

package() {
  cd "${srcdir}/${_realname}-$_pkgver"
  make install DESTDIR="${pkgdir}"

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
