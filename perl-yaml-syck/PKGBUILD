
pkgname=perl-yaml-syck
_realname=YAML-Syck
pkgver=1.27
pkgrel=1
pkgdesc="Fast, lightweight YAML loader and dumper"
arch=('x86_64')
license=('custom')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/T/TO/TODDR/${_realname}-$pkgver.tar.gz")
md5sums=('8920091e68a078cfa9c42041e5759162')

build() {
  cd "${srcdir}/${_realname}-$pkgver"

  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-$pkgver"
  
  make test
}

package() {
  cd "${srcdir}/${_realname}-$pkgver"
  make install DESTDIR="${pkgdir}"

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
