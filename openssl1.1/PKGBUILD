
pkgname=openssl1.1
_pkgname=openssl
pkgver=1.1.1t
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security, legacy 1.1 version'
arch=('x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=('perl' 'zlib')
optdepends=('ca-certificates')
options=('!makeflags')
backup=('etc/ssl/openssl.cnf')
source=("https://www.openssl.org/source/${_pkgname}-${pkgver}.tar.gz"
        'ca-dir_openssl1.1.patch')
sha256sums=('8dee9b24bdb1dcbf0c3d1e9b02fb8f6bf22165e807f45adeb7c9677536859d3b'
            'd6dd76398e67f07640e49e7a6b0f0585801dcd6a031b72bf4d19938562512c4d')

build() {
    cd ${_pkgname}-${pkgver}

    # set ca dir to /etc/ssl by default
    patch -p0 -i ${srcdir}/ca-dir_openssl1.1.patch

    ./Configure --prefix=/usr \
        --openssldir=/etc/ssl \
        --libdir=lib/openssl1.1 \
        shared zlib enable-ec_nistp_64_gcc_128 \
        linux-x86_64 \
        -Wa,--noexecstack ${CFLAGS} ${LDFLAGS} \

    make depend
    make
}

check() {
    cd ${_pkgname}-${pkgver}
    # the test fails due to missing write permissions in /etc/ssl
    # revert this patch for make test
    patch -p0 -R -i ${srcdir}/ca-dir_openssl1.1.patch
    # /test/recipes/30-test_afalg.t                  (Wstat: 256 Tests: 1 Failed: 1)
    make test
    patch -p0 -i ${srcdir}/ca-dir_openssl1.1.patch
}

package() {
    cd ${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install_sw
    
    # make it compatible with regular openssl install
    install -m755 -d ${pkgdir}/usr/include/openssl1.1

    mv ${pkgdir}/usr/include/openssl ${pkgdir}/usr/include/openssl1.1/
    mv ${pkgdir}/usr/lib/openssl1.1/libcrypto.so.1.1 ${pkgdir}/usr/lib/
    mv ${pkgdir}/usr/lib/openssl1.1/libssl.so.1.1 ${pkgdir}/usr/lib/

    ln -sf ../libssl.so.1.1 ${pkgdir}/usr/lib/openssl1.1/libssl.so
    ln -sf ../libcrypto.so.1.1 ${pkgdir}/usr/lib/openssl1.1/libcrypto.so

    mv ${pkgdir}/usr/bin/openssl ${pkgdir}/usr/bin/openssl1.1

    sed -e 's|/include$|/include/openssl1.1|' -i ${pkgdir}/usr/lib/openssl1.1/pkgconfig/*.pc
    
    rm -rf ${pkgdir}/etc
    rm -rf ${pkgdir}/usr/bin/c_rehash

    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
