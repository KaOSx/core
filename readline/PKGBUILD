
pkgname=readline
_basever=8.2
_patchlevel=013 #prepare for some patches
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc="GNU readline library"
arch=('x86_64')
url="https://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
depends=('glibc' 'ncurses')
backup=('etc/inputrc')
options=('!emptydirs')
source=("https://ftp.gnu.org/gnu/readline/readline-${_basever}.tar.gz"
        'inputrc')
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} https://ftp.gnu.org/gnu/readline/readline-${_basever}-patches/readline${_basever//./}-$(printf "%03d" $p))
    done
fi
md5sums=('4aa1b31be779e6b84f9a96cb66bc50f6'
         '58d54966c1191db45973cb3191ac621a'
         'b800ce35690436a995a4fadf939e53cc'
         '22dcbd9d174166bc0cfbd4a6957603a0'
         '94a336980c337d7bf54c0191988055f1'
         'dabdab80dc147cffc6d5ef275b6daf67'
         '9e847d8b5243c364138ad9475de8c9b9'
         '8812592f2df5ce441b61b92fb63dc91f'
         'e57991bb85e2f025119ba78dde994665'
         '2a61c084c3867d85b99811d14a181f1d'
         'f5980a8a847f8c31c8ba4781562408db'
         'e3a188e5425bd2e17b32bd8626bfed16'
         '7b82c14952568066a69fc0e1625c8760'
         '8777aeadd188a6ee89a7a51bd412c288'
         '35a23c3e1d3305915f0b90e840dc49db')

build() {
  cd ${pkgname}-${_basever}
  for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
    msg "applying patch readline${_basever//./}-$(printf "%03d" $p)"
    patch -p0 -i ${srcdir}/readline${_basever//./}-$(printf "%03d" $p)
  done

  # Remove RPATH from shared objects
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf

  CFLAGS="$CFLAGS -fPIC"

  ./configure --prefix=/usr 
  make SHLIB_LIBS=-lncurses
}

package() {
  cd ${pkgname}-${_basever}
  make DESTDIR=${pkgdir} install

  install -Dm644 ../inputrc ${pkgdir}/etc/inputrc
}

