
pkgname=git
pkgver=2.6.2
pkgrel=1
pkgdesc="Git is a free and open source distributed version control system designed to handle everything from small to very large projects with speed and efficiency."
arch=('x86_64')
url="http://git-scm.com/"
license=('GPL2')
depends=('curl' 'expat>=2.0' 'perl-error' 'perl' 'pcre')
makedepends=('python2' 'emacs')
optdepends=('tk: gitk and git gui'
            'perl-libwww: git svn'
            'perl-term-readkey: git svn'
            'perl-net-smtp-ssl: git send-email TLS support'
            'python2: various helper scripts'
            'subversion: git svn'
            'cvsps: git cvsimport')
#install="git.install"
source=("https://www.kernel.org/pub/software/scm/git/git-${pkgver}.tar.xz"
        "https://www.kernel.org/pub/software/scm/git/git-manpages-${pkgver}.tar.xz"
        'git-daemon@.service'
        'git-daemon.socket')
md5sums=('32ae5ad29763fc927bfcaeab55385fd9'
         '0c0c1446e166ea59a55e7eec8e9135c2'
         '042524f942785772d7bd52a1f02fe5ae'
         'f67869315c2cc112e076f0c73f248002')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make prefix=/usr gitexecdir=/usr/lib/git-core \
       USE_LIBPCRE=1 \
       NO_CROSS_DIRECTORY_HARDLINKS=1 \
       all

  cd contrib/emacs
  make prefix=/usr
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  local jobs="$(expr "$MAKEFLAGS" : '.*\(-j[0-9]*\).*')"
  jobs='-j1'
  mkdir -p /dev/shm/git-test
  
  make prefix=/usr gitexecdir=/usr/lib/git-core \
       USE_LIBPCRE=1 \
       NO_CROSS_DIRECTORY_HARDLINKS=1 \
       NO_SVN_TESTS=y \
       DEFAULT_TEST_TARGET=prove \
       GIT_PROVE_OPTS="${jobs} -Q" \
       GIT_TEST_OPTS="--root=/dev/shm/git-test" \
       test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix=/usr gitexecdir=/usr/lib/git-core \
       USE_LIBPCRE=1 \
       NO_CROSS_DIRECTORY_HARDLINKS=1 \
       INSTALLDIRS=vendor DESTDIR="${pkgdir}" install 

  # bash completion
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions/"
  install -m644 ./contrib/completion/git-completion.bash "${pkgdir}/usr/share/bash-completion/completions/git"
  # git prompt
  mkdir -p "${pkgdir}/usr/share/git/"
  install -m644 ./contrib/completion/git-prompt.sh "${pkgdir}/usr/share/git/git-prompt.sh"
  # more contrib stuff
  cp -a ./contrib/* "${pkgdir}/usr/share/git/"

  # emacs interface
  cd contrib/emacs
  make prefix=/usr DESTDIR="${pkgdir}" install

  # manpages
  for mansect in man1 man5 man7; do
    for manpage in "${srcdir}/${mansect}"/*; do
      install -D -m644 "${manpage}" "${pkgdir}/usr/share/man/${mansect}/$(basename ${manpage})"
    done
  done

  # remove perllocal.pod, .packlist, and empty directories.
  rm -rf "${pkgdir}/usr/lib/perl5"

  # systemd 
  install -D -m644 "${srcdir}/git-daemon@.service" "${pkgdir}/usr/lib/systemd/system/git-daemon@.service"
  install -D -m644 "${srcdir}/git-daemon.socket" "${pkgdir}/usr/lib/systemd/system/git-daemon.socket"
}

