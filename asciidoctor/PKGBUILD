
pkgname=asciidoctor
pkgver=2.0.23
_rubyver=3.3.0
pkgrel=3
pkgdesc='Text processor and publishing toolchain for converting AsciiDoc content to HTML5, DocBook, PDF, and other formats.'
arch=('x86_64')
url='https://asciidoctor.org'
license=('MIT')
depends=('ruby')
source=("https://rubygems.org/downloads/${pkgname}-${pkgver}.gem")
noextract=("${pkgname}-${pkgver}.gem")
sha256sums=('52208807f237dfa0ca29882f8b13d60b820496116ad191cf197ca56f2b7fddf3')

package() {
    gem install --ignore-dependencies --no-user-install -V \
        -i ${pkgdir}/usr/lib/ruby/gems/${_rubyver}/ -n ${pkgdir}/usr/bin \
        ${pkgname}-${pkgver}.gem

    install -Dm644 ${pkgdir}/usr/lib/ruby/gems/${_rubyver}/gems/${pkgname}-${pkgver}/LICENSE \
        ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
        
    install -Dm644 ${pkgdir}/usr/lib/ruby/gems/${_rubyver}/gems/${pkgname}-${pkgver}/man/asciidoctor.1 \
        ${pkgdir}/usr/share/man/man1/asciidoctor.1

    rm ${pkgdir}/usr/lib/ruby/gems/${_rubyver}/cache/${pkgname}-${pkgver}.gem
    rm -rf ${pkgdir}/usr/lib/ruby/gems/${_rubyver}/gems/${pkgname}-${pkgver}/man
}
