
pkgname=gawk
pkgver=4.1.3
pkgrel=1
pkgdesc="The GNU awk utility interprets a special-purpose programming language that makes it possible to handle simple data-reformatting jobs with just a few lines of code."
arch=('x86_64')
url="http://www.gnu.org/software/gawk/gawk.html"
license=('GPL')
groups=('base')
provides=('awk')
install="gawk.install"
depends=('sh' 'glibc' 'mpfr')
makedepends=('gzip')
source=("ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('97f8f44149ea9b9e94be97f68988be87')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
       --libexecdir=/usr/lib \
       --without-libsigsegv
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make check
}


package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make install DESTDIR="${pkgdir}"
}

