
pkgname=mkinitcpio-busybox
pkgver=1.34.1
pkgrel=2
pkgdesc="Base initramfs tools"
arch=('x86_64')
url="https://www.busybox.net/"
license=('GPL')
depends=('glibc' 'libxcrypt')
options=('!buildflags')
source=("https://busybox.net/downloads/busybox-${pkgver}.tar.bz2"
        'config')
md5sums=('5ad7368a73d12b8c4f8881bf7afb3d82'
         '3a63521b1c5c5f397274730050155a24')

build() {
  cd busybox-${pkgver}

  local safeflags="${CARCH/_/-} -mtune=generic -Os -pipe -fno-strict-aliasing"
  sed 's|^\(CONFIG_EXTRA_CFLAGS\)=.*|\1="-march='"$safeflags"'"|' ${srcdir}/config > .config

  make
}

package() {
  install -Dm755 ${srcdir}/busybox-${pkgver}/busybox ${pkgdir}/usr/lib/initcpio/busybox
}

