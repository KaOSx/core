
pkgname=serf
pkgver=1.3.8
pkgrel=1
pkgdesc="High-performance asynchronous HTTP client library"
url="http://code.google.com/p/serf/"
arch=('x86_64')
license=('Apache')
depends=('apr' 'apr-util' 'openssl' 'zlib')
makedepends=('scons')
options=('!libtool')
source=("http://serf.googlecode.com/svn/src_releases/serf-${pkgver}.tar.bz2")
md5sums=('2e4efe57ff28cb3202a112e90f0c2889')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  scons PREFIX=/usr GSSAPI=/usr/bin/krb5-config
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  scons check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d "${pkgdir}/usr"
  
  scons PREFIX="${pkgdir}/usr" install
}
