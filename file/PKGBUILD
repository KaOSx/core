
pkgname=file
pkgver=5.33
pkgrel=2
pkgdesc="File type identification utility"
arch=('x86_64')
license=('custom')
groups=('base')
url="https://www.darwinsys.com/file/"
depends=('glibc' 'zlib')
options=('!libtool')
source=("ftp://ftp.astron.com/pub/file/${pkgname}-${pkgver}.tar.gz")
md5sums=('bbe6db96e3a9ca9554dce647390540ef')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --datadir=/usr/share/file \
    --disable-libseccomp
  make 
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
  
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

