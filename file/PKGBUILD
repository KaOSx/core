#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>


pkgname=file
pkgver=5.10
pkgrel=1
pkgdesc="File type identification utility"
arch=('i686' 'x86_64')
license=('custom')
groups=('base')
url="http://www.darwinsys.com/file/"
depends=('glibc' 'zlib')
options=('!libtool')
source=("ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('4cea34b087b060772511e066e2038196')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's#\$(datadir)/misc#\$(datadir)#' configure 
  ./configure --prefix=/usr --datadir=/usr/share/file
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/misc"
  ln -s ../file/magic.mgc "${pkgdir}/usr/share/misc"
  
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim:set ts=2 sw=2 et:
