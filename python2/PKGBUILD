
pkgname=python2
pkgver=2.7.18
pkgrel=1
_pybasever=2.7
pkgdesc="A high-level scripting language"
arch=('x86_64')
license=('PSF')
url="https://www.python.org"
depends=('db' 'bzip2' 'gdbm' 'openssl' 'zlib' 'expat' 'sqlite' 'libffi')
makedepends=('tk')
source=("https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz")
md5sums=('fd6cc8ec0a78c44036f825e739f36e5a')

build() {
  cd Python-${pkgver}

  # http://bugs.python.org/issue10835 
  sed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c

  # Use built-in SQLite module  
  sed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py

  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python2|" Lib/cgi.py

  # Use system copies of various libraries (expat, zlib and libffi)
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  export OPT="${CFLAGS}"
 ./configure --prefix=/usr \
             --enable-shared \
             --enable-ipv6 \
             --enable-unicode=ucs4 \
             --with-threads \
             --with-system-ffi \
             --with-dbmliborder=gdbm:ndbm \
             --with-system-expat
  
  make 
}

package() {
  cd Python-${pkgver}
  make DESTDIR=${pkgdir} altinstall maninstall

  ln -sf python${_pybasever}        ${pkgdir}/usr/bin/python2
  ln -sf python${_pybasever}-config ${pkgdir}/usr/bin/python2-config
  ln -sf python${_pybasever}.1      ${pkgdir}/usr/share/man/man1/python2.1

  ln -sf ../../libpython${_pybasever}.so \
    ${pkgdir}/usr/lib/python${_pybasever}/config/libpython${_pybasever}.so

  mv ${pkgdir}/usr/bin/smtpd.py ${pkgdir}/usr/lib/python${_pybasever}

  install -dm755 ${pkgdir}/usr/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py \
    ${pkgdir}/usr/lib/python${_pybasever}/Tools/i18n
  install -m755 Tools/scripts/{README,*py} \
    ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts

  # clean up #!s
  find ${pkgdir}/usr/lib/python${_pybasever}" -name '*.py' | \
    xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python2|

  # clean-up reference to build directory
  sed -i "s|${srcdir}/Python-${pkgver}:||" \
    ${pkgdir}/usr/lib/python${_pybasever}/config/Makefile
    
  # Conflicts with python3 
  rm ${pkgdir}/usr/bin/2to3

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}  

