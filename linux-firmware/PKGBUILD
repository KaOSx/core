
pkgname=linux-firmware
pkgver=20240909
_b43=5.100.138
_legacy=3.130.20.0
pkgrel=1
pkgdesc="Firmware files for Linux"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git"
license=('GPL2' 'GPL3' 'custom')
makedepends=('b43-fwcutter' 'rdfind')
options=(!strip emptydirs)
source=("https://mirrors.edge.kernel.org/pub/linux/kernel/firmware/${pkgname}-${pkgver}.tar.xz"
        "http://mirror2.openwrt.org/sources/broadcom-wl-${_b43}.tar.bz2"
        "http://downloads.openwrt.org/sources/wl_apsta-${_legacy}.o")
sha256sums=('943fbd19883cf8eadf89e0b22422549db056557b1ecd30a56400615971369671'
            'f1e7067aac5b62b67b8b6e4c517990277804339ac16065eb13c731ff909ae46f'
            '7dba610b1d96dd14e901bcbce14cd6ecd1b1ac6f5c0035b0d6b6dc46a7c3ef90')

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -Dm644 LICEN* WHENCE ${pkgdir}/usr/share/licenses/linux-firmware/
  # arm firmware is useless
  rm ${pkgdir}/lib/firmware/mrvl/prestera/mvsw_prestera_fw_arm64-v4.1.img

  # install b43-firmware
  b43-fwcutter -w ${pkgdir}/lib/firmware/ ${srcdir}/broadcom-wl-${_b43}/linux/wl_apsta.o

  # install b43-legacy-firmware
  b43-fwcutter -w ${pkgdir}/lib/firmware/ ${srcdir}/wl_apsta-${_legacy}.o
}
