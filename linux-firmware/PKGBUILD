#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dog]org>
# maintainer Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=linux-firmware
pkgver=20130221
_b43=5.100.138
_legacy=3.130.20.0
pkgrel=1
pkgdesc="Firmware files for Linux"
arch=('any')
url="http://git.kernel.org/?p=linux/kernel/git/dwmw2/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
makedepends=('b43-fwcutter')
provides=('kernel26-firmware=3.1.99' 'linux-firmware=3.1.99')
conflicts=('linux-firmware-git'
           'kernel26-firmware'
           'ar9170-fw'
           'carl9170-fw'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw'
           'b43-firmware'
           'b43-firmware-legacy')
replaces=('kernel26-firmware'
           'ar9170-fw'
           'carl9170'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw'
           'b43-firmware'
           'b43-firmware-legacy')
options=(!strip emptydirs)
source=("http://chakra.sourceforge.net/sources/linux-firmware/$pkgname-$pkgver.tar.xz"
        "http://mirror2.openwrt.org/sources/broadcom-wl-${_b43}.tar.bz2"
        "http://downloads.openwrt.org/sources/wl_apsta-${_legacy}.o")
sha256sums=('e1e47de6b9eb101994d269794ba8cb640e9ac01e15fa5c8e9c4f3ac22a07325f'
            'f1e7067aac5b62b67b8b6e4c517990277804339ac16065eb13c731ff909ae46f'
            '7dba610b1d96dd14e901bcbce14cd6ecd1b1ac6f5c0035b0d6b6dc46a7c3ef90')

# create tarball: source PKGBUILD && mksource

mksource() {
	  rm -vRf linux-firmware*
	  git clone git://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git
	  pushd ${pkgname}
          git archive --format=tar --prefix linux-firmware-$(date +%Y%m%d)/ master | xz -9 > ../linux-firmware-$(date +%Y%m%d).tar.xz
	  popd
	  sha256sum linux-firmware-$(date +%Y%m%d).tar.xz
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -d -m755 "${pkgdir}/usr/lib/firmware"
  cp -a * "${pkgdir}/usr/lib/firmware/"
  install -d -m755 "${pkgdir}/usr/share/licenses/linux-firmware/"
  rm -f "${pkgdir}/usr/lib/firmware/GPL*"
  mv "${pkgdir}/usr/lib/firmware"/{LICEN*,WHENCE} "${pkgdir}/usr/share/licenses/linux-firmware/"

  # install b43-firmware
  b43-fwcutter -w "${pkgdir}/usr/lib/firmware/" ${srcdir}/broadcom-wl-${_b43}/linux/wl_apsta.o

  # install b43-legacy-firmware
  b43-fwcutter -w "${pkgdir}/usr/lib/firmware/" ${srcdir}/wl_apsta-${_legacy}.o
}
