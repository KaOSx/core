
pkgname=kmod
pkgver=27
pkgrel=1
#_kver=4.0.5-1
pkgdesc="Linux kernel module handling"
arch=('x86_64')
url='https://git.kernel.org/pub/scm/utils/kernel/kmod/kmod.git/tree/README'
license=('GPL2')
depends=('glibc' 'zlib')
options=('!libtool')
source=("https://www.kernel.org/pub/linux/utils/kernel/kmod/${pkgname}-${pkgver}.tar.xz"
        'depmod-search.conf')
sha256sums=('c1d3fbf16ca24b95f334c1de1b46f17bbe5a10b0e81e72668bdc922ebffbbc0c'
            '884bf2246f9cd42a4dce9886153d91a7cdfb0dcabcb0f293d31cb33077be76a4')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --sysconfdir=/etc \
    --with-openssl \
    --with-xz \
    --with-zlib \
    --disable-test-modules
  make
}

check() {
  make -C ${pkgname}-${pkgver} check 
}

package() {
  make -C${pkgname}-${pkgver} DESTDIR=${pkgdir} install

  # extra directories
  install -dm755 ${pkgdir}/{etc,usr/lib}/{depmod,modprobe}.d ${pkgdir}/sbin
  
  # add symlinks to kmod
  ln -s ../usr/bin/kmod ${pkgdir}/sbin/modprobe
  ln -s ../usr/bin/kmod ${pkgdir}/sbin/depmod

  for tool in {ins,ls,rm,dep}mod mod{probe,info}; do
    ln -s kmod ${pkgdir}/usr/bin/$tool
  done

  # install depmod.d file for search/ dir
  install -Dm644 ${srcdir}/depmod-search.conf ${pkgdir}/usr/lib/depmod.d/search.conf
}
