
pkgname=kmod
pkgver=32
pkgrel=1
#_kver=4.0.5-1
pkgdesc="Linux kernel module handling"
arch=('x86_64')
url='https://git.kernel.org/pub/scm/utils/kernel/kmod/kmod.git'
license=('GPL2')
depends=('glibc' 'openssl' 'zlib' 'zstd')
options=('!libtool')
source=("https://www.kernel.org/pub/linux/utils/kernel/kmod/${pkgname}-${pkgver}.tar.xz"
        'depmod-search.conf')
sha256sums=('630ed0d92275a88cb9a7bf68f5700e911fdadaf02e051cf2e4680ff8480bd492'
            '884bf2246f9cd42a4dce9886153d91a7cdfb0dcabcb0f293d31cb33077be76a4')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --sysconfdir=/etc \
    --with-openssl \
    --with-xz \
    --with-zlib \
    --with-zstd \
    --disable-test-modules
  make
}

check() {
  # 1 fail FAIL: testsuite/test-util
  make -C ${pkgname}-${pkgver} check 
}

package() {
  make -C${pkgname}-${pkgver} DESTDIR=${pkgdir} install

  # extra directories
  install -dm755 ${pkgdir}/{etc,usr/lib}/{depmod,modprobe}.d ${pkgdir}/sbin
  
  # add symlinks to kmod
  ln -s ../usr/bin/kmod ${pkgdir}/sbin/modprobe
  ln -s ../usr/bin/kmod ${pkgdir}/sbin/depmod

  #for tool in {ins,ls,rm,dep}mod mod{probe,info}; do
  #  ln -s kmod ${pkgdir}/usr/bin/$tool
  #done

  # install depmod.d file for search/ dir
  install -Dm644 ${srcdir}/depmod-search.conf ${pkgdir}/usr/lib/depmod.d/search.conf
}
