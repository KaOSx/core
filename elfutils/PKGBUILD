
pkgname=elfutils
pkgver=0.188
pkgrel=1
pkgdesc="Collection of libraries and utilities for working with ELF object files and DWARF debugging information."
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
source=("https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2")
sha512sums=('585551b2d937d19d1becfc2f28935db1dd1a3d25571a62f322b70ac8da98c1a741a55d070327705df6c3e2ee026652e0b9a3c733b050a0b0ec5f2fc75d5b74b5')

build() {
  cd ${pkgname}-${pkgver}

  # needed for check()
  CFLAGS+=" -g"

  ./configure --prefix=/usr \
    --program-prefix="eu-" \
    --disable-debuginfod
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # no FAIL
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR=${pkgdir}
}


