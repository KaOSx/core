
pkgname=elfutils
pkgver=0.191
pkgrel=2
pkgdesc="Collection of libraries and utilities for working with ELF object files and DWARF debugging information."
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
source=("https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2")
sha512sums=('e22d85f25317a79b36d370347e50284c9120c86f9830f08791b7b6a7b4ad89b9bf4c7c71129133b8d193a0edffb2a2c17987b7e48428b9670aff5ce918777e04')

build() {
  cd ${pkgname}-${pkgver}

  # needed for check()
  CFLAGS+=" -g"

  ./configure --prefix=/usr \
    --program-prefix="eu-" \
    --disable-debuginfod
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # FAIL: run-srcfiles-self.sh
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR=${pkgdir}
}


