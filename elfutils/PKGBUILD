
pkgname=elfutils
pkgver=0.190
pkgrel=2
pkgdesc="Collection of libraries and utilities for working with ELF object files and DWARF debugging information."
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
source=("https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2")
sha512sums=('9c4f5328097e028286c42f29e39dc3d80914b656cdfbbe05b639e91bc787ae8ae64dd4d69a6e317ce30c01648ded10281b86a51e718295f4c589df1225a48102')

build() {
  cd ${pkgname}-${pkgver}

  # needed for check()
  CFLAGS+=" -g"

  ./configure --prefix=/usr \
    --program-prefix="eu-" \
    --disable-debuginfod
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # FAIL: run-srcfiles-self.sh
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR=${pkgdir}
}


