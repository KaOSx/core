
pkgname=elfutils
pkgver=0.187
pkgrel=1
pkgdesc="Collection of libraries and utilities for working with ELF object files and DWARF debugging information."
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
source=("https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2")
sha512sums=('a9b9e32b503b8b50a62d4e4001097ed2721d3475232a6380e6b9853bd1647aec016440c0ca7ceb950daf1144f8db9814ab43cf33cc0ebef7fc91e9e775c9e874')

build() {
  cd ${pkgname}-${pkgver}

  # needed for check()
  CFLAGS+=" -g"

  ./configure --prefix=/usr \
    --program-prefix="eu-" \
    --disable-debuginfod
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # no FAIL
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR=${pkgdir}
}


