
pkgname=elfutils
pkgver=0.181
pkgrel=1
pkgdesc="Collection of libraries and utilities for working with ELF object files and DWARF debugging information."
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
source=("https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2")
sha512sums=('d565541d5817f409dc89ebb1ee593366f69c371a1531308eeb67ff934b14a0fab0c9009fd7c23240efbaa1b4e04edac5c425e47d80e3e66ba03dcaf000afea36')

build() {
  cd ${pkgname}-${pkgver}

  CFLAGS+=" -g"
  
  ./configure --prefix=/usr \
    --program-prefix="eu-" \
    --disable-debuginfod
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # 1 test FAIL: run-reverse-sections-self.sh 
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make install DESTDIR=${pkgdir}

}


