
pkgname=elfutils
pkgver=0.185
pkgrel=1
pkgdesc="Collection of libraries and utilities for working with ELF object files and DWARF debugging information."
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
source=("https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2")
sha512sums=('34de0de1355b11740e036e0fc64f2fc063587c8eb121b19216ee5548d3f0f268d8fc3995176c47190466b9d881007cfa11a9d01e9a50e38af6119492bf8bb47f')

build() {
  cd ${pkgname}-${pkgver}

  CFLAGS+=" -g"
  
  ./configure --prefix=/usr \
    --program-prefix="eu-" \
    --disable-debuginfod
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # 1 test FAIL: run-reverse-sections-self.sh 
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make install DESTDIR=${pkgdir}

}


