
pkgname=automake
pkgver=1.16.2
pkgrel=1
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('x86_64')
license=('GPL')
url="https://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
makedepends=('autoconf')
checkdepends=('dejagnu' 'python3' 'java-environment' 'vala' 'emacs-nox' 'gcc-fortran')
source=("https://ftp.gnu.org/gnu/automake/${pkgname}-${pkgver}.tar.xz")
md5sums=('6cb234c86f3f984df29ce758e6d0d1d7')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr
  make
}

check(){
  cd ${pkgname}-${pkgver}
  # see http://www.linuxfromscratch.org/lfs/view/development/chapter06/automake.html
  #sed -i "s:./configure:LEXLIB=/usr/lib/libfl.a &:" t/lex-{clean,depend}-cxx.sh
 
  make check PYTHON=/usr/bin/python3
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}

