
pkgname=pciutils
pkgver=3.9.0
pkgrel=1
pkgdesc="PCI bus configuration space access library and tools"
arch=('x86_64')
license=('GPL2')
groups=('base')
url="https://mj.ucw.cz/pciutils.html"
depends=('glibc' 'hwids' 'kmod' 'sh')
makedepends=('wget')
source=("https://mj.ucw.cz/download/linux/pci/${pkgname}-${pkgver}.tar.gz")
sha256sums=('8953a785b2e3af414434b8fdcbfb75c90758819631001e60dd3afb89b22b2331')

build() {
  cd ${pkgname}-${pkgver}

  ./update-pciids.sh

  make OPT="${CFLAGS} -fPIC -DPIC" ZLIB=no SHARED=no PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man all
  cp lib/libpci.a ${srcdir}/
  make clean
  make OPT="${CFLAGS}" ZLIB=no SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man all
}

package() {
  cd ${pkgname}-${pkgver}
  make SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man DESTDIR=${pkgdir} install install-lib

  install -m644 ${srcdir}/libpci.a ${pkgdir}/usr/lib/
  # provided by the hwdata package
  rm -rf $pkgdir/usr/share/hwdata
}
