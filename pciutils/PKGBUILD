
pkgname=pciutils
pkgver=3.11.1
pkgrel=1
pkgdesc="PCI bus configuration space access library and tools"
arch=('x86_64')
license=('GPL2')
groups=('base')
url="https://mj.ucw.cz/pciutils.html"
depends=('glibc' 'hwdata' 'kmod' 'sh')
makedepends=('wget')
source=("https://mj.ucw.cz/download/linux/pci/${pkgname}-${pkgver}.tar.gz")
sha256sums=('1904864ce5b0272d0a2e42e72ceac9e8810d1898480567f36b70642f8205fbfd')

build() {
  cd ${pkgname}-${pkgver}

  ./update-pciids.sh

  make OPT="${CFLAGS} -fPIC -DPIC" ZLIB=no SHARED=no PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man all
  cp lib/libpci.a ${srcdir}/
  make clean
  make OPT="${CFLAGS}" ZLIB=no SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man all
}

package() {
  cd ${pkgname}-${pkgver}
  make SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man DESTDIR=${pkgdir} install install-lib

  install -m644 ${srcdir}/libpci.a ${pkgdir}/usr/lib/
  # provided by the hwdata package
  rm -rf $pkgdir/usr/share/hwdata
}
