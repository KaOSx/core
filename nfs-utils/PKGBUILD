
pkgname=nfs-utils
pkgver=2.8.1
pkgrel=1
pkgdesc="Support programs for Network File Systems"
arch=('x86_64')
url='https://nfs.sourceforge.net'
license=('GPL')
backup=(etc/{exports,nfs.conf,nfsmount.conf})
depends=('glibc' 'e2fsprogs' 'rpcbind' 'libtirpc' 'libevent' 'device-mapper' 'sqlite' 'libldap')
makedepends=('pkgconfig' 'autoconf' 'automake' 'rpcsvc-proto')
source=("https://mirrors.edge.kernel.org/pub/linux/utils/nfs-utils/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'exports')
sha256sums=('a0be95b08396908048583fcf6b34a7c1ab49d53e8a12eb28673ccfd2eff0c505'
            '610715ed3daedc43b2536f541c7c57e138fb31eab5d837d9a6187a7403e30154')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --enable-nfsv4 \
    --enable-nfsv41 \
    --enable-gss \
    --enable-svcgss \
    --without-tcp-wrappers \
    --with-statedir=/var/lib/nfs \
    --enable-ipv6 \
    --sysconfdir=/etc \
    --enable-libmount-mount \
    --without-gssglue \
    --enable-mountconfig \
    --libexecdir=/usr/sbin
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -D -m 644 utils/mount/nfsmount.conf ${pkgdir}/etc/nfsmount.conf
  install -D -m 644 nfs.conf ${pkgdir}/etc/nfs.conf

  # systemd files
  for i in systemd/{*.service,*.mount,*.target}; do
    install -D -m 644 $i ${pkgdir}/usr/lib/systemd/system/$(basename $i)
  done

  install -d -m 644 ${pkgdir}/usr/share/doc/${pkgname}
  install -m 644 NEWS ${pkgdir}/usr/share/doc/${pkgname}/
  install -m 644 README ${pkgdir}/usr/share/doc/${pkgname}/

  install -D -m 644 ../exports ${pkgdir}/etc/exports
  install -D -m 644 utils/nfsidmap/id_resolver.conf ${pkgdir}/etc/request-key.d/id_resolver.conf

  mkdir ${pkgdir}/etc/exports.d
  mkdir -m 555 ${pkgdir}/var/lib/nfs/rpc_pipefs
  mkdir ${pkgdir}/var/lib/nfs/v4recovery
}

