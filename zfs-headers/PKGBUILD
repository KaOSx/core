
_extramodules=6.0.12-1
_kver="$(cat /lib/modules/extramodules-6.0/version)"

pkgname=zfs-headers
pkgver=2.1.7
pkgrel=2
pkgdesc="Kernel headers for the Zettabyte File System."
arch=('x86_64')
url="https://zfsonlinux.org/"
license=('CDDL')
depends=('kmod' 'linux' "zfs=${pkgver}")
makedepends=('linux-headers' 'elfutils')
source=("https://github.com/zfsonlinux/zfs/releases/download/zfs-${pkgver}/zfs-${pkgver}.tar.gz")
sha256sums=("6462e63e185de6ff10c64ffa6ed773201a082f9dd13e603d7e8136fcb4aca71b")

build() {
    cd zfs-${pkgver}

    #./autogen.sh
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --datadir=/usr/share \
        --libdir=/lib \
        --includedir=/usr/include \
        --with-udevdir=/usr/lib/udev \
        --libexecdir=/usr/lib \
        --with-config=kernel \
        --with-linux=/lib/modules/${_extramodules}/build \
        --with-linux-obj=/lib/modules/${_extramodules}/build
    make
}

package() {
    cd zfs-${pkgver}

    make DESTDIR=${pkgdir} install
    #remove references to the buildsystem
    sed -i "s|${srcdir}||" ${pkgdir}/usr/src/zfs-*/${_extramodules}/Module.symvers
    #remove modules
    rm -r ${pkgdir}/lib
    #remove kernel specific headers
    rm -r ${pkgdir}/usr/src/zfs-${pkgver}/${_extramodules}
    rm -r ${pkgdir}/usr/src/spl-${pkgver}/${_extramodules}

    install -D -m0644 LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}
}
