# NOTE: requires rebuilt with each new gcc version

pkgname=libtool
pkgver=2.4.7
pkgrel=6
pkgdesc="A generic library support script"
arch=('x86_64')
url="https://www.gnu.org/software/libtool"
license=('GPL')
options=('!libtool')
depends=('sed' 'grep' 'sh' 'tar' 'gcc=14.2.0') # Needed by libtoolize
makedepends=('help2man')
groups=('base-devel')
provides=('libltdl')
conflicts=('libltdl')
replaces=('libltdl')
source=("https://ftpmirror.gnu.org/libtool/${pkgname}-${pkgver}.tar.xz"
        'no-clean-gnulib.patch')
md5sums=('2fc0b6ddcd66a89ed6e45db28fa44232'
         '23c8ef65f19705239f1aa362f429e3ad')

build() {
  cd ${pkgbase}-${pkgver}
  # https://bugs.gentoo.org/527200  build failures with 2.4.3 in ocatve & groff
  #patch -p1 -i "${srcdir}/no-clean-gnulib.patch"

  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgbase}-${pkgver}
  # 66: Link order test                                 FAILED (link-order.at:106)
  # see https://src.fedoraproject.org/rpms/libtool/c/74058f9770441b4dd984abf471a00739f0dafaee?branch=rawhide
  # 169: Run tests with low max_cmd_len                 FAILED (cmdline_wrap.at:48)
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}



