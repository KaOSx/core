
pkgname=rsync
pkgver=3.3.0
pkgrel=2
pkgdesc="Open source utility that provides fast incremental file transfer. "
arch=('x86_64')
url="https://rsync.samba.org/"
license=('GPL3')
depends=('perl' 'lz4' 'openssl' 'xxhash' 'zstd')
backup=('etc/rsyncd.conf' 'etc/xinetd.d/rsync')
source=("https://download.samba.org/pub/rsync/src/${pkgname}-${pkgver}.tar.gz"
        'rsyncd.conf'
        'lchmod.diff')
md5sums=('f5c17f9c9164ef9e60d9d8c96b23da06'
         'bce64d122a8e0f86872a4a21a03bc7f3'
         '61e214c952ad6b7bc1e192717d4e0591')

build() {
	cd ${pkgname}-${pkgver}
	# from https://github.com/WayneD/rsync/commit/9dd62525f3b98d692e031f22c02be8f775966503
	# rsync call in linux-api-headers header_install exposed fail, does not fully fix
	#patch -p1 -i ${srcdir}/lchmod.diff

	./configure --prefix=/usr \
        --disable-debug
	make 
}

check() {
	cd ${pkgname}-${pkgver}

	make test
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install 

	install -Dm644 ../rsyncd.conf ${pkgdir}/etc/rsyncd.conf
	install -Dm644 packaging/lsb/rsync.xinetd ${pkgdir}/etc/xinetd.d/rsync
	install -Dm644 packaging/systemd/rsync.service ${pkgdir}/usr/lib/systemd/system/rsyncd.service
	install -Dm644 packaging/systemd/rsync.socket ${pkgdir}/usr/lib/systemd/system/rsyncd.socket
	install -Dm644 packaging/systemd/rsync@.service ${pkgdir}/usr/lib/systemd/system/rsyncd@.service
	install -Dm755 support/rrsync ${pkgdir}/usr/lib/rsync/rrsync
}

