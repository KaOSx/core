#
# maintainer: abveritas@chakra-project.org

pkgname=systemd
pkgver=201
pkgrel=1
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
depends=('acl' 'bash' 'glibc' 'glib2' 'kmod' 'hwids' 'util-linux' 'kbd' 'kmod' 'libcap' 'libxslt'
         'linux-api-headers' 'pam' 'xz' 'libgcrypt' 'dbus-core') 
makedepends=('docbook-xsl' 'gobject-introspection' 'gperf' 'intltool')
optdepends=('python2-gobject3: systemd-analyze'
            'dbus-python: systemd-analyze')
backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
        etc/dbus-1/system.d/org.freedesktop.hostname1.conf
        etc/dbus-1/system.d/org.freedesktop.login1.conf
        etc/dbus-1/system.d/org.freedesktop.locale1.conf
        etc/dbus-1/system.d/org.freedesktop.timedate1.conf
        etc/systemd/system.conf
        etc/systemd/user.conf
        etc/systemd/logind.conf
        etc/systemd/journald.conf
        etc/udev/udev.conf)
provides=("udev=$pkgver")
conflicts=('udev' 'sysvinit' 'initscripts' 'initscripts-systemd')
replaces=('udev')
groups=('base')
install="systemd.install"
options=('!libtool')
source=("http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz"
        'initcpio-hook-udev'
        'initcpio-install-udev'
        'initcpio-install-timestamp'
        'use-split-usr-path.patch'
        '99-default.preset'
        '98-chakra.rules'
        'is_bundle')
md5sums=('3e758392ff0e9206b3f7ee252b4a654b'
         'e99e9189aa2f6084ac28b8ddf605aeb8'
         'fb37e34ea006c79be1c54cbb0f803414'
         'df69615503ad293c9ddf9d8b7755282d'
         '21572b99173a7cdeaff965e0a5e9182b'
         'a1bf25da736d3719e9720b2e4caea341'
         '40f1d228553f939430e6d8d781f06daf'
         '05d26a1936bf5b075bedd76952ab1b53')

build() {
  cd "$pkgname-$pkgver"
  
  patch -p1 -i "$srcdir/use-split-usr-path.patch"

  ./configure \
      PYTHON=python2 \
      PYTHON_CONFIG=python2-config \
      --enable-static \
      --libexecdir=/usr/lib \
      --localstatedir=/var \
      --sysconfdir=/etc \
      --enable-introspection \
      --disable-gtk-doc \
      --disable-audit \
      --disable-ima \
      --with-firmware-path=/usr/lib/firmware/updates:/lib/firmware/updates:/usr/lib/firmware:/lib/firmware

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  printf "d /run/console 0755 root root\n" > "$pkgdir/usr/lib/tmpfiles.d/console.conf"

  install -dm755 "$pkgdir/bin"
  ln -s ../usr/lib/systemd/systemd "$pkgdir/bin/systemd"

  # Remove rpm dir
  rm -r "$pkgdir/etc/rpm"

  install -dm755 "$pkgdir/sbin"
  for tool in runlevel reboot shutdown poweroff halt telinit; do
    ln -s '/usr/bin/systemctl' "$pkgdir/sbin/$tool"
  done

  ln -s '../usr/lib/systemd/systemd' "$pkgdir/sbin/init"

  # the path to udevadm is hardcoded in some places
  install -d "$pkgdir/sbin"
  ln -s ../usr/bin/udevadm "$pkgdir/sbin/udevadm"

  # udevd is no longer udevd because systemd. why isn't udevadm now udevctl?
  ln -s ../lib/systemd/systemd-udevd "$pkgdir/usr/bin/udevd"

  # add back tmpfiles.d/legacy.conf
  install -m644 "systemd-$pkgver/tmpfiles.d/legacy.conf" "$pkgdir/usr/lib/tmpfiles.d"

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  sed -i 's#GROUP="dialout"#GROUP="uucp"#g;
          s#GROUP="tape"#GROUP="storage"#g;
          s#GROUP="cdrom"#GROUP="optical"#g' "$pkgdir"/usr/lib/udev/rules.d/*.rules

  # add mkinitcpio hooks
  install -Dm644 "$srcdir/initcpio-install-udev" "$pkgdir/usr/lib/initcpio/install/udev"
  install -Dm644 "$srcdir/initcpio-hook-udev" "$pkgdir/usr/lib/initcpio/hooks/udev"
  install -Dm644 "$srcdir/initcpio-install-timestamp" "$pkgdir/usr/lib/initcpio/install/timestamp"
  
  # Chakra specific .preset
  #install -Dm644 "$srcdir/99-default.preset" "$pkgdir/usr/lib/systemd/system-preset/99-default.preset"
  #hide bundles in Doplhin places
  install -m644 "$srcdir/98-chakra.rules" "$pkgdir/usr/lib/udev/rules.d/98-chakra.rules"
  install -m755 "$srcdir/is_bundle" "$pkgdir/usr/lib/udev/is_bundle"
  # Make journalctl persistent
  install -dm755 "$pkgdir/var/log/journal"
  sed -i -e 's/#SystemMaxUse=/SystemMaxUse=25M/' ${pkgdir}/etc/systemd/journald.conf
}


