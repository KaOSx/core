
pkgname=systemd
pkgver=216
pkgrel=1
pkgdesc="System and Session Manager"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
depends=('acl' 'bash' 'glibc' 'glib2' 'kmod' 'hwids' 'util-linux' 'kbd' 'libcap' 'libxslt'
         'linux-api-headers' 'pam' 'xz' 'libgcrypt' 'dbus') 
makedepends=('docbook-xsl' 'gobject-introspection' 'gperf' 'intltool' 'cryptsetup' 'python2' 'python2-lxml'
             'libmicrohttpd')
optdepends=('cryptsetup: required for encrypted block devices'
            'libmicrohttpd: systemd-journal-gatewayd')
backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
        etc/dbus-1/system.d/org.freedesktop.hostname1.conf
        etc/dbus-1/system.d/org.freedesktop.login1.conf
        etc/dbus-1/system.d/org.freedesktop.locale1.conf
        etc/dbus-1/system.d/org.freedesktop.machine1.conf
        etc/dbus-1/system.d/org.freedesktop.timedate1.conf
        etc/systemd/bootchart.conf
        etc/systemd/system.conf
        etc/systemd/user.conf
        etc/systemd/logind.conf
        etc/systemd/journald.conf
        etc/udev/udev.conf)
provides=("udev=$pkgver")
groups=('base')
install="systemd.install"
options=('!libtool')
source=("http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz"
        'initcpio-hook-udev'
        'initcpio-install-systemd'
        'initcpio-install-udev')
md5sums=('04fda588a04f549da0f397dce3ae6a39'
         '7a79eb3e4ce19906cfebfea75fb9f8bb'
         'dae619987dc95b721547a85ef36dfa91'
         'bde43090d4ac0ef048e3eaee8202a407')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --libexecdir=/usr/lib \
      --localstatedir=/var \
      --sysconfdir=/etc \
      --enable-introspection \
      --enable-compat-libs \
      --disable-gtk-doc \
      --disable-audit \
      --disable-ima \
      --disable-kdbus \
      --disable-networkd \
      --with-firmware-path=/usr/lib/firmware/updates:/lib/firmware/updates:/usr/lib/firmware:/lib/firmware

  make
}

check() {
  make -C "$pkgname-$pkgver" check || true
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  #printf "d /run/console 0755 root root\n" > "$pkgdir/usr/lib/tmpfiles.d/console.conf"

  install -dm755 "$pkgdir/bin"
  ln -s ../usr/lib/systemd/systemd "$pkgdir/bin/systemd"

  install -dm755 "$pkgdir/sbin"
  for tool in runlevel reboot shutdown poweroff halt telinit; do
    ln -s '/usr/bin/systemctl' "$pkgdir/sbin/$tool"
  done

  ln -s '../usr/lib/systemd/systemd' "$pkgdir/sbin/init"

  # the path to udevadm is hardcoded in some places
  install -d "$pkgdir/sbin"
  ln -s ../usr/bin/udevadm "$pkgdir/sbin/udevadm"

  # udevd is no longer udevd because systemd. why isn't udevadm now udevctl?
  ln -s ../lib/systemd/systemd-udevd "$pkgdir/usr/bin/udevd"

  # add back tmpfiles.d/legacy.conf
  install -m644 "systemd-$pkgver/tmpfiles.d/legacy.conf" "$pkgdir/usr/lib/tmpfiles.d"

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  sed -i 's#GROUP="dialout"#GROUP="uucp"#g;
          s#GROUP="tape"#GROUP="storage"#g;
          s#GROUP="cdrom"#GROUP="optical"#g' "$pkgdir"/usr/lib/udev/rules.d/*.rules
  sed -i 's/dialout/uucp/g;
          s/tape/storage/g;
          s/cdrom/optical/g' "$pkgdir"/usr/lib/sysusers.d/basic.conf
          
  # add mkinitcpio hooks
  install -Dm644 "$srcdir/initcpio-install-systemd" "$pkgdir/usr/lib/initcpio/install/systemd"
  install -Dm644 "$srcdir/initcpio-install-udev" "$pkgdir/usr/lib/initcpio/install/udev"
  install -Dm644 "$srcdir/initcpio-hook-udev" "$pkgdir/usr/lib/initcpio/hooks/udev"
  
  rm -r "$pkgdir/usr/lib/rpm"
  rm "$pkgdir/usr/lib/sysctl.d/50-coredump.conf"
  
  # Make journalctl persistent
  install -dm755 "$pkgdir/var/log/journal"
  sed -i -e 's/#SystemMaxUse=/SystemMaxUse=25M/' ${pkgdir}/etc/systemd/journald.conf
  
  # 208 changed permissions for /var/log/journal, 209 misses /var/log/journal
  install -o root -g systemd-journal -dm2755 "$pkgdir/var/log/journal"
  #chown root:systemd-journal "$pkgdir/var/log/journal"
  #chmod 2755 "$pkgdir/var/log/journal"
}


