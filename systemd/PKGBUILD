
pkgname=systemd
pkgver=233
pkgrel=1
epoch=1
pkgdesc="System and Session Manager"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1')
depends=('acl' 'bash' 'glibc' 'kmod' 'hwids' 'util-linux' 'kbd' 'libcap' 'libxslt' 'iptables'
         'linux-api-headers' 'pam' 'xz' 'libgcrypt' 'dbus' 'libidn' 'lz4' 'python2-systemd' 'quota' 'libseccomp') # 'audit') 
makedepends=('docbook-xsl' 'gperf' 'intltool' 'cryptsetup' 'libmicrohttpd' 'gnu-efi-libs')
optdepends=('cryptsetup: required for encrypted block devices'
            'libmicrohttpd: systemd-journal-gatewayd')
backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
        etc/dbus-1/system.d/org.freedesktop.hostname1.conf
        etc/dbus-1/system.d/org.freedesktop.login1.conf
        etc/dbus-1/system.d/org.freedesktop.locale1.conf
        etc/dbus-1/system.d/org.freedesktop.machine1.conf
        etc/dbus-1/system.d/org.freedesktop.timedate1.conf
        etc/systemd/bootchart.conf
        etc/systemd/system.conf
        etc/systemd/user.conf
        etc/systemd/logind.conf
        etc/systemd/journald.conf
        etc/udev/udev.conf)
conflicts=('gummiboot')
replaces=('gummiboot')
provides=("udev=${pkgver}" 'gummiboot')
groups=('base')
install="systemd.install"
options=('!libtool')
source=("https://github.com/systemd/systemd/archive/v${pkgver}.tar.gz"
        'initcpio-hook-udev'
        'initcpio-install-systemd'
        'initcpio-install-udev'
        'loader.conf'
        'KaOS.conf'
        'KaOS_fallback.conf'
        'splash.bmp'
        'systemd-tmpfiles.hook'
        "https://github.com/systemd/systemd/commit/843d5baf6aad6c53fc00ea8d95d83209a4f92de1.diff"
        "https://github.com/systemd/systemd/commit/c9f7b4d356a453a01aa77a6bb74ca7ef49732c08.diff")
md5sums=('11d3ff48f3361b8bdcfcdc076a31b537'
         'b2da235893d76ed6b810fa5e8d2afe67'
         'f422035c2a36b4fa51f4a369eccc01f4'
         'bde43090d4ac0ef048e3eaee8202a407'
         '30b74b38eaf1178bbc540bda9a24bfe2'
         'eb444ae327472b98a65631ede777720a'
         'b8d8ebeab17edbe91209e705cfc9e774'
         'd7eec59a2aaa54770d22ccfa52d0ac2b'
         '7d5910d12fdfce56228a1aac8c3d618c'
         '30ac6477920c184f1a537d46fb194ec3'
         '5bce98bbc4528b64d9bde495fbf4c813')

build() {
  cd ${pkgname}-${pkgver}
  #rm src/journal/audit_type-to-name.h
  #rm src/udev/keyboard-keys-from-name.gperf
  
  sed -i 's|Linux Boot Manager|Systemd Boot Manager|' src/boot/bootctl.c
  # revert cgroup switch https://github.com/systemd/systemd/commit/843d5baf6aad6c53fc00ea8d95d83209a4f92de1
  #patch -p1 -i ${srcdir}/843d5baf6aad6c53fc00ea8d95d83209a4f92de1.diff
  
  NTP_SERVERS="0.north-america.pool.ntp.org 1.north-america.pool.ntp.org 2.north-america.pool.ntp.org 3.north-america.pool.ntp.org"
  
  ./autogen.sh
  ./configure \
      --libexecdir=/usr/lib \
      --localstatedir=/var \
      --sysconfdir=/etc \
      --enable-split-usr \
      --enable-lz4 \
      --enable-efi \
      --enable-gnuefi \
      --disable-audit \
      --disable-ima \
      --disable-networkd \
      --without-python \
      --with-default-dnssec=no \
      --without-kill-user-processes \
      --with-ntp-servers="${NTP_SERVERS}" \
      --with-default-hierarchy=hybrid \
      --with-fallback-hostname=KaOS \
      --with-dbuspolicydir=/etc/dbus-1/system.d 
      #--with-firmware-path=/usr/lib/firmware/updates:/lib/firmware/updates:/usr/lib/firmware:/lib/firmware

  make
}

check() {
  make -C ${pkgname}-${pkgver} check || true
}

package() {
  make -C ${pkgname}-${pkgver} DESTDIR=${pkgdir} install

  install -dm755 ${pkgdir}/bin
  ln -s ../usr/lib/systemd/systemd ${pkgdir}/bin/systemd

  install -dm755 ${pkgdir}/sbin
  for tool in runlevel reboot shutdown poweroff halt telinit; do
    ln -s '/usr/bin/systemctl' ${pkgdir}/sbin/$tool
  done

  ln -s '../usr/lib/systemd/systemd' ${pkgdir}/sbin/init

  install -d ${pkgdir}/sbin
  ln -s ../usr/bin/udevadm ${pkgdir}/sbin/udevadm

  ln -s ../lib/systemd/systemd-udevd ${pkgdir}/usr/bin/udevd

  install -m644 systemd-${pkgver}/tmpfiles.d/legacy.conf ${pkgdir}/usr/lib/tmpfiles.d

  # Replace in optical 
  sed -i 's#GROUP="dialout"#GROUP="uucp"#g;
          s#GROUP="tape"#GROUP="storage"#g;
          s#GROUP="cdrom"#GROUP="optical"#g' ${pkgdir}/usr/lib/udev/rules.d/*.rules
  sed -i 's/dialout/uucp/g;
          s/tape/storage/g;
          s/cdrom/optical/g' ${pkgdir}/usr/lib/sysusers.d/basic.conf
          
  # add mkinitcpio hooks
  install -Dm644 ${srcdir}/initcpio-install-systemd ${pkgdir}/usr/lib/initcpio/install/systemd
  install -Dm644 ${srcdir}/initcpio-install-udev ${pkgdir}/usr/lib/initcpio/install/udev
  install -Dm644 ${srcdir}/initcpio-hook-udev ${pkgdir}/usr/lib/initcpio/hooks/udev
  
  rm -r ${pkgdir}/usr/lib/rpm
  
  # 208 changed permissions for /var/log/journal
  #install -o root -g systemd-journal -dm2755 ${pkgdir}/var/log/journal
  chown root:systemd-journal ${pkgdir}/var/log/journal
  chmod 2755 ${pkgdir}/var/log/journal{,/remote}
  
  # Make journalctl persistent, set max
  sed -i -e 's|#SystemMaxUse=|SystemMaxUse=25M|' ${pkgdir}/etc/systemd/journald.conf
  
  # Install systemd-boot files, ex-gummi
  install -D -m0644 ${srcdir}/loader.conf ${pkgdir}/usr/lib/systemd/boot/loader/loader.conf
  install -D -m0644 ${srcdir}/KaOS.conf ${pkgdir}/usr/lib/systemd/boot/loader/entries/KaOS.conf
  install -D -m0644 ${srcdir}/KaOS_fallback.conf ${pkgdir}/usr/lib/systemd/boot/loader/entries/KaOS_fallback.conf
  install -D -m0644 ${srcdir}/splash.bmp ${pkgdir}/usr/lib/systemd/boot/splash.bmp
  
  # adjust default PAM config
  sed 's|account required pam_unix.so|account  include system-login|g' -i ${pkgdir}/etc/pam.d/systemd-user
  sed 's|session optional pam_systemd.so|session  include system-login|g' -i ${pkgdir}/etc/pam.d/systemd-user
  
  # pacman hook
  install -Dm644 ../systemd-tmpfiles.hook ${pkgdir}/usr/share/libalpm/hooks/systemd-tmpfiles.hook
}


