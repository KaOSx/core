
pkgname=systemd
pkgver=242
_pkgver=242
pkgrel=1
epoch=1
pkgdesc="System and Session Manager"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1')
depends=('acl' 'bash' 'glibc' 'kmod' 'hwids' 'util-linux' 'kbd' 'libcap' 'libxslt' 'iptables'
         'linux-api-headers' 'pam' 'xz' 'libgcrypt' 'dbus' 'libidn2' 'lz4'
         'quota' 'libseccomp' 'cryptsetup') # 'audit') 
makedepends=('docbook-xsl' 'gperf' 'intltool' 'libmicrohttpd' 'gnu-efi-libs' 'meson' 'ninja' 'python3-lxml')
optdepends=('libmicrohttpd: systemd-journal-gatewayd')
backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
        etc/dbus-1/system.d/org.freedesktop.hostname1.conf
        etc/dbus-1/system.d/org.freedesktop.login1.conf
        etc/dbus-1/system.d/org.freedesktop.locale1.conf
        etc/dbus-1/system.d/org.freedesktop.machine1.conf
        etc/dbus-1/system.d/org.freedesktop.timedate1.conf
        etc/systemd/bootchart.conf
        etc/systemd/system.conf
        etc/systemd/user.conf
        etc/systemd/logind.conf
        etc/systemd/journald.conf
        etc/udev/udev.conf)
conflicts=('gummiboot')
replaces=('gummiboot')
provides=("udev=${pkgver}" 'gummiboot')
groups=('base')
install="systemd.install"
options=('!libtool')
source=("https://github.com/systemd/systemd/archive/v${_pkgver}.tar.gz"
        'initcpio-hook-udev'
        'initcpio-install-systemd'
        'initcpio-install-udev'
        'loader.conf'
        'KaOS.conf'
        'KaOS_fallback.conf'
        'splash.bmp'
        'systemd-tmpfiles.hook'
        'service.diff'
        "https://github.com/systemd/systemd/commit/8f6b442a78d0b485f044742ad90b2e8271b4e68e.diff"
        "https://github.com/systemd/systemd-stable/commit/69df16671b5ac43bf19bdeb370963b61dc33e2e2.diff"
        "https://github.com/systemd/systemd-stable/commit/e8ffb554dce466823f8e95ca5d7f700ebe8ec13f.diff"
        "https://github.com/systemd/systemd-stable/commit/f9d88e891eb2d39528840e67d38b3a9be032c582.diff"
        "https://github.com/systemd/systemd-stable/commit/2760b0dc5aecbb5549801942b12f1d75cbafae09.diff"
        "https://github.com/systemd/systemd-stable/commit/8bca4621fc003a148c70248c55aa877dfe61fd3f.diff"
        "https://github.com/systemd/systemd/commit/75720bff62a84896e9a0654afc7cf9408cf89a38.diff")
md5sums=('5e004a4007cebbc4c7a06bfd2b9b3d4c'
         'b2da235893d76ed6b810fa5e8d2afe67'
         'f422035c2a36b4fa51f4a369eccc01f4'
         'bde43090d4ac0ef048e3eaee8202a407'
         '30b74b38eaf1178bbc540bda9a24bfe2'
         'eb444ae327472b98a65631ede777720a'
         'b8d8ebeab17edbe91209e705cfc9e774'
         'd7eec59a2aaa54770d22ccfa52d0ac2b'
         '7d5910d12fdfce56228a1aac8c3d618c'
         'b20711c6e42585d2a85ff01f8adb5c68'
         '704acafa13d503b6a63701e2765d7e31'
         '480e3e97a367da661c9aeca249fe6d85'
         '1f3040e7304d9008bd47770ee36daed3'
         '563eab1aa72e8ce2e0423813a5afa85d'
         'cae3c87c9b14be9cc4caa19eac4bb4fd'
         'ef78e0cd467ec1192f34c34a29f0c000'
         '48700f2d4767c623dd75ee4198cb88bc')

prepare() {
  cd ${pkgname}-${_pkgver}
  
  sed -i 's|Linux Boot Manager|Systemd Boot Manager|' src/boot/bootctl.c
  
  # revert SuccessAction= until figured why it is set to invalid on KaOS with 241 & up
  #patch -p1 -i ${srcdir}/service.diff
  # newer meson has debug, clashes with systemd 239 debug
  #patch -p1 -i ${srcdir}/8f6b442a78d0b485f044742ad90b2e8271b4e68e.diff
  # CVE's https://www.openwall.com/lists/oss-security/2019/02/18/3
  #patch -p1 -i ${srcdir}/69df16671b5ac43bf19bdeb370963b61dc33e2e2.diff
  #patch -p1 -i ${srcdir}/e8ffb554dce466823f8e95ca5d7f700ebe8ec13f.diff
  #patch -p1 -i ${srcdir}/f9d88e891eb2d39528840e67d38b3a9be032c582.diff
  #patch -p1 -i ${srcdir}/2760b0dc5aecbb5549801942b12f1d75cbafae09.diff
  #patch -p1 -i ${srcdir}/8bca4621fc003a148c70248c55aa877dfe61fd3f.diff
  # stat.h changes with glibc 2.29
  #patch -p1 -i ${srcdir}/75720bff62a84896e9a0654afc7cf9408cf89a38.diff
}

build() {
  mkdir -p build
  cd build
  
  NTP_SERVERS="0.north-america.pool.ntp.org 1.north-america.pool.ntp.org 2.north-america.pool.ntp.org 3.north-america.pool.ntp.org"
  # cp locale-archive from /usr/lib/locale
  #export LANG=en_US.UTF-8
  
  meson ../${pkgname}-${_pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dsplit-usr=true \
    -Dsplit-bin=true \
    -Drootprefix=/usr \
    -Dlz4=true \
    -Defi=true \
    -Dgnu-efi=true \
    -Daudit=false \
    -Dima=false \
    -Dlibidn2=true \
    -Dnetworkd=false \
    -Ddefault-dnssec=no \
    -Ddefault-kill-user-processes=false \
    -Dntp-servers="${NTP_SERVERS}" \
    -Ddefault-hierarchy=hybrid \
    -Dfallback-hostname=KaOS \
    -Ddbuspolicydir=/etc/dbus-1/system.d 
    # -Ddefault-locale=C
    ninja
}

check() {
  cd build
  ninja test || true
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install
  
  cd ..
  
  install -dm755 ${pkgdir}/bin
  ln -s ../usr/lib/systemd/systemd ${pkgdir}/bin/systemd

  install -dm755 ${pkgdir}/sbin
  for tool in runlevel reboot shutdown poweroff halt telinit; do
    ln -s '/usr/bin/systemctl' ${pkgdir}/sbin/$tool
  done

  ln -s '../usr/lib/systemd/systemd' ${pkgdir}/sbin/init

  install -d ${pkgdir}/sbin
  ln -s ../usr/bin/udevadm ${pkgdir}/sbin/udevadm

  ln -s ../lib/systemd/systemd-udevd ${pkgdir}/usr/bin/udevd

  install -m644 systemd-${_pkgver}/tmpfiles.d/legacy.conf ${pkgdir}/usr/lib/tmpfiles.d

  # Replace in optical 
  sed -i 's#GROUP="dialout"#GROUP="uucp"#g;
          s#GROUP="tape"#GROUP="storage"#g;
          s#GROUP="cdrom"#GROUP="optical"#g' ${pkgdir}/usr/lib/udev/rules.d/*.rules
  sed -i 's/dialout/uucp/g;
          s/tape/storage/g;
          s/cdrom/optical/g' ${pkgdir}/usr/lib/sysusers.d/basic.conf
          
  # add mkinitcpio hooks
  install -Dm644 ${srcdir}/initcpio-install-systemd ${pkgdir}/usr/lib/initcpio/install/systemd
  install -Dm644 ${srcdir}/initcpio-install-udev ${pkgdir}/usr/lib/initcpio/install/udev
  install -Dm644 ${srcdir}/initcpio-hook-udev ${pkgdir}/usr/lib/initcpio/hooks/udev
  
  rm -r ${pkgdir}/usr/lib/rpm
  
  # 208 changed permissions for /var/log/journal
  #install -o root -g systemd-journal -dm2755 ${pkgdir}/var/log/journal
  chown root:systemd-journal ${pkgdir}/var/log/journal
  chmod 2755 ${pkgdir}/var/log/journal{,/remote}
  
  # Make journalctl persistent, set max
  sed -i -e 's|#SystemMaxUse=|SystemMaxUse=25M|' ${pkgdir}/etc/systemd/journald.conf
  
  # Install systemd-boot files, ex-gummi
  install -D -m0644 ${srcdir}/loader.conf ${pkgdir}/usr/lib/systemd/boot/loader/loader.conf
  install -D -m0644 ${srcdir}/KaOS.conf ${pkgdir}/usr/lib/systemd/boot/loader/entries/KaOS.conf
  install -D -m0644 ${srcdir}/KaOS_fallback.conf ${pkgdir}/usr/lib/systemd/boot/loader/entries/KaOS_fallback.conf
  install -D -m0644 ${srcdir}/splash.bmp ${pkgdir}/usr/lib/systemd/boot/splash.bmp
  
  # adjust default PAM config
  sed 's|account required pam_unix.so|account  include system-login|g' -i ${pkgdir}/etc/pam.d/systemd-user
  sed 's|session optional pam_systemd.so|session  include system-login|g' -i ${pkgdir}/etc/pam.d/systemd-user
  
  # pacman hook
  install -Dm644 ../systemd-tmpfiles.hook ${pkgdir}/usr/share/libalpm/hooks/systemd-tmpfiles.hook
  
  # Systemd 239 started shipping openresolv replacement, remove for now, conflicting file only
  rm ${pkgdir}/usr/sbin/resolvconf
}


