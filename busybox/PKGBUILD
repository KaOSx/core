
pkgname=busybox
pkgver=1.36.1
pkgrel=1
pkgdesc="Base initramfs tools"
arch=('x86_64')
url="https://www.busybox.net/"
license=('GPL')
depends=('glibc' 'libxcrypt')
provides=('mkinitcpio-busybox')
replaces=('mkinitcpio-busybox')
conflicts=('mkinitcpio-busybox')
options=('!buildflags')
source=("https://busybox.net/downloads/busybox-${pkgver}.tar.bz2"
        'config')
md5sums=('0fc591bc9f4e365dfd9ade0014f32561'
         '391328c0540790684d3f5a1ace9dc391')

build() {
  cd busybox-${pkgver}

  local safeflags="${CARCH/_/-} -mtune=generic -Os -pipe -fno-strict-aliasing"
  sed 's|^\(CONFIG_EXTRA_CFLAGS\)=.*|\1="-march='"$safeflags"'"|' ${srcdir}/config > .config

  make
}

package() {
  # mkinitcpio expects it in /usr/lib/initcpio
  install -Dm755 ${srcdir}/busybox-${pkgver}/busybox ${pkgdir}/usr/lib/initcpio/busybox
  # drcaut expects it in /usr/bin
  install -Dm755 ${srcdir}/busybox-${pkgver}/busybox ${pkgdir}/usr/bin/busybox
}

