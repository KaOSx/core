
pkgname=libnl
pkgver=3.7.0
_pkgver=3_7_0
pkgrel=1
pkgdesc="Library for applications dealing with netlink sockets"
arch=('x86_64')
url="https://www.infradead.org/~tgr/libnl/"
license=('GPL')
depends=('glibc')
conflicts=('libnl3')
backup=('etc/libnl/classid' 'etc/libnl/pktloc')
options=('!libtool')
source=("https://github.com/thom311/libnl/releases/download/libnl${_pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('9fe43ccbeeea72c653bdcf8c93332583135cda46a79507bfd0a483bb57f65939')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}

