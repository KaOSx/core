
pkgname=guile2.0
pkgver=2.0.14
pkgrel=3
pkgdesc='Portable, embeddable Scheme implementation written in C. Legacy branch.'
url="https://www.gnu.org/software/guile/"
arch=('x86_64')
license=('GPL')
depends=('gmp' 'libltdl' 'ncurses' 'texinfo' 'gc' 'libffi' 'libunistring')
options=('!libtool')
source=("https://ftp.gnu.org/pub/gnu/guile/guile-${pkgver}.tar.gz")
md5sums=('333b6eec83e779935a45c818f712484e')

build() {
  cd guile-${pkgver}
  ./configure --prefix=/usr \
	--disable-static  \
	--disable-error-on-warning \
        --program-suffix=2.0
  make
}

package() {
  cd guile-${pkgver}
  make DESTDIR=${pkgdir} install
  
  rm -rf ${pkgdir}/usr/share/info
  sed -i '1s/guile/guile2.0/' -i ${pkgdir}/usr/bin/guile-config2.0
  mv ${pkgdir}/usr/share/aclocal/guile.m4 ${pkgdir}/usr/share/aclocal/guile20.m4
  # stop elfutils warnings
  rm ${pkgdir}/usr/lib/libguile-2.0.so.*-gdb.scm
}
