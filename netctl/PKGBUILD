
pkgname=netctl
pkgver=1.16
pkgrel=1
pkgdesc="Profile based systemd network management"
url="http://projects.archlinux.org/netctl.git/"
license=("GPL")
arch=('x86_64')
depends=('coreutils' 'iproute2' 'openresolv')
makedepends=('pkg-config') 
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support (or dhcpcd)'
            'dhcpcd: for DHCP support (or dhclient)'
            'wpa_supplicant: for wireless networking support'
            'wpa_actiond: for automatic wireless connections through netctl-auto'
            'ppp: for pppoe connections')
source=("https://sources.archlinux.org/other/packages/netctl/netctl-${pkgver}.tar.xz")
md5sums=('2bd3ecbb52dc4fd496bc1714f68eca80')

package() {
  cd netctl-${pkgver}
  make DESTDIR=${pkgdir} install

  # Shell Completion
  install -D -m644 contrib/bash-completion ${pkgdir}/usr/share/bash-completion/completions/netctl
  install -D -m644 contrib/zsh-completion ${pkgdir}/usr/share/zsh/site-functions/_netctl
}
