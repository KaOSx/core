
pkgname=libxcrypt
pkgver=4.4.36
pkgrel=3
pkgdesc='Extended crypt library for descrypt, md5crypt, bcrypt, and others'
arch=('x86_64')
url='https://github.com/besser82/libxcrypt/'
license=('GPL')
depends=('glibc')
source=("https://github.com/besser82/libxcrypt/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e5e1f4caee0a01de2aee26e3138807d6d3ca2b8e67287966d1fefd65e1fd8943')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-hashes=strong,glibc \
    --disable-obsolete-api \
    --disable-failure-tokens
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check 
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
