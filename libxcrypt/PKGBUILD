
pkgname=libxcrypt
pkgver=4.4.30
pkgrel=1
pkgdesc='Extended crypt library for descrypt, md5crypt, bcrypt, and others'
arch=('x86_64')
url='https://github.com/besser82/libxcrypt/'
license=('GPL')
depends=('glibc')
source=("https://github.com/besser82/libxcrypt/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('b3667f0ba85daad6af246ba4090fbe53163ad93c8b6a2a1257d22a78bb7ceeba')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-hashes=strong,glibc \
    --disable-obsolete-api \
    --disable-failure-tokens
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check 
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
