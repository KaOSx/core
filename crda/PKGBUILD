
pkgname=crda
pkgver=3.18
pkgrel=1
pkgdesc="Central Regulatory Domain Agent"
arch=('x86_64')
url="http://wireless.kernel.org/en/developers/Regulatory/CRDA"
license=('custom')
depends=('wireless-regdb' 'libnl' 'libgcrypt' 'systemd' 'iw')
makedepends=('python2-m2crypto')
install=crda.install
source=("https://kernel.org/pub/software/network/crda/${pkgname}-${pkgver}.tar.xz"
        'set-wireless-regdom'
        'libreg.patch')
md5sums=('0431fef3067bf503dfb464069f06163a'
         '65c93f2ff2eb1b29d9e9fa102ae2dd45'
         '067be591ddedb9572a73373c92fb4bc7')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  sed -e "s|$(Q)ldconfig||" -i ${srcdir}/${pkgname}-${pkgver}/Makefile
  patch -p1 -i "${srcdir}"/libreg.patch
  
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" UDEV_RULE_DIR=/usr/lib/udev/rules.d/ SBINDIR=/usr/bin/ install
 
  echo 'ACTION=="add" SUBSYSTEM=="module", DEVPATH=="/module/cfg80211", RUN+="/usr/bin/set-wireless-regdom"' >> "${pkgdir}"/usr/lib/udev/rules.d/85-regulatory.rules

  install -D -m644 "${srcdir}"/${pkgname}-${pkgver}/LICENSE "${pkgdir}"/usr/share/licenses/crda/LICENSE
  install -D -m755 "${srcdir}"/set-wireless-regdom "${pkgdir}"/usr/bin/set-wireless-regdom
}

