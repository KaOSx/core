
_extramodules=6.2.10-1
__extramodules=extramodules-6.2
_kver="$(cat /lib/modules/${__extramodules}/version)"

pkgname=zfs-kmod
pkgver=2.1.9
pkgrel=5
pkgdesc="Kernel module for the Zettabyte File System."
arch=('x86_64')
url="https://zfsonlinux.org/"
license=('CDDL')
depends=('kmod' 'linux' "zfs=${pkgver}" 'zfs-headers')
makedepends=('linux-headers' 'elfutils')
install=zfs-kmod.install
source=("https://github.com/zfsonlinux/zfs/releases/download/zfs-${pkgver}/zfs-${pkgver}.tar.gz"
        "https://github.com/openzfs/zfs/commit/3ace9dea01f2088e81c0b90ca8e07faad7364eca.diff"
        "https://github.com/openzfs/zfs/commit/2e9c9b792fd34b4fe1fb524444529f41bcb1b7c5.diff")
#source=("https://github.com/openzfs/zfs/archive/${_commit}.zip")
sha256sums=('6b172cdf2eb54e17fcd68f900fab33c1430c5c59848fa46fab83614922fe50f6'
            '1c1322194bbaee4c13186033b9b25eed1440b1f076faf735e45aa7a30fd6dd87'
            '41e05c2f20e630c8f943af253fa73024b1cb8e222c7caf3ed5883d58ca2cc31e')

build() {
    cd zfs-${pkgver}
    patch -p1 -i ${srcdir}/3ace9dea01f2088e81c0b90ca8e07faad7364eca.diff
    patch -p1 -i ${srcdir}/2e9c9b792fd34b4fe1fb524444529f41bcb1b7c5.diff

    ./autogen.sh
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --datadir=/usr/share \
        --libdir=/lib \
        --includedir=/usr/include \
        --with-udevdir=/usr/lib/udev \
        --libexecdir=/usr/lib \
        --with-config=kernel \
        --with-linux=/lib/modules/${_extramodules}/build \
        --with-linux-obj=/lib/modules/${_extramodules}/build
    make

    # sign the modules
    _file=/usr/src/linux-${_kver}/scripts/sign-file
    _pem=/$HOME/signing_key.pem
    _key=/$HOME/signing_key.x509

    ${_file} sha256 ${_pem} ${_key} module/avl/zavl.ko
    ${_file} sha256 ${_pem} ${_key} module/icp/icp.ko
    ${_file} sha256 ${_pem} ${_key} module/lua/zlua.ko
    ${_file} sha256 ${_pem} ${_key} module/nvpair/znvpair.ko
    ${_file} sha256 ${_pem} ${_key} module/spl/spl.ko
    ${_file} sha256 ${_pem} ${_key} module/unicode/zunicode.ko
    ${_file} sha256 ${_pem} ${_key} module/zcommon/zcommon.ko
    ${_file} sha256 ${_pem} ${_key} module/zfs/zfs.ko
    ${_file} sha256 ${_pem} ${_key} module/zstd/zzstd.ko
}

package() {
    cd zfs-${pkgver}

    make DESTDIR=${pkgdir} install
    #remove references to the buildsystem
    sed -i "s|${srcdir}||" ${pkgdir}/usr/src/zfs-*/${_extramodules}/Module.symvers
    #remove header files packaged in zfs-headers
    rm -r ${pkgdir}/usr/src/zfs-${pkgver}/include
    rm -r ${pkgdir}/usr/src/spl-${pkgver}/include
    rm ${pkgdir}/usr/src/zfs-${pkgver}/zfs*
    rm -r ${pkgdir}/usr/src/spl-${pkgver}/spl*

    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${__extramodules}'/" "${startdir}/zfs-kmod.install"
    install -D -m0644 LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}
}
