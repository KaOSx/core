
pkgname=net-tools
pkgver=2.10
_commit=0eebece8c964e3cfa8a018f42b2e7e751a7009a0
pkgrel=5
pkgdesc="Configuration tools for Linux networking"
arch=('x86_64')
license=('GPL')
url="https://sourceforge.net/projects/net-tools/"
groups=('base')
depends=('glibc')
#source=("https://sourceforge.net/code-snapshots/git/n/ne/net-tools/code.git/net-tools-code-${_commit}.zip")
source=("https://sourceforge.net/projects/net-tools/files/net-tools-${pkgver}.tar.xz")
options=('!makeflags')
md5sums=('78aae762c95e2d731faf88d482e4cde5')

build() {
  cd ${pkgname}-${pkgver}

  yes "" | make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # hostname is provided by inetutils
  rm ${pkgdir}/bin/{hostname,dnsdomainname,domainname}
  rm -rf ${pkgdir}/usr/share/man/man1
  # empty symlinks to hostname
  rm ${pkgdir}/bin/{nis,yp}domainname
}
