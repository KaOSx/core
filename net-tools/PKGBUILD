
pkgname=net-tools
pkgver=1.60.7
_commit=0eebece8c964e3cfa8a018f42b2e7e751a7009a0
pkgrel=1
pkgdesc="Configuration tools for Linux networking"
arch=('x86_64')
license=('GPL')
url="https://sourceforge.net/projects/net-tools/"
groups=('base')
depends=('glibc')
source=("https://sourceforge.net/code-snapshots/git/n/ne/net-tools/code.git/net-tools-code-${_commit}.zip")
options=('!makeflags')
md5sums=('91301780a363690dffa6ee07084e80b0')

build() {
  cd ${pkgname}-code-${_commit}
  
  yes "" | make
}

package() {
  cd ${pkgname}-code-${_commit}
  make DESTDIR=${pkgdir} update

  # hostname is provided by inetutils
  rm ${pkgdir}/bin/{hostname,dnsdomainname,domainname}
  rm -rf ${pkgdir}/usr/share/man/man1
  # empty symlinks to hostname
  rm ${pkgdir}/bin/{nis,yp}domainname
}
