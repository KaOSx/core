
pkgname=libxml2
pkgver=2.9.11
pkgrel=1
pkgdesc="XML parsing library, version 2"
arch=('x86_64')
url="http://www.xmlsoft.org/"
license=('MIT')
depends=('zlib' 'readline' 'ncurses' 'xz')
makedepends=('python3')
options=('!libtool')
source=("ftp://ftp.xmlsoft.org/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "http://www.w3.org/XML/Test/xmlts20080827.tar.gz")
md5sums=('bce2745076131c8127da9aeac126223e'
         'ae3d1ebe000a3972afa104ca7f0e1b4a')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
     --with-threads \
     --with-history \
     --with-python=/usr/bin/python3
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  mv ${srcdir}/xmlconf .
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
