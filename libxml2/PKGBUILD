
pkgname=libxml2
pkgver=2.10.3
_pkgver=2.10
pkgrel=2
pkgdesc="XML parsing library, version 2"
arch=('x86_64')
url="http://www.xmlsoft.org/"
license=('MIT')
depends=('zlib' 'readline' 'ncurses' 'xz')
makedepends=('python3')
options=('!libtool')
source=("https://download.gnome.org/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz"
        "http://www.w3.org/XML/Test/xmlts20080827.tar.gz")
sha256sums=('5d2cc3d78bec3dbe212a9d7fa629ada25a7da928af432c93060ff5c17ee28a9c'
            '96151685cec997e1f9f3387e3626d61e6284d4d6e66e0e440c209286c03e9cc7')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
     --with-threads \
     --with-history \
     --with-legacy \
     --with-ftp \
     --with-python=/usr/bin/python3
  make
}

check() {
  cd ${pkgname}-${pkgver}

  mv ${srcdir}/xmlconf .
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
  install -Dm644 Copyright ${pkgdir}/usr/share/licenses/${pkgname}/Copyright
}
