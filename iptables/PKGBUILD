
pkgname=iptables
pkgver=1.6.0
pkgrel=2
pkgdesc="A Linux kernel packet control tool"
arch=('x86_64')
license=('GPL2')
url='http://www.netfilter.org/projects/iptables/index.html'
depends=('glibc' 'bash')
makedepends=('linux-api-headers' 'chrpath')
options=('!libtool')
source=("http://www.netfilter.org/projects/iptables/files/${pkgname}-${pkgver}.tar.bz2"
        'empty.rules'
        'simple_firewall.rules'
        'empty-filter.rules'
        'empty-mangle.rules'
        'empty-nat.rules'
        'empty-raw.rules'
        'empty-security.rules'
        'iptables.service'
        'ip6tables.service'
        'iptables-flush')
md5sums=('27ba3451cb622467fc9267a176f19a31'
         '14186bbafe21bb0638c0cb8e0903c829'
         '88fe330e810bb89b3aa08747bae3e3f9'
         '3ae6e25f397195611bab943945ff31ee'
         '60a90471364984ef43fa750d02b19e6e'
         '07f769473723984ecd1d13ef841c0222'
         '0e202e7eaab6692d2dcf9ea04766db5c'
         '07423e206c5c7a61689eebf12a1dbe52'
         '0b63db98a863cb2050e3b4596605a8ca'
         'af1ec8be985119b2a3e1b16f1a7f5968'
         'a19e0630d200f066522f0baef815dd75')

build() {
  cd ${pkgname}-${pkgver}

  rm include/linux/types.h

 ./configure --prefix=/usr \
     --libexecdir=/usr/lib/iptables \
     --sysconfdir=/etc \
     --with-xtlibdir=/usr/lib/iptables \
     --enable-devel \
     --enable-shared \
     --disable-nftables
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # http://www.spinics.net/lists/netfilter-devel/msg24969.html
  chrpath --delete ${pkgdir}/usr/lib/iptables/*.so

  cd ${srcdir}
  install -D -m644 empty.rules ${pkgdir}/etc/iptables/empty.rules
  install -D -m644 simple_firewall.rules ${pkgdir}/etc/iptables/simple_firewall.rules

  mkdir -p ${pkgdir}/var/lib/{iptables,ip6tables}
  install -m644 empty-{filter,mangle,nat,raw,security}.rules ${pkgdir}/var/lib/iptables
  install -m644 empty-{filter,mangle,nat,raw,security}.rules ${pkgdir}/var/lib/ip6tables

  # systemd 
  install -Dm644 ${srcdir}/iptables.service ${pkgdir}/usr/lib/systemd/system/iptables.service
  install -Dm644 ${srcdir}/ip6tables.service ${pkgdir}/usr/lib/systemd/system/ip6tables.service
  install -Dm755 ${srcdir}/iptables-flush ${pkgdir}/usr/lib/systemd/scripts/iptables-flush  
}
