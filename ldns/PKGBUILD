 
pkgname=ldns
pkgver=1.8.2
pkgrel=1
pkgdesc='DNS library that facilitates DNS tool programming.'
url='https://www.nlnetlabs.nl/projects/ldns/'
license=('custom:BSD')
arch=('x86_64')
depends=('openssl' 'libpcap')
options=('!staticlibs')
source=("https://www.nlnetlabs.nl/downloads/ldns/${pkgname}-${pkgver}.tar.gz")
sha256sums=('b92b001cdd382de653620a05445e42e17a827eec93d64ee587ad291a533cc1e9')

build() {
    cd ${pkgname}-${pkgver}

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-drill \
        --with-examples \
        --disable-gost \
        --disable-rpath
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install

    install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
