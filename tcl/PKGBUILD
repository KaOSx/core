
pkgname=tcl
pkgver=8.6.10
_pkgver=8.6
pkgrel=2
pkgdesc="The Tcl scripting language"
arch=('x86_64')
url="https://core.tcl-lang.org/index.html"
license=('custom')
depends=('glibc' 'zlib')
source=("https://sourceforge.net/projects/tcl/files/Tcl/${pkgver}/tcl${pkgver}-src.tar.gz")
md5sums=('97c55573f8520bcab74e21bfd8d0aadc')

build() {
  cd tcl${pkgver}/unix
  # Already in sqlite3:
  rm -rf ${srcdir}/tcl${pkgver}/pkgs/sqlite3*

  ./configure --prefix=/usr --mandir=/usr/share/man --enable-threads --enable-64bit
  
  make
  
  # LFS recommendation http://www.linuxfromscratch.org/blfs/view/svn/general/tcl.html
  sed -e "s@^\(TCL_SRC_DIR='\).*@\1/usr/include'@" \
      -e "/TCL_B/s@='\(-L\)\?.*unix@='\1/usr/lib@" \
      -i tclConfig.sh
}

check() {
  cd tcl${pkgver}/unix
  
  make test
}

package()
{
  cd tcl${pkgver}/unix
  
  make INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf tclsh${_pkgver} ${pkgdir}/usr/bin/tclsh
  
  install -Dm644 ../license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
