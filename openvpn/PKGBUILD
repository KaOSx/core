
pkgname=openvpn
pkgver=2.3.4
pkgrel=1
pkgdesc="An easy-to-use, robust, and highly configurable VPN (Virtual Private Network)"
arch=('x86_64')
url="http://openvpn.net/index.php/open-source.html"
depends=('openssl' 'lzo2' 'iproute2')
license=('custom')
backup=(usr/share/openvpn/easy-rsa/vars
        usr/share/openvpn/easy-rsa/openssl.cnf)
source=("http://swupdate.openvpn.net/community/releases/openvpn-${pkgver}.tar.gz"
        "https://github.com/OpenVPN/easy-rsa/archive/2.2.2.tar.gz"
	'openvpn@.service'
	'systemd.patch')
md5sums=('04d47237907faabe9d046970ffe44b2e'
         '040238338980617bc9c2df4274349593'
         '0846aa59d0b123d61030890f9a4135ae'
         'e1bd1523e38745e948c710db1a330bb1')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p0 -i $srcdir/systemd.patch
  
  # Build openvpn
  CFLAGS="$CFLAGS -DPLUGIN_LIBDIR=\\\"/usr/lib/openvpn\\\""
     ./configure --prefix=/usr \
        --enable-password-save \
        --mandir=/usr/share/man \
        --enable-iproute2 \
        --enable-systemd
  make

  # Build easy-rsa
  cd $srcdir/easy-rsa-2.2.2
  autoreconf -i
  ./configure --prefix=/usr --with-easyrsadir=/usr/share/openvpn/easy-rsa
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  # Install openvpn
  make DESTDIR=$pkgdir install
  install -d -m755 $pkgdir/etc/openvpn
  # Install examples
  install -d -m755 $pkgdir/usr/share/openvpn
  cp -r sample/sample-config-files $pkgdir/usr/share/openvpn/examples
  # Install license
  install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  # Install contrib
  install -d -m755 $pkgdir/usr/share/openvpn/contrib
  cp -r contrib $pkgdir/usr/share/openvpn
  # Install easy-rsa
  cd $srcdir/easy-rsa-2.2.2
  make install DESTDIR=$pkgdir
  rm -f ${pkgdir}/usr/share/openvpn/easy-rsa/openssl-0.9.?.cnf
  # install systemd units
  install -Dm644 "$srcdir/openvpn@.service" "$pkgdir/usr/lib/systemd/system/openvpn@.service"
}
