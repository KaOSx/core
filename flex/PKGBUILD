#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=flex
pkgver=2.5.35
pkgrel=5
pkgdesc="A tool for generating text-scanning programs"
arch=('i686' 'x86_64')
url="http://flex.sourceforge.net"
license=('custom')
groups=('base-devel')
depends=('glibc' 'm4' 'sh')
install=flex.install
source=(http://downloads.sourceforge.net/sourceforge/flex/flex-$pkgver.tar.bz2 
        flex-2.5.35-gcc44.patch
        flex-2.5.35-hardening.patch
        flex-2.5.35-missing-prototypes.patch
        flex-2.5.35-sign.patch
	lex.sh)
md5sums=('10714e50cea54dc7a227e3eddcd44d57'
         'e4444ef5c07db71a43280be74139bdea'
         'de952b3ed7cc074bc8c3e6ab73634048'
         '6b83f56b1b654c6a321cdc530a3ec68d'
         'd87fd9e9762ba7e230d516bdcf1c8c6f'
         'f725259ec23a9e87ee29e2ef82eda9a5')

build() {
  cd $srcdir/$pkgname-$pkgver || return 1
  patch -Np1 -i $srcdir/flex-2.5.35-gcc44.patch || return 1
  patch -Np1 -i $srcdir/flex-2.5.35-hardening.patch || return 1
  patch -Np1 -i $srcdir/flex-2.5.35-missing-prototypes.patch || return 1
  patch -Np1 -i $srcdir/flex-2.5.35-sign.patch || return 1
  
  ./configure --prefix=/usr \
    --mandir=/usr/share/man --infodir=/usr/share/info || return 1
  make || return 1
  make prefix=$pkgdir/usr \
    mandir=$pkgdir/usr/share/man \
    infodir=$pkgdir/usr/share/info \
    install || return 1
  install -Dm755 $srcdir/lex.sh $pkgdir/usr/bin/lex || return 1

  install -Dm644 COPYING \
  	$pkgdir/usr/share/licenses/$pkgname/license.txt || return 1
   
}
