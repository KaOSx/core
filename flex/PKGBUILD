
pkgname=flex
pkgver=2.5.38
pkgrel=1
pkgdesc="A tool for generating text-scanning programs"
arch=('x86_64')
url="http://flex.sourceforge.net"
license=('custom')
groups=('base-devel')
depends=('glibc' 'm4' 'sh')
install=flex.install
source=("http://downloads.sourceforge.net/sourceforge/flex/flex-$pkgver.tar.bz2")
md5sums=('b230c88e65996ff74994d08a2a2e0f27')

build() {
  cd $srcdir/$pkgname-$pkgver 
  
  ./configure --prefix=/usr 
  make
}

check() {
  cd $srcdir/$pkgname-$pkgver 
  sed -i -e '/test-bison-yylloc/d' -e '/test-bison-yylval/d' tests/Makefile.in

  make check
}

package() {
  cd $srcdir/$pkgname-$pkgver 
  make DESTDIR=$pkgdir install
  
  ln -s flex ${pkgdir}/usr/bin/lex 
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/license.txt 
}
