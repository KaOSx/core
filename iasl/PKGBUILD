
pkgname=iasl
pkgver=20230628
_pkgver=R06_28_23
pkgrel=1
pkgdesc="Intel ACPI Source Language compiler"
arch=('x86_64')
url="https://acpica.org"
license=('GPLv2')
depends=('glibc')
makedepends=('flex' 'bison')
options=('!makeflags')
#source=("https://acpica.org/sites/acpica/files/acpica-unix-${pkgver}.tar.gz")
source=("https://github.com/acpica/acpica/archive/refs/tags/${_pkgver}.tar.gz")
md5sums=('ca8425f76b1b42cb4956e81025c1df6d')

build() {
  cd acpica-${_pkgver}
  # fails to build with gcc 12, see https://github.com/acpica/acpica/issues/771
  export CFLAGS="-Wno-dangling-pointer"

  make
}

package() {
  cd acpica-${_pkgver}

  make DESTDIR=${pkgdir} install
  #install -D -m644 ../LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
