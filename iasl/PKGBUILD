
pkgname=iasl
pkgver=20221020
pkgrel=1
pkgdesc="Intel ACPI Source Language compiler"
arch=('x86_64')
url="https://acpica.org"
license=('GPLv2')
depends=('glibc')
makedepends=('flex' 'bison')
options=('!makeflags')
source=("https://acpica.org/sites/acpica/files/acpica-unix-${pkgver}.tar_0.gz")
md5sums=('907b0d617604abea3ec223acd80fbf87')

build() {
  cd acpica-unix-${pkgver}
  # fails to build with gcc 12, see https://github.com/acpica/acpica/issues/771
  export CFLAGS="-Wno-dangling-pointer"

  make
}

package() {
  cd acpica-unix-${pkgver}

  make DESTDIR=${pkgdir} install
  #install -D -m644 ../LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
