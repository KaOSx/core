
pkgname=iasl
pkgver=20190215
pkgrel=1
pkgdesc="Intel ACPI Source Language compiler"
arch=('x86_64')
url="http://acpica.org"
license=('custom')
depends=('glibc')
makedepends=('flex' 'bison')
options=('!makeflags')
source=("https://acpica.org/sites/acpica/files/acpica-unix-${pkgver}.tar.gz"
        'LICENSE')
md5sums=('65bd51cdb9dbee5eda7c707f18be18f9'
         '8615526144f69ea4e870d9bc37df9b29')

build() {
  cd acpica-unix-${pkgver}/generate/unix
  sed -i -e 's/_CYGWIN/_LINUX/g' -e 's/-Werror//g' Makefile.config

  make clean
  make
}

package() {
  cd acpica-unix-${pkgver}/generate/unix

  make DESTDIR=${pkgdir} install
  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
