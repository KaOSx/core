
pkgname=iasl
pkgver=20180810
pkgrel=1
pkgdesc="Intel ACPI Source Language compiler"
arch=('x86_64')
url="http://acpica.org"
license=('custom')
depends=('glibc')
makedepends=('flex' 'bison')
options=('!makeflags')
source=("https://acpica.org/sites/acpica/files/acpica-unix-${pkgver}.tar.gz"
        'LICENSE')
md5sums=('42476d226990380911ac0bbffa3a9a16'
         '8615526144f69ea4e870d9bc37df9b29')

build() {
  cd acpica-unix-${pkgver}/generate/unix
  sed -i -e 's/_CYGWIN/_LINUX/g' -e 's/-Werror//g' Makefile.config

  make clean
  make
}

package() {
  cd acpica-unix-${pkgver}/generate/unix

  make DESTDIR=${pkgdir} install
  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
