
pkgname=cmake
pkgver=3.14.1
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
license=('custom')
url="https://www.cmake.org"
depends=('curl' 'libarchive' 'shared-mime-info' 'libuv')
makedepends=('qt5-base')
#source=("https://www.cmake.org/files/v3.13/${pkgname}-${pkgver}.tar.gz")
source=("https://github.com/Kitware/CMake/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('7321be640406338fc12590609c42b0fae7ea12980855c1be363d25dcd76bb25f')

build() {
  cd ${pkgname}-${pkgver}

  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --system-libs \
    --qt-gui \
    --no-system-jsoncpp \
    --no-system-librhash \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 Copyright.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
