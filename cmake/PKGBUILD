
pkgname=cmake
pkgver=3.16.5
_pkgver=3.16
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
license=('custom')
url="https://www.cmake.org"
depends=('curl' 'libarchive' 'shared-mime-info' 'libuv')
makedepends=('qt5-base')
source=("https://www.cmake.org/files/v${_pkgver}/${pkgname}-${pkgver}.tar.gz")
#source=("https://github.com/Kitware/CMake/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('5f760b50b8ecc9c0c37135fae5fbf00a2fef617059aa9d61c1bb91653e5a8bfc')

build() {
  cd ${pkgname}-${pkgver}
  # set default libdir to lib with cmake > 3.16
  sed -i 's|/etc/arch-release|/etc/KaOS-release|' Modules/GNUInstallDirs.cmake

  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --system-libs \
    --qt-gui \
    --no-system-jsoncpp \
    --no-system-librhash \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 Copyright.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
