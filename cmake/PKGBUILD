
pkgname=cmake
pkgver=3.30.3
_pkgver=3.30
pkgrel=3
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
license=('custom')
url="https://www.cmake.org"
depends=('curl' 'libarchive' 'shared-mime-info' 'libuv' 'jsoncpp')
makedepends=('qt6-base')
source=("https://www.cmake.org/files/v${_pkgver}/${pkgname}-${pkgver}.tar.gz")
#source=("https://github.com/Kitware/CMake/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('6d5de15b6715091df7f5441007425264bdd477809f80333fdf95f846aaff88e4')

build() {
  cd ${pkgname}-${pkgver}
  # set default libdir to lib with cmake > 3.16
  sed -i 's|/etc/arch-release|/etc/KaOS-release|' Modules/GNUInstallDirs.cmake

  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --system-libs \
    --qt-gui \
    --no-system-cppdap \
    --no-system-librhash \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 Copyright.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
