
pkgname=shared-mime-info
pkgver=2.3
pkgrel=1
pkgdesc="Freedesktop.org Shared MIME Info"
arch=('x86_64')
url="https://freedesktop.org/Software/shared-mime-info"
license=('GPL')
depends=('libxml2' 'glib2')
makedepends=('intltool' 'meson' 'ninja' 'itstool' 'xmlto')
options=('!makeflags')
#source=("https://freedesktop.org/~hadess/${pkgname}-${pkgver}.tar.xz"
source=("https://gitlab.freedesktop.org/xdg/shared-mime-info/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'update-mime-database.hook')
md5sums=('a6c4b35bc36c3764d18e2472dc9c8484'
         'e7c5fd94c7e17ff8c3af7a8c6618abaa')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dupdate-mimedb=false
  ninja
}

check() {
  cd build

  ninja test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
  install -Dm644 ../update-mime-database.hook ${pkgdir}/usr/share/libalpm/hooks/update-mime-database.hook
}
