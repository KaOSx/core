
pkgname=shared-mime-info
pkgver=2.1
pkgrel=1
pkgdesc="Freedesktop.org Shared MIME Info"
arch=('x86_64')
url="https://freedesktop.org/Software/shared-mime-info"
license=('GPL')
depends=('libxml2' 'glib2')
makedepends=('intltool' 'meson' 'ninja' 'itstool' 'xmlto')
options=('!makeflags')
#source=("https://freedesktop.org/~hadess/${pkgname}-${pkgver}.tar.xz"
source=("https://gitlab.freedesktop.org/xdg/shared-mime-info/uploads/0ee50652091363ab0d17e335e5e74fbe/${pkgname}-${pkgver}.tar.xz"
        'update-mime-database.hook')
md5sums=('410725367f52ddbc06ef9eda883b8f73'
         'e7c5fd94c7e17ff8c3af7a8c6618abaa')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dupdate-mimedb=false
  ninja
}

check() {
  cd build
  
  ninja test
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
  install -Dm644 ../update-mime-database.hook ${pkgdir}/usr/share/libalpm/hooks/update-mime-database.hook
}
