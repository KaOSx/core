
pkgname=findutils
pkgver=4.7.0
pkgrel=1
pkgdesc="GNU utilities to locate files"
url="https://www.gnu.org/software/findutils"
arch=('x86_64')
license=('GPL3')
groups=('base')
depends=('glibc' 'sh')
source=("https://ftp.gnu.org/pub/gnu/findutils/findutils-${pkgver}.tar.xz")
md5sums=('731356dec4b1109b812fecfddfead6b2')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --with-packager="KaOS" \
    --with-packager-bug-reports="https://github.com/KaOSx/core/issues" \
    --libexecdir=/usr/lib
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}


package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # files used from mlocate
  rm ${pkgdir}/usr/bin/locate
  rm ${pkgdir}/usr/bin/updatedb
  rm ${pkgdir}/usr/share/man/man1/locate.1
  rm ${pkgdir}/usr/share/man/man1/updatedb.1
  rm -r ${pkgdir}/usr/share/man/man5
}
