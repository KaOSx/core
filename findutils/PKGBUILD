
pkgname=findutils
pkgver=4.4.2
pkgrel=6
pkgdesc="GNU utilities to locate files"
arch=('x86_64')
license=('GPL3')
groups=('base')
depends=('glibc' 'sh')
url="http://www.gnu.org/software/findutils"
source=("ftp://ftp.gnu.org/pub/gnu/findutils/findutils-$pkgver.tar.gz")
install=findutils.install
md5sums=('351cc4adb07d54877fa15f75fb77d39f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Don't build or install locate because we use mlocate,
  # which is a secure version of locate.
  sed -i '/^SUBDIRS/s/locate//' Makefile.in

  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make check
}


package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR=$pkgdir install
}
