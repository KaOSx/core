
pkgname=dnsutils
# _pkver when P version used. After 9.13, only use even number, odd are for unstable, development.
pkgver=9.14.4
_pkgver=9.14.4
pkgrel=1
pkgdesc="DNS utilities, tools, name resolver and domain name authority server"
arch=('x86_64')
url="https://www.isc.org/downloads/bind/"
license=('custom:ISC')
depends=('openssl' 'libidn2')
makedepends=('python3-ply')
options=('makeflags')
source=("https://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.gz")
md5sums=('3b39246f972ac66a6b7d05e9a4076248')

build() {
	cd bind-${_pkgver}

	export STD_CDEFINES='-DDIG_SIGCHASE'

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static \
		--disable-linux-caps \
		--with-openssl \
		--with-libidn2 \
		--with-python=/usr/bin/python3

	make
}

package() {
	cd bind-${_pkgver}
	
	install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
	cd bin; make DESTDIR=${pkgdir} install
}
