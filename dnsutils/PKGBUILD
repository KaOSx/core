#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=dnsutils
# Use a period and not a hyphen before the patch level for proper versioning.
pkgver=9.8.0.P1
_pkgver=9.8.0-P1
pkgrel=2
pkgdesc="Various DNS utilities - dig host nslookup nsupdate"
arch=(i686 x86_64)
url="https://www.isc.org/software/bind"
license=('custom:ISC')
depends=('openssl')
replaces=('bind-tools' 'host')
options=('makeflags')
source=(http://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.gz
	tools-only.patch)
md5sums=('d97ecd8f62a4cf4a769742c79cd8f921'
         'be0558f70ed81d90e68e6b6003ba12ed')

build() {
  cd "${srcdir}/bind-${_pkgver}"
  patch -Np1 -i "${srcdir}/tools-only.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--with-openssl=yes --disable-linux-caps || return 1
  make || return 1
}

package() {
  cd "${srcdir}/bind-${_pkgver}/bin"
  make DESTDIR="${pkgdir}" install || return 1
  install -Dm644 ../COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
