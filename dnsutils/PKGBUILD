
pkgname=dnsutils
# _pkver when P version used.
pkgver=9.10.3
_pkgver=9.10.3
pkgrel=1
pkgdesc="DNS utilities, tools, name resolver and domain name authority server"
arch=('x86_64')
url="https://www.isc.org/software/bind"
license=('custom:ISC')
depends=('openssl')
options=('makeflags')
source=("http://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.gz")
md5sums=('d8cbf04a62a139a841d4bf878087a555')

build() {
	cd "${srcdir}/bind-${_pkgver}"

	export STD_CDEFINES='-DDIG_SIGCHASE'

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static \
		--disable-linux-caps \
		--with-openssl  

	make
}

package() {
	cd "${srcdir}/bind-${_pkgver}"
	
	install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	cd bin; make DESTDIR="${pkgdir}" install
}
