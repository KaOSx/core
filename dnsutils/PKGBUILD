
pkgname=dnsutils
# _pkver when P version used. After 9.13, only use even number, odd are for unstable, development.
pkgver=9.20.2
_pkgver=9.20.2
pkgrel=1
pkgdesc="DNS utilities, tools, name resolver and domain name authority server"
arch=('x86_64')
url="https://www.isc.org/downloads/bind/"
license=('custom:ISC')
depends=('openssl' 'libidn2' 'libuv' 'json-c' 'libnsl')
makedepends=('python3')
options=('makeflags')
source=("https://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.xz")
md5sums=('f524316a2293ab0e935ae9cdfb69ae79')

build() {
    cd bind-${_pkgver}

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --disable-linux-caps \
        --disable-doh \
        --disable-geoip \
        --with-openssl \
        --with-libidn2
    make
}

package() {
    cd bind-${_pkgver}

    make DESTDIR=${pkgdir} install
    install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    #cd bin; make DESTDIR=${pkgdir} install
}
