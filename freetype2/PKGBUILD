
pkgname=freetype2
pkgver=2.4.12
pkgrel=1
pkgdesc="TrueType font rendering library"
arch=('x86_64')
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib' 'sh' 'bzip2')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2"
        'freetype-2.2.1-enable-valid.patch')
md5sums=('3463102764315eb86c0d3c2e1f3ffb7d'
         '214119610444c9b02766ccee5e220680')

build() {
  cd "${srcdir}/freetype-${pkgver}"

  patch -p1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch"

  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/freetype-${pkgver}"
  
  make -k check
}

package() {
  cd "${srcdir}/freetype-${pkgver}"

  make DESTDIR="${pkgdir}" install
}


