
pkgname=freetype2
pkgver=2.9
pkgrel=1
pkgdesc="TrueType font rendering library"
arch=('x86_64')
license=('GPL')
url="https://www.freetype.org/"
depends=('zlib' 'sh' 'bzip2')
options=('!libtool')
source=("https://sourceforge.net/projects/freetype/files/freetype2/${pkgver}/freetype-${pkgver}.tar.bz2"
        'freetype-2.3.0-enable-spr.patch')
md5sums=('513c403c110016fdc7e537216a642b1d'
         '0e968faa7c82747b0d4816749b5baad1')

build() {
  cd freetype-${pkgver}
  patch -p1 -i ${srcdir}/freetype-2.3.0-enable-spr.patch

  ./configure --prefix=/usr --disable-static --without-png
  make
}

check() {
  cd freetype-${pkgver}
  
  make -k check
}

package() {
  cd freetype-${pkgver}

  make DESTDIR=${pkgdir} install
}


