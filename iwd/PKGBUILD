 
pkgname=iwd
pkgver=1.6
pkgrel=1
pkgdesc='Wireless daemon for Linux'
arch=('x86_64')
url='https://git.kernel.org/pub/scm/network/wireless/iwd.git/tree/README'
license=('GPL-2.1')
depends=('glibc' 'readline')
backup=(etc/iwd/main.conf)
source=("https://www.kernel.org/pub/linux/network/wireless/${pkgname}-${pkgver}.tar.xz")
sha256sums=('07945e3166b7289bc8f103ba0f433cf735dd9f9ba78ddc9e475a6a478db344e7')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/iwd \
    --enable-manual-pages \
    --enable-hwsim \
    --enable-ofono \
    --enable-sim-hardcoded \
    --enable-wired 
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # 3 fail unit/test-arc4 unit/test-wsc unit/test-eap-sim
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make install DESTDIR=${pkgdir}
  
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
