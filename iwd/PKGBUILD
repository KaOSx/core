 
pkgname=iwd
pkgver=1.16
pkgrel=1
pkgdesc='Wireless daemon for Linux'
arch=('x86_64')
url='https://git.kernel.org/pub/scm/network/wireless/iwd.git/tree/README'
license=('GPL-2.1')
depends=('glibc' 'readline')
backup=(etc/iwd/main.conf)
source=("https://www.kernel.org/pub/linux/network/wireless/${pkgname}-${pkgver}.tar.xz")
sha256sums=('af548398aea2089a3a5103e5586561f24791090a17d4b2e50785e2faab5ed03a')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/iwd \
    --enable-manual-pages \
    --enable-hwsim \
    --enable-ofono \
    --enable-wired 
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make install DESTDIR=${pkgdir}

  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
