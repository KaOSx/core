 
pkgname=iwd
pkgver=2.22
pkgrel=1
pkgdesc='Wireless daemon for Linux'
arch=('x86_64')
url='https://git.kernel.org/pub/scm/network/wireless/iwd.git/tree/README'
license=('GPL-2.1')
depends=('glibc' 'readline')
backup=(etc/iwd/main.conf)
source=("https://www.kernel.org/pub/linux/network/wireless/${pkgname}-${pkgver}.tar.xz")
sha256sums=('2c41c5da9924b90f8383b293b0c0b3d0bfb34fdc8822d8d0d37ec100707f263e')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/iwd \
    --enable-manual-pages \
    --enable-hwsim \
    --enable-ofono \
    --enable-wired 
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # no FAIL
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make install DESTDIR=${pkgdir}

  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
