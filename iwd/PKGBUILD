 
pkgname=iwd
pkgver=1.3
pkgrel=1
pkgdesc='Wireless daemon for Linux'
arch=('x86_64')
url='https://git.kernel.org/pub/scm/network/wireless/iwd.git/tree/README'
license=('GPL-2.1')
depends=('glibc' 'readline')
backup=(etc/iwd/main.conf)
source=("https://www.kernel.org/pub/linux/network/wireless/${pkgname}-${pkgver}.tar.xz")
sha256sums=('fb1ea2bf25335dd719ac8d7c8acad113a701d2bd5c1aebcac0d65df4b70eab5c')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/iwd \
    --enable-manual-pages \
    --enable-hwsim \
    --enable-ofono \
    --enable-sim-hardcoded \
    --enable-wired 
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make install DESTDIR=${pkgdir}
  
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
