
pkgname=pacman
pkgver=6.0.2
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="https://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('bash' 'glibc' 'libarchive' 'curl' 'gpgme' 'pacman-mirrorlist')
makedepends=('asciidoc' 'meson' 'ninja' 'doxygen')
checkdepends=('python3' 'fakechroot')
backup=(etc/pacman.conf etc/makepkg.conf)
options=('!libtool')
source=("https://sources.archlinux.org/other/pacman/${pkgname}-${pkgver}.tar.xz"
        'pacman.conf'
        'makepkg.conf'
        "https://gitlab.archlinux.org/pacman/pacman/-/commit/a6b06a5b17e19db8700597ee06bae42b2cfe7b5a.diff")
md5sums=('f71e6f06867749735960d0c1d199d375'
         '38ea69609d8c37d11c7ca49478cd75d1'
         '0ce6144eaecdbaedf1ca48527aa3df36'
         'c8569599f262236d0e069603640aaa84')

prepare() {
  cd ${pkgname}-${pkgver}
  # bash 5.2 needed
  patch -p1 -i ${srcdir}/a6b06a5b17e19db8700597ee06bae42b2cfe7b5a.diff
}

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Ddoc=enabled \
    -Ddoxygen=enabled \
    -Dscriptlet-shell=/bin/bash \
    -Dldconfig=/sbin/ldconfig
  ninja
}

check() {
  cd build
  ninja test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  mkdir -p ${pkgdir}/etc
  install -m644 $srcdir/pacman.conf ${pkgdir}/etc/pacman.conf
  install -m644 $srcdir/makepkg.conf ${pkgdir}/etc/
}


