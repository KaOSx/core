
pkgname=pacman
pkgver=5.0.2
pkgrel=2
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('bash' 'glibc' 'libarchive' 'curl' 'gpgme' 'pacman-mirrorlist')
makedepends=('asciidoc')
checkdepends=('python2' 'fakechroot')
backup=(etc/pacman.conf etc/makepkg.conf)
options=('!libtool')
#install=pacman.install
source=("https://sources.archlinux.org/other/pacman/${pkgname}-${pkgver}.tar.gz"
        'pacman.conf'
        'makepkg.conf')
md5sums=('f36f5e7e95a89436febe1bcca874fc33'
         '5600ceea45ce0de5262a0c37f42ea28b'
         '6e3061b47b39ba529fee7f0848e26d3e')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc \
    --with-scriptlet-shell=/bin/bash \
    --with-ldconfig=/bin/ldconfig
  make V=1
  make -C contrib
}

check() {
  make -C ${pkgname}-${pkgver} check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  make DESTDIR=${pkgdir} -C contrib install

  mkdir -p ${pkgdir}/etc
  install -m644 $srcdir/pacman.conf ${pkgdir}/etc/pacman.conf
  install -m644 $srcdir/makepkg.conf ${pkgdir}/etc/

  # completion files
  install -Dm644 scripts/completion/bash_completion ${pkgdir}/usr/share/bash-completion/completions/pacman
  install -Dm644 scripts/completion/zsh_completion ${pkgdir}/usr/share/zsh/site-functions/_pacman
  
  for f in makepkg pacman-key; do
    ln -s pacman "${pkgdir}/usr/share/bash-completion/completions/$f"
  done
  
  # stop rankmirrors fail
  sed -e 's|/core}"|/core}" echo|' -i ${pkgdir}/usr/bin/rankmirrors
}

