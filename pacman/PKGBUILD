
pkgname=pacman
pkgver=6.0.2
pkgrel=2
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="https://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('bash' 'glibc' 'libarchive' 'curl' 'gpgme' 'pacman-mirrorlist')
makedepends=('asciidoc' 'meson' 'ninja' 'doxygen')
checkdepends=('python3' 'fakechroot')
backup=(etc/pacman.conf etc/makepkg.conf)
options=('!libtool')
source=("https://sources.archlinux.org/other/pacman/${pkgname}-${pkgver}.tar.xz"
        'pacman.conf'
        'makepkg.conf'
        "https://gitlab.archlinux.org/pacman/pacman/-/commit/de11824527ec4e2561e161ac40a5714ec943543c.diff")
md5sums=('f2c7e82cc5483a2c90f228a0393f5526'
         '38ea69609d8c37d11c7ca49478cd75d1'
         '0ce6144eaecdbaedf1ca48527aa3df36'
         '9b944e898030bd5afedc27bcdccebe70')

prepare() {
  cd ${pkgname}-${pkgver}
  # strip fixes, needed for 6.0.2
  patch -p1 -i ${srcdir}/de11824527ec4e2561e161ac40a5714ec943543c.diff
}

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Ddoc=enabled \
    -Ddoxygen=enabled \
    -Dscriptlet-shell=/bin/bash \
    -Dldconfig=/sbin/ldconfig
  ninja
}

check() {
  cd build
  ninja test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  mkdir -p ${pkgdir}/etc
  install -m644 $srcdir/pacman.conf ${pkgdir}/etc/pacman.conf
  install -m644 $srcdir/makepkg.conf ${pkgdir}/etc/
}


