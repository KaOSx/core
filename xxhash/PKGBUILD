 
# rebuild rsync & gdb on any update
pkgname=xxhash
pkgver=0.8.2
pkgrel=1
pkgdesc="Extremely fast non-cryptographic hash algorithm, working at speeds close to RAM limits."
arch=('x86_64')
url="https://cyan4973.github.io/xxHash/"
license=('GPL2' 'BSD')
makedepends=('cmake')
source=("https://github.com/Cyan4973/xxHash/archive/v${pkgver}.tar.gz")
md5sums=('a7628d7a4fd608a7573c5a3e2c856ea5')

build() {
  cmake -B build -S xxHash-${pkgver}/cmake_unofficial \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
  cmake --build build
}

package() {

  sed -i -e 's|/cmake_unofficial/../xxhsum.1|/cli/xxhsum.1|' build/cmake_install.cmake
  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 xxHash-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/xxhash/LICENSE
}
