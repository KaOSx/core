
pkgname=tk
pkgver=8.6.4
_pkgver=8.6
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl"
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${pkgver}" 'libxss' 'libxft')
source=("http://downloads.sourceforge.net/sourceforge/tcl/${pkgname}${pkgver}-src.tar.gz")
md5sums=('261754d7dc2a582f00e35547777e1fea')

build() {
  cd "${srcdir}/${pkgname}${pkgver}/unix"

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-threads \
              --enable-xft \
              --enable-64bit
  
  make
  
  # LFS recommendation http://www.linuxfromscratch.org/blfs/view/svn/general/tk.html
  sed -e "s@^\(TK_SRC_DIR='\).*@\1/usr/include'@" \
      -e "/TK_B/s@='\(-L\)\?.*unix@='\1/usr/lib@" \
      -i tkConfig.sh
}

package() {
  cd "${srcdir}/${pkgname}${pkgver}/unix"
    
  make INSTALL_ROOT="${pkgdir}" install install-private-headers
  ln -sf "wish${_pkgver}" "${pkgdir}/usr/bin/wish"

  # install license
  install -Dm644 license.terms "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

