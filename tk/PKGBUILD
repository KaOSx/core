
pkgname=tk
pkgver=8.6.15
_pkgver=8.6.15
__pkgver=8.6
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl"
arch=('x86_64')
url="https://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${_pkgver}" 'libxss' 'libxft')
source=("https://downloads.sourceforge.net/sourceforge/tcl/${pkgname}${pkgver}-src.tar.gz")
md5sums=('6d64b6eb021062f378017d403fedcbe6')

build() {
  cd ${pkgname}${_pkgver}/unix

  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-threads \
    --enable-xft \
    --enable-64bit

  make

  # LFS recommendation http://www.linuxfromscratch.org/blfs/view/svn/general/tk.html
  sed -e "s@^\(TK_SRC_DIR='\).*@\1/usr/include'@" \
      -e "/TK_B/s@='\(-L\)\?.*unix@='\1/usr/lib@" \
      -i tkConfig.sh
}

package() {
  cd ${pkgname}${_pkgver}/unix

  make INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf wish${__pkgver} ${pkgdir}/usr/bin/wish

  install -Dm644 license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

