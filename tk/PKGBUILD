#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=tk
pkgver=8.5.11
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl"
arch=('i686' 'x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${pkgver}" 'libxss' 'libxft')
source=("http://downloads.sourceforge.net/sourceforge/tcl/${pkgname}${pkgver}-src.tar.gz")
md5sums=('b61b72f0aad230091b100746f078b8f1')

[ "${CARCH}" = "x86_64" ] && __64bit="--enable-64bit" || __64bit="--disable-64bit"

build() {
  cd "${srcdir}/${pkgname}${pkgver}/unix"

  # Fix the Xft detection routine, or we will get ugly fonts.
  sed -i 's/FT_New_Face/XftFontOpen/g' configure.in
  autoreconf

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-threads \
              --enable-xft \
              "${__64bit}"
  
  make
}

package() {
  cd "${srcdir}/${pkgname}${pkgver}/unix"
    
  make INSTALL_ROOT="${pkgdir}" install install-private-headers
  ln -sf "wish8.5" "${pkgdir}/usr/bin/wish"
  
  # install private headers (FS#14388)
  cd "${srcdir}/${pkgname}${pkgver}"
  for dir in compat generic generic/ttk unix; do
    install -dm755 "${pkgdir}/usr/include/tk-private/${dir}"
    install -m644 -t "${pkgdir}/usr/include/tk-private/${dir}" "${dir}"/*.h
  done

  # install license
  install -Dm644 license.terms "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # remove buildroot traces / fixes #3602
  sed -i \
      -e "s,^TK_BUILD_LIB_SPEC='-L.*/unix,TK_BUILD_LIB_SPEC='-L/usr/lib," \
      -e "s,^TK_SRC_DIR='.*',TK_SRC_DIR='/usr/include'," \
      -e "s,^TK_BUILD_STUB_LIB_SPEC='-L.*/unix,TK_BUILD_STUB_LIB_SPEC='-L/usr/lib," \
      -e "s,^TK_BUILD_STUB_LIB_PATH='.*/unix,TK_BUILD_STUB_LIB_PATH='/usr/lib," \
      "${pkgdir}/usr/lib/tkConfig.sh"
}

# vim:set ts=2 sw=2 et:
