
pkgname=tk
pkgver=8.6.1
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl"
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${pkgver}" 'libxss' 'libxft')
source=("http://downloads.sourceforge.net/sourceforge/tcl/${pkgname}${pkgver}-src.tar.gz")
md5sums=('63f21c3a0e0cefbd854b4eb29b129ac6')

build() {
  cd "${srcdir}/${pkgname}${pkgver}/unix"

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-threads \
              --enable-xft \
              --enable-64bit
  
  make
}

package() {
  cd "${srcdir}/${pkgname}${pkgver}/unix"
    
  make INSTALL_ROOT="${pkgdir}" install install-private-headers
  ln -sf "wish8.6" "${pkgdir}/usr/bin/wish"

  # install license
  install -Dm644 license.terms "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # remove buildroot traces 
  sed -i \
      -e "s,^TK_BUILD_LIB_SPEC='-L.*/unix,TK_BUILD_LIB_SPEC='-L/usr/lib," \
      -e "s,^TK_SRC_DIR='.*',TK_SRC_DIR='/usr/include'," \
      -e "s,^TK_BUILD_STUB_LIB_SPEC='-L.*/unix,TK_BUILD_STUB_LIB_SPEC='-L/usr/lib," \
      -e "s,^TK_BUILD_STUB_LIB_PATH='.*/unix,TK_BUILD_STUB_LIB_PATH='/usr/lib," \
      "${pkgdir}/usr/lib/tkConfig.sh"
}

