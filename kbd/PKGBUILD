
pkgname=kbd
pkgver=2.4.0
pkgrel=3
pkgdesc="Keytable files and keyboard utilities"
arch=('x86_64')
url="https://www.kbd-project.org"
license=('GPL')
depends=('glibc' 'pam')
makedepends=('check')
source=("https://mirrors.edge.kernel.org/pub/linux/utils/kbd/${pkgname}-${pkgver}.tar.xz"
        'kbd2.2.0.patch')
sha256sums=('55f0740458cfd3a84e775e50d7e8b92dc01846db1edad8e2411ccc293ece9b9f'
            'b5bf6a9d7a1374f65f8373a3f4b89d0e19b809e889123fb58d33eee17eeb376c')

build() {
  cd ${pkgname}-${pkgver}
  # kbd 2.2.0 not compatible with encryption/loadkmap
  #patch -p1 -i ${srcdir}/kbd2.2.0.patch

  # Rename conflicting keymaps to have unique names
  mv data/keymaps/i386/qwertz/cz{,-qwertz}.map
  mv data/keymaps/i386/olpc/es{,-olpc}.map
  mv data/keymaps/i386/olpc/pt{,-olpc}.map
  #mv data/keymaps/i386/dvorak/no{,-dvorak}.map
  mv data/keymaps/i386/fgGIod/trf{,-fgGIod}.map
  mv data/keymaps/i386/colemak/{en-latin9,colemak}.map

  ./configure --prefix=/usr \
    --datadir=/usr/share/kbd \
    --mandir=/usr/share/man \
    --sysconfdir=/etc
    
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes
}

package() {
  cd ${pkgname}-${pkgver}

  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes DESTDIR=${pkgdir} install
}
