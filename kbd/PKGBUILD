
pkgname=kbd
pkgver=2.2.0
pkgrel=1
pkgdesc="Keytable files and keyboard utilities"
arch=('x86_64')
url="http://www.kbd-project.org"
license=('GPL')
depends=('glibc' 'pam')
makedepends=('check')
source=("https://mirrors.edge.kernel.org/pub/linux/utils/kbd/${pkgname}-${pkgver}.tar.xz")
sha256sums=('21a1bc5f6fb3b18ce9fdd717e4533368060a3182a39c7155eaf7ec0f5f83e9f7')

build() {
  cd ${pkgname}-${pkgver}

  # Rename conflicting keymaps to have unique names
  mv data/keymaps/i386/qwertz/cz{,-qwertz}.map
  mv data/keymaps/i386/olpc/es{,-olpc}.map
  mv data/keymaps/i386/olpc/pt{,-olpc}.map
  #mv data/keymaps/i386/dvorak/no{,-dvorak}.map
  mv data/keymaps/i386/fgGIod/trf{,-fgGIod}.map
  mv data/keymaps/i386/colemak/{en-latin9,colemak}.map

  ./configure --prefix=/usr \
    --datadir=/usr/share/kbd \
    --mandir=/usr/share/man
    
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes
}

package() {
  cd ${pkgname}-${pkgver}
  
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes DESTDIR=${pkgdir} install
}
