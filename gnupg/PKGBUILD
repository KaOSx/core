
pkgname=gnupg
pkgver=2.1.9
pkgrel=1
pkgdesc="GNU Privacy Guard 2 - a PGP replacement tool"
arch=('x86_64')
license=('GPL')
url="http://www.gnupg.org/"
depends=('libldap' 'curl' 'bzip2' 'zlib' 'libksba' 'libgcrypt' 'libgpg-error'
	 'npth' 'libassuan' 'texinfo' 'readline' 'pinentry' 'gnutls') # 'libusb')
install=${pkgname}.install
# https://www.gnupg.org/faq/whats-new-in-2.1.html
conflicts=('gnupg2' 'dirmngr')
provides=("gnupg2=${pkgver}" 'dirmngr')
replaces=('gnupg2' 'dirmngr')
source=("ftp://ftp.gnupg.org/gcrypt/gnupg/gnupg-$pkgver.tar.bz2")
md5sums=('0aabfec527b4b0b11a823c8a8ef9a9ab')

build() {
	cd ${srcdir}/gnupg-$pkgver
	
	./configure --prefix=/usr \
              --sysconfdir=/etc \
	      --libexecdir=/usr/lib/gnupg \
	      --enable-maintainer-mode \
	      --enable-symcryptrun \
	      --enable-gpgtar 
  make
}

check() {
	cd "${srcdir}/gnupg-${pkgver}"
	
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	
	ln -s gpg2 "${pkgdir}"/usr/bin/gpg
	ln -s gpgv2 "${pkgdir}"/usr/bin/gpgv
	ln -s gpg2.1.gz "${pkgdir}"/usr/share/man/man1/gpg.1.gz
	rm "${pkgdir}/usr/share/gnupg/com-certs.pem" 
}

