
pkgname=gnupg
pkgver=2.0.23
pkgrel=1
pkgdesc="GNU Privacy Guard 2 - a PGP replacement tool"
arch=('x86_64')
depends=('libldap' 'curl' 'bzip2' 'zlib' 'libksba' 'libgcrypt'
	'pth' 'libusb-compat' 'libassuan' 'texinfo' 'readline' 'pinentry' 'dirmngr')
license=('GPL')
url="http://www.gnupg.org/"
install=${pkgname}.install
conflicts=('gnupg2')
provides=("gnupg2=${pkgver}")
replaces=('gnupg2')
source=("ftp://ftp.gnupg.org/gcrypt/gnupg/gnupg-$pkgver.tar.bz2"
        'protect-tool-env.patch')
md5sums=('1c30b3aa1f99f17b4988e1ab616355d4'
         '7e5167aa042dbe18ab79d8b0df3f3cab')

build() {
	cd ${srcdir}/gnupg-$pkgver
	patch -p1 -i ${srcdir}/protect-tool-env.patch 
	
	./configure --prefix=/usr \
              --sysconfdir=/etc \
	      --libexecdir=/usr/lib/gnupg \
	      --enable-maintainer-mode \
	      --enable-symcryptrun \
	      --enable-gpgtar 
  make
}

check() {
	cd "${srcdir}/gnupg-${pkgver}"
	
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	
	ln -s gpg2 "${pkgdir}"/usr/bin/gpg
	ln -s gpgv2 "${pkgdir}"/usr/bin/gpgv
	ln -s gpg2.1.gz "${pkgdir}"/usr/share/man/man1/gpg.1.gz
	rm "${pkgdir}/usr/share/gnupg/com-certs.pem" 
}

