
pkgname=libpcap
pkgver=1.6.1
pkgrel=1
pkgdesc="A system-independent interface for user-level packet capture"
arch=('x86_64')
url="http://www.tcpdump.org/"
license=('BSD')
depends=('glibc' 'libnl' 'sh' 'libusb')
makedepends=('flex') # 'bluez' 'linux-headers')
source=("http://www.tcpdump.org/release/libpcap-${pkgver}.tar.gz")
md5sums=('5eb05edf6b6c6e63d536d1c9fbfb2f7c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  ./configure --prefix=/usr --enable-ipv6 --with-libnl # --enable-bluetooth
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  install -d -m755 ${pkgdir}/usr/bin
  make DESTDIR=${pkgdir} install
  # remove static library
  rm -rf ${pkgdir}/usr/lib/libpcap.a
  
  # backwards compatibility, programs often look for net/bpf.h
  mkdir -p ${pkgdir}/usr/include/net
  cd ${pkgdir}/usr/include/net
  ln -s ../pcap-bpf.h bpf.h

  #install the license
  install -D -m644 ${srcdir}/$pkgname-$pkgver/LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}

