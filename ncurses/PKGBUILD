
pkgname=ncurses
pkgver=5.9
pkgrel=6
pkgdesc="System V Release 4.0 curses emulation library"
arch=('x86_64')
url="http://www.gnu.org/software/ncurses/"
license=('MIT')
depends=('glibc')
source=("ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('8cb9c412e5f2d96bc6f459aa8c6282a1')

build() {
  cd "${srcdir}"
  mkdir -p ncurses{,w}-build
  
  cd "${srcdir}/ncursesw-build"
  ${srcdir}/${pkgname}-${pkgver}/configure --prefix=/usr \
      --mandir=/usr/share/man \
      --with-shared \
      --with-normal \
      --without-debug \
      --without-ada \
      --enable-widec \
      --enable-pc-files
  make 

  # external binary support 
  cd ${srcdir}/ncurses-build
  ${srcdir}/${pkgname}-${pkgver}/configure --prefix=/usr \
      --with-shared \
      --with-normal \
      --without-debug \
      --without-ada \
      --with-chtype=long
  make 
}

package() {
  cd "${srcdir}/ncursesw-build"
  make DESTDIR=${pkgdir} install

  # link to non-wide-character ncurses libraries
  for lib in curses ncurses form panel menu ; do \
    echo "INPUT(-l${lib}w)" >"${pkgdir}/usr/lib/lib${lib}.so" ; \
    ln -s "lib${lib}w.a" "${pkgdir}/usr/lib/lib${lib}.a" ; \
  done
  ln -s libncurses++w.a "${pkgdir}/usr/lib/libncurses++.a"
  
  for lib in ncurses ncurses++ form panel menu; do
    ln -s ${lib}w.pc ${pkgdir}/usr/lib/pkgconfig/${lib}.pc
  done

  # look for -lcurses during build
  echo "INPUT(-lncursesw)" >"${pkgdir}/usr/lib/libcursesw.so"
  ln -sf libncurses.so "${pkgdir}/usr/lib/libcurses.so"
  ln -sf libncursesw.a "${pkgdir}/usr/lib/libcursesw.a"
  ln -sf libncurses.a "${pkgdir}/usr/lib/libcurses.a"

   # non-widec compatibility libraries
  cd ${srcdir}/ncurses-build
  for lib in ncurses form panel menu; do
    install -Dm755 lib/lib${lib}.so.${pkgver} ${pkgdir}/usr/lib/lib${lib}.so.${pkgver}
    ln -s lib${lib}.so.${pkgver} ${pkgdir}/usr/lib/lib${lib}.so.5
  done

  # install license
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  grep -B 100 '$Id' README > "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}

