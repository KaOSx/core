
pkgname=fakeroot
pkgver=1.24
pkgrel=1
pkgdesc="Gives a fake root environment, useful for building packages as a non-privileged user."
arch=('x86_64')
license=('GPL')
url="https://packages.debian.org/fakeroot"
groups=('base-devel')
install="fakeroot.install"
depends=('glibc' 'filesystem' 'util-linux' 'sed' 'sh')
options=('!libtool')
source=("https://deb.debian.org/debian/pool/main/f/fakeroot/${pkgname}_${pkgver}.orig.tar.gz"
        "dlsym.patch")
md5sums=('964e5f438f1951e5a515dd54edd50fa6'
         '62f6a56ee03486d2d8d178b922c612cd')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/dlsym.patch

  #./bootstrap
  ./configure --prefix=/usr \
        --libdir=/usr/lib/libfakeroot \
        --disable-static \
        --with-ipc=sysv

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -d -m755 ${pkgdir}/etc/ld.so.conf.d
  echo '/usr/lib/libfakeroot' > ${pkgdir}/etc/ld.so.conf.d/fakeroot.conf
  
  install -D -m644 ${srcdir}/${pkgname}-${pkgver}/README ${pkgdir}/usr/share/doc/${pkgname}/README
}

