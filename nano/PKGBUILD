
pkgname=nano
pkgver=2.5.2
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
license=('GPL')
url="http://www.nano-editor.org"
groups=('base')
depends=('glibc' 'ncurses' 'sh')
makedepends=('groff')
source=("http://www.nano-editor.org/dist/v2.5/nano-${pkgver}.tar.gz")
backup=(etc/nanorc)
install=nano.install
md5sums=('036830de22b8a76a0e6f84c0cb36ff12')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-color \
              --enable-nanorc \
	      --enable-multibuffer \
	      --disable-wrapping-as-root
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
  install -DTm644 ${srcdir}/${pkgname}-${pkgver}/doc/nanorc.sample ${pkgdir}/etc/nanorc
}
