
pkgname=nano
pkgver=2.4.3
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
license=('GPL')
url="http://www.nano-editor.org"
groups=('base')
depends=('glibc' 'ncurses' 'sh')
source=("http://www.nano-editor.org/dist/v2.4/nano-${pkgver}.tar.gz")
backup=(etc/nanorc)
install=nano.install
md5sums=('0acc0ca68eee68ef38f899532a4873c6')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-color \
              --enable-nanorc \
	      --enable-multibuffer \
	      --disable-wrapping-as-root
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
  install -DTm644 ${srcdir}/${pkgname}-${pkgver}/doc/nanorc.sample ${pkgdir}/etc/nanorc
}
