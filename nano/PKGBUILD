
pkgname=nano
pkgver=7.2
_pkgver=7
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
license=('GPL')
url="https://www.nano-editor.org"
groups=('base')
depends=('glibc' 'ncurses' 'sh')
makedepends=('groff')
source=("https://www.nano-editor.org/dist/v${_pkgver}/nano-${pkgver}.tar.xz")
backup=(etc/nanorc)
md5sums=('13742c686c6ddb0b7b294634f0c13cec')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --enable-color \
        --enable-nanorc \
        --enable-multibuffer \
        --disable-wrapping-as-root
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -DTm644 ${srcdir}/${pkgname}-${pkgver}/doc/sample.nanorc ${pkgdir}/etc/nanorc
}
