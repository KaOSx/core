
pkgname=docbook-xsl
pkgver=1.79.1
pkgrel=2
pkgdesc="XML stylesheets for Docbook-xml transformations."
arch=('x86_64')
license=('custom')
url="http://scrollkeeper.sourceforge.net/docbook.shtml"
depends=('libxml2' 'libxslt' 'docbook-xml')
source=("http://downloads.sourceforge.net/sourceforge/docbook/docbook-xsl-${pkgver}.tar.bz2")
install=docbook-xsl.install
md5sums=('b48cbf929a2ad85e6672f710777ca7bc')
groups=('base-devel')

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	_pkgroot=${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}

        install -dm755 ${_pkgroot}
	install -m644 VERSION VERSION.xsl ${_pkgroot}

	for fn in assembly common eclipse epub epub3 fo highlighting html htmlhelp javahelp lib \
                  manpages params profiling roundtrip template website xhtml xhtml-1_1 xhtml5; do
		install -dm755 ${_pkgroot}/${fn}
		install -m644 ${fn}/*.{xml,xsl,dtd,ent} ${_pkgroot}/${fn} || true  # ignore missing files
	done

	install -dm755 ${pkgdir}/etc/xml
	install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
