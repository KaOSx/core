
pkgname=docbook-xsl
pkgver=1.79.2
pkgrel=4
pkgdesc="XML stylesheets for Docbook-xml transformations."
arch=('x86_64')
license=('custom')
url="https://github.com/docbook"
depends=('libxml2' 'libxslt' 'docbook-xml')
install=docbook-xsl.install
groups=('base-devel')
source=("https://github.com/docbook/xslt10-stylesheets/releases/download/release%2F${pkgver}/docbook-xsl-${pkgver}.tar.bz2"
        'non-recursive-string.patch')
md5sums=('4a400f0264a19329c7f95f69e098744a'
         '4cb5c9bc0e39516921f56f57301341b0')

package() {
    cd ${pkgname}-${pkgver}
    patch -p2 -i ${srcdir}/non-recursive-string.patch

    _pkgroot=${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}

    install -dm755 ${_pkgroot}
    install -m644 VERSION VERSION.xsl ${_pkgroot}

    for fn in assembly common eclipse epub epub3 fo highlighting html htmlhelp javahelp lib \
                  manpages params profiling roundtrip template website xhtml xhtml-1_1 xhtml5; do
        install -dm755 ${_pkgroot}/${fn}
        install -m644 ${fn}/*.{xml,xsl,dtd,ent} ${_pkgroot}/${fn} || true  # ignore missing files
    done

    install -dm755 ${pkgdir}/etc/xml
    install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
