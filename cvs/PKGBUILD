#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

pkgname=cvs
pkgver=1.11.23
pkgrel=11
pkgdesc="Concurrent Versions System - a source control system"
arch=('x86_64')
url="http://cvs.nongnu.org"
license=('GPL')
depends=('krb5')
optdepends=('openssh: for using cvs over ssh' 'inetutils: for using cvs over rsh')
install=cvs.install
source=("ftp://ftp.gnu.org/non-gnu/cvs/source/stable/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
	'cvs-1.11.23-getline64.patch'
        'cvs-1.11.23-cve-2010-3846.patch')
md5sums=('0213ea514e231559d6ff8f80a34117f0'
         '943f10d93fbadaea0ac54553dd85c03f'
         'b089d3792c225857f00c3eee33809a16')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  unset EDITOR VISUAL

  patch -p1 -i ../cvs-1.11.23-getline64.patch

  # CVE-2010-3864, see https://www.redhat.com/security/data/cve/CVE-2010-3846.html
  patch -p1 -i ../cvs-1.11.23-cve-2010-3846.patch

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}"/usr/share/info/dir
}
