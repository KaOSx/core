#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer : abveritas@chakra-project.org
# maintainer : Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=('ruby' 'ruby-docs')
pkgver=1.9.3_p392
_pkgver=1.9.3-p392
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming"
arch=('x86_64')
url="http://www.ruby-lang.org/en/"
license=('custom')
makedepends=('gdbm' 'db' 'openssl' 'zlib' 'readline' 'tk' 'libffi'
             'doxygen' 'graphviz' 'libyaml')
options=('!emptydirs')
source=("ftp://ftp.ruby-lang.org/pub/ruby/1.9/${pkgname}-${_pkgver}.tar.bz2")
md5sums=('a810d64e2255179d2f334eb61fb8519c')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --enable-shared \
      --enable-pthread \
      --disable-rpath
  make
}

package_ruby() {
  depends=('gdbm' 'db' 'openssl' 'zlib' 'readline' 'libffi' 'libyaml')
  provides=('rubygems' 'rake')
  conflicts=('rubygems' 'rake')
  optdepends=('tk: for Ruby/TK')

  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install-nodoc
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_ruby-docs() {
  pkgdesc="Documentation files for ruby"

  depends=('ruby')

  cd "${srcdir}/ruby-${_pkgver}"
  make -j1 DESTDIR="${pkgdir}" install-doc install-capi
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/ruby-docs/LICENSE"
  install -D -m644 BSDL "${pkgdir}/usr/share/licenses/ruby-docs/BSDL"
}
