
pkgname=('ruby' 'ruby-docs')
pkgver=2.3.1
_pkgver=2.3.1
pkgrel=2
pkgdesc="An object-oriented language for quick and easy programming"
arch=('x86_64')
url="http://www.ruby-lang.org/en/"
license=('custom')
makedepends=('gdbm' 'db' 'openssl' 'zlib' 'readline' 'tk' 'libffi'
             'doxygen' 'libyaml')
options=('!emptydirs')
source=("http://cache.ruby-lang.org/pub/ruby/2.3/${pkgname}-${_pkgver}.tar.xz")
md5sums=('01e9d172a5c33b385e92fc0cc2899766')

build() {
  cd ${pkgname}-${_pkgver}
  
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --enable-shared \
      --disable-rpath
  make
}

package_ruby() {
  depends=('gdbm' 'db' 'openssl' 'zlib' 'readline' 'libffi' 'libyaml')
  provides=('rubygems' 'rake')
  conflicts=('rubygems' 'rake')
  optdepends=('tk: for Ruby/TK')

  cd ${pkgname}-${_pkgver}
  make DESTDIR=${pkgdir} install-nodoc
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

package_ruby-docs() {
  pkgdesc="Documentation files for ruby"
  depends=('ruby')

  cd ruby-${_pkgver}
  make -j1 DESTDIR=${pkgdir} install-doc install-capi
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/ruby-docs/LICENSE
  install -D -m644 BSDL ${pkgdir}/usr/share/licenses/ruby-docs/BSDL
}
