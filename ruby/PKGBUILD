
pkgname=('ruby' 'ruby-docs')
pkgver=3.3.6
_pkgver=3.3
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming"
arch=('x86_64')
url="https://www.ruby-lang.org/en/"
license=('custom')
depends=('libxcrypt')
makedepends=('gdbm' 'openssl' 'zlib' 'readline' 'tk' 'libffi' 'doxygen' 'libyaml')
options=('!emptydirs')
source=("https://cache.ruby-lang.org/pub/ruby/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('540975969d1af42190d26ff629bc93b1c3f4bffff4ab253e245e125085e66266')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --enable-shared \
      --disable-rpath
  make
}

package_ruby() {
  depends=('gdbm' 'openssl' 'zlib' 'readline' 'libffi' 'libyaml')
  provides=('rubygems' 'rake')
  conflicts=('rubygems' 'rake')
  optdepends=('tk: for Ruby/TK')

  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install-nodoc
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

package_ruby-docs() {
  pkgdesc="Documentation files for ruby"
  depends=('ruby')

  cd ruby-${pkgver}
  make -j1 DESTDIR=${pkgdir} install-doc install-capi
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/ruby-docs/LICENSE
  install -D -m644 BSDL ${pkgdir}/usr/share/licenses/ruby-docs/BSDL
}
