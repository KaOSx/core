
_grub_lua_ver=24

pkgname=grub-efi-x86_64
pkgver=2.00
pkgrel=1
pkgdesc="GNU GRand Unified Bootloader (2) - x86_64 UEFI Modules"
url="https://www.gnu.org/software/grub/"
arch=('x86_64')
license=('GPL3')
depends=("grub-common=${pkgver}" 'dosfstools' 'efibootmgr')
makedepends=('xz' 'python' 'autogen' 'texinfo' 'help2man' 'gettext' 'device-mapper' 'fuse')
options=('!strip' '!emptydirs')
replaces=('grub2-efi-x86_64')
provides=('grub2-efi-x86_64')

source=("http://ftp.gnu.org/gnu/grub/grub-${pkgver}.tar.xz"
        "ftp://ftp.archlinux.org/other/grub2/grub2_extras_lua_r${_grub_lua_ver}.tar.xz")

noextract=("grub2_extras_lua_r${_grub_lua_ver}.tar.xz")

md5sums=('a1043102fbc7bcedbf53e7ee3d17ab91'
         '704ea9f250b6137f05fa0197fd07053a')

build() {
	cd "${srcdir}/grub-${pkgver}"

	## add grub-extras
	export GRUB_CONTRIB="${srcdir}/grub-${pkgver}/grub-extras/" 
	install -d "${srcdir}/grub-${pkgver}/grub-extras"

	bsdtar xf "${srcdir}/grub2_extras_lua_r${_grub_lua_ver}.tar.xz" \
		-C "${srcdir}/grub-${pkgver}/grub-extras"
		
	sed -i -e '/gets is a security/d' "${srcdir}/grub-2.00/grub-core/gnulib/stdio.in.h"

	## start the actual build process
	cd "${srcdir}/grub-${pkgver}/"
	./autogen.sh

	CFLAGS="" ./configure \
		--with-platform="efi" \
		--target="x86_64" \
		--host="${CARCH}-unknown-linux-gnu" \
		--disable-efiemu \
		--enable-mm-debug \
		--enable-nls \
		--enable-device-mapper \
		--enable-cache-stats \
		--enable-grub-mkfont \
		--enable-grub-mount \
		--prefix="/usr" \
		--bindir="/usr/bin" \
		--sbindir="/usr/sbin" \
		--mandir="/usr/share/man" \
		--infodir="/usr/share/info" \
		--datarootdir="/usr/share" \
		--sysconfdir="/etc" \
		--program-prefix="" \
		--with-bootdir="/boot" \
		--with-grubdir="grub" \
		--disable-werror

	CFLAGS="" make

}

package() {

	cd "${srcdir}/grub-${pkgver}"
	make DESTDIR="${pkgdir}/" install 

	## remove non platform-specific files
	rm -rf "${pkgdir}"/{boot,etc,usr/{share,bin,sbin}}

	## remove gdb debugging related files
	rm -f "${pkgdir}/usr/lib/grub/x86_64-efi"/*.module || true
	rm -f "${pkgdir}/usr/lib/grub/x86_64-efi"/*.image || true
	rm -f "${pkgdir}/usr/lib/grub/x86_64-efi"/{kernel.exec,gdb_grub,gmodule.pl} || true

}
