
pkgname=libinih
pkgver=56
pkgrel=2
pkgdesc='INI Not Invented Here is a simple .INI file parser written in C'
arch=('x86_64')
url='https://github.com/benhoyt/inih'
license=('BSD')
depends=('gcc-libs')
makedepends=('meson' 'ninja')
source=("https://github.com/benhoyt/inih/archive/r${pkgver}.tar.gz")
md5sums=('e27e394554d9f339b728035a5766d8f3')

build() {
  mkdir -p build
  cd build

  meson setup ../inih-r${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Ddistro_install=true \
    -Dwith_INIReader=true
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
  install -Dm 644 ../inih-r${pkgver}/LICENSE.txt -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
