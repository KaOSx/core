
pkgname=libinih
pkgver=54
pkgrel=1
pkgdesc='INI Not Invented Here is a simple .INI file parser written in C'
arch=('x86_64')
url='https://github.com/benhoyt/inih'
license=('BSD')
depends=('gcc-libs')
makedepends=('meson' 'ninja')
source=("https://github.com/benhoyt/inih/archive/r${pkgver}.tar.gz")
md5sums=('d1874d94262c5d52ba28b5f2bc59c77b')

build() {
  mkdir -p build
  cd build

  meson ../inih-r${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Ddistro_install=true \
    -Dwith_INIReader=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
  install -Dm 644 ../inih-r${pkgver}/LICENSE.txt -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
