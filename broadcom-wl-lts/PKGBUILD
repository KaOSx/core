#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer : abveritas@chakra-project.org

# Find the kernel name inside the chroot
_extramodules=extramodules-3.0-lts
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=broadcom-wl-lts
pkgver=5.100.82.112
pkgrel=6
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
arch=('x86_64')
url='http://www.broadcom.com/support/802.11/linux_sta.php'
license=('custom')
depends=('linux-lts>=3.0' 'linux-lts<3.1')
makedepends=('linux-lts-headers')
backup=('etc/modprobe.d/broadcom-wl-lts.conf')
install=install
source=("http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_x86_64-v${pkgver//./_}.tar.gz"
        'modprobe.d'
        'linux3.patch'
        'license.patch'
        'semaphore.patch')
sha1sums=('5bd78c20324e6a4aa9f3fafdc6f0155e884d5131'
          '89bf92286ede30dd85304c6c4e42e89cfdc0f60a'
          '3e18f905bbe5e8b99a53d6ecb3b80a919f3531f2'
          'ea7b67982ddc0f56fd3becb9914fd4458fe7d373'
          '105f8e1d48ebe1f25d53859e5ab9326a27435c66')

build() {
	cd "${srcdir}"

	patch -p1 -i linux3.patch
	patch -p1 -i license.patch
	patch -p1 -i semaphore.patch

	make -C /lib/modules/"${_kver}"/build M=`pwd`
}

package() {
	cd "${srcdir}"

	install -D -m 755 wl.ko "${pkgdir}/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/lib/modules/${_extramodules}/wl.ko"

	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl-lts.conf
}