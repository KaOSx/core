# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=linux-api-headers
pkgver=5.15.18
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('x86_64')
url="https://www.gnu.org/software/libc"
license=('GPL2')
provides=("kernel-headers=${pkgver}")
conflicts=('kernel-headers')
replaces=('kernel-headers')
source=("https://www.kernel.org/pub/linux/kernel/v5.x/linux-${pkgver}.tar.xz")
sha256sums=('21dd70e84d776a3403836d435bc523059a43f5bce72c75728981126dd9786c5e')

build() {
  cd linux-${pkgver}

  make mrproper
  make headers_check
}

package() {
  cd linux-${pkgver}
  make INSTALL_HDR_PATH=${pkgdir}/usr headers_install

  # mostly duplicate headers from libdrm
  rm -r ${pkgdir}/usr/include/drm
}
