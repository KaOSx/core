
pkgbase="sqlite"
pkgname=('sqlite' 'sqlite-doc') 
_pkgver=3470000
_docver=3470000
pkgver=3.47.0
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('x86_64')
license=('custom')
url="https://www.sqlite.org/"
makedepends=('tcl' 'readline')
source=("https://www.sqlite.org/2024/sqlite-src-${_pkgver}.zip"
        "https://www.sqlite.org/2024/sqlite-doc-${_docver}.zip"
        'license.txt')
options=('!libtool' '!emptydirs' '!makeflags')
sha1sums=('bf0843f48988265a563a697ea765aa4edaa6bd80'
          '614b2236f37c852cb5619c854699cbd31c43e453'
          'f34f6daa4ab3073d74e774aad21d66878cf26853')

build() {
  export CFLAGS="$CFLAGS -DSQLITE_ENABLE_COLUMN_METADATA=1 \
                         -DSQLITE_ENABLE_UNLOCK_NOTIFY \
                         -DSQLITE_SECURE_DELETE \
                         -DSQLITE_ENABLE_DBSTAT_VTAB=1"
  export TCLLIBDIR="/usr/lib/sqlite-${pkgver}"

  # sqlite
  cd sqlite-src-${_pkgver}
  ./configure --prefix=/usr \
	--disable-static \
	--enable-fts5 
  make
}

package_sqlite() {

 pkgdesc="A C library that implements an SQL database engine"
 depends=('readline')
 provides=("sqlite3=$pkgver" 'sqlite-tcl')
 replaces=('sqlite3' 'sqlite-tcl')

  cd sqlite-src-${_pkgver}
  make DESTDIR=${pkgdir} install

  install -D -m644 ../license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}

package_sqlite-doc() {
 pkgdesc="Documentation as a bundle of static HTML files, including all of the SQL Syntax and the C/C++ interface specs."
 provides=("sqlite3-doc=$pkgver")
 replaces=("sqlite3-doc")

  cd ${srcdir}/sqlite-doc-${_docver}
  mkdir -p ${pkgdir}/usr/share/doc/${pkgbase}
  cp -R *  ${pkgdir}/usr/share/doc/${pkgbase}/

  # fix permissions and remove obsolete files
  find ${pkgdir} -type f -perm 755 -exec ls -lha {} \;
  find ${pkgdir} -type f -perm 755 -exec chmod 644 {} \;

  find ${pkgdir} -type f -name '*~' -exec ls -lha {} \;
  find ${pkgdir} -type d -name '*~' -exec ls -lha {} \;
  find ${pkgdir} -name '*~' -exec rm -f {} \;

  find ${pkgdir} -type f -name '.~*' -exec ls -lha {} \; # /build/pkg/sqlite-doc/usr/share/doc/sqlite3/images/fileformat/.~lock.indexpage.odg#
  find ${pkgdir} -type d -name '.~*' -exec ls -lha {} \;
  find ${pkgdir} -name '.~*' -exec rm -f {} \;
}

