
pkgname=xz
pkgver=5.6.0
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('x86_64')
url='https://xz.tukaani.org/xz-utils/'
license=('GPL' 'LGPL' '0BSD')
depends=('sh')
options=('!libtool')
source=("https://github.com/tukaani-project/xz/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'xzgrep-ZDI-CAN-16587.patch')
md5sums=('cfb1afdfcfeca02f7677b1b401bc536e'
         'a194a4240198e221b7d0249dc2c1fe06')

build() {
    cd ${pkgname}-${pkgver}
    # https://www.mail-archive.com/xz-devel@tukaani.org/msg00551.html
    #patch -p1 -i ${srcdir}/xzgrep-ZDI-CAN-16587.patch
    #export CFLAGS="$CFLAGS -Wno-format-truncation"

    ./configure --prefix=/usr \
        --disable-rpath \
        --enable-werror
    make
}

check() {
    cd ${pkgname}-${pkgver}

    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install

    install -d -m755 ${pkgdir}/usr/share/licenses/xz/
    ln -sf /usr/share/doc/xz/COPYING ${pkgdir}/usr/share/licenses/xz/
    ln -sf /usr/share/licenses/common/GPL2/license.txt ${pkgdir}/usr/share/doc/xz/COPYING.GPLv2
}

