 
pkgbase=boost
pkgname=('boost-libs' 'boost')
pkgver=1.83.0
_pkgver=1_83_0
pkgrel=2
pkgdesc="Free peer-reviewed portable C++ source libraries"
arch=('x86_64')
url="https://www.boost.org/"
license=('custom')
makedepends=('icu' 'python3' 'bzip2' 'zlib' 'openmpi' 'zstd') 
source=("https://boostorg.jfrog.io/artifactory/main/release/${pkgver}/source/${pkgbase}_${_pkgver}.tar.bz2"
        'boost-1.81.0-phoenix-multiple-definitions.patch')
license=('custom')
sha256sums=('6478edfe2f3305127cffe8caf73ea0176c53769f4bf1585be237eb30798c3b8e'
            '9f83056a04f42d94ed477365194dbef674b7ad9e0661a7ea7392fb03207eaba0')

build() {
  export _stagedir=${srcdir}/stagedir
  local JOBS="$(sed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"

  cd ${pkgbase}_${_pkgver}

  # https://github.com/boostorg/phoenix/issues/111
  patch -p1 -i ${srcdir}/boost-1.81.0-phoenix-multiple-definitions.patch
  ./bootstrap.sh --with-toolset=gcc --with-icu --with-python=/usr/bin/python3

  # tools
  install -d -m 755 ${_stagedir}/bin
  install tools/build/src/engine/b2 ${_stagedir}/bin/b2

   sed -i 's|using python : 3.10 : "/usr" ;|using python : 3.10 : /usr/bin/python3 : /usr/include/python3.10 : /usr/lib ;|g' project-config.jam 
   # Support for OpenMPI
   echo "using mpi ;" >> project-config.jam
  
  #quickbook support
  install -d -m 755 ${_stagedir}/share/boostbook
  cp -a tools/boostbook/{xsl,dtd} ${_stagedir}/share/boostbook/

  # ${_stagedir} for split package
  ${_stagedir}/bin/b2 \
      variant=release \
      debug-symbols=off \
      threading=multi \
      runtime-link=shared \
      link=shared,static \
      toolset=gcc \
      python=3.10 \
      cflags="${CPPFLAGS} ${CFLAGS} -O3" linkflags="${LDFLAGS}" \
      --prefix=${_stagedir} \
      --layout=system \
      ${JOBS} \
      install
}

package_boost() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Development"
    depends=("boost-libs=${pkgver}" 'python3')

     export _stagedir=${srcdir}/stagedir

    install -d -m 755 ${pkgdir}/usr
    cp -a ${_stagedir}/{bin,include,share} ${pkgdir}/usr

    install -d ${pkgdir}/usr/lib
    cp -a ${_stagedir}/lib/*.a ${pkgdir}/usr/lib/
    cp -a ${_stagedir}/lib/cmake ${pkgdir}/usr/lib/
    # Don't set to cmake files to python37, does not exists
    sed -i 's|${CMAKE_MATCH_2}.${CMAKE_MATCH_3}|${CMAKE_MATCH_2}|' ${pkgdir}/usr/lib/cmake/Boost-${pkgver}/BoostConfig.cmake

    # license
    install -D -m644 ${pkgbase}_${_pkgver}/LICENSE_1_0.txt ${pkgdir}/usr/share/licenses/boost/LICENSE_1_0.txt

    ln -s /usr/bin/b2 ${pkgdir}/usr/bin/bjam
}

package_boost-libs() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime"
    depends=('gcc-libs' 'bzip2' 'zlib' 'icu')
    optdepends=('openmpi: for mpi support')
    options=('!staticlibs')

     export _stagedir=${srcdir}/stagedir

    install -d -m 755 ${pkgdir}/usr
    cp -a ${_stagedir}/lib ${pkgdir}/usr
    # included in boost:
    rm ${pkgdir}/usr/lib/*.a
    rm -r ${pkgdir}/usr/lib/cmake

    # issue with finding new python lib versioning, vigra exposed, add link for old scheme
    ln -s /usr/lib/libboost_python310.so.${pkgver} ${pkgdir}/usr/lib/libboost_python3.so

    # license
    install -D -m644 ${pkgbase}_${_pkgver}/LICENSE_1_0.txt ${pkgdir}/usr/share/licenses/boost-libs/LICENSE_1_0.txt
}

