 
pkgbase=boost
pkgname=('boost-libs' 'boost')
pkgver=1.71.0
_pkgver=1_71_0
pkgrel=1
pkgdesc="Free peer-reviewed portable C++ source libraries"
arch=('x86_64')
url="http://www.boost.org/"
license=('custom')
makedepends=('icu' 'python3' 'bzip2' 'zlib' 'openmpi' 'zstd') 
source=("https://dl.bintray.com/boostorg/release/${pkgver}/source/${pkgbase}_${_pkgver}.tar.bz2"
        "python.diff")
license=('custom')
sha256sums=('d73a8da01e8bf8c7eda40b4c84915071a8c8a0df4a6734537ddde4a8580524ee'
            '3984cfb1922a909d65794f173068da59fb2a7c4efdaf42911c3add329a2b9789')

build() {
  export _stagedir=${srcdir}/stagedir
  local JOBS="$(sed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"

  cd ${pkgbase}_${_pkgver}
  
  ./bootstrap.sh --with-toolset=gcc --with-icu --with-python=/usr/bin/python3

  # tools
  install -d -m 755 ${_stagedir}/bin
  install tools/build/src/engine/b2 ${_stagedir}/bin/b2

   sed -i 's|using python : 3.7 : "/usr" ;|using python : 3.7 : /usr/bin/python3 : /usr/include/python3.7m : /usr/lib ;|g' project-config.jam 
   # Support for OpenMPI
   echo "using mpi ;" >> project-config.jam
  
  #quickbook support
  install -d -m 755 ${_stagedir}/share/boostbook
  cp -a tools/boostbook/{xsl,dtd} ${_stagedir}/share/boostbook/

  # ${_stagedir} for split package
  ${_stagedir}/bin/b2 \
      variant=release \
      debug-symbols=off \
      threading=multi \
      runtime-link=shared \
      link=shared,static \
      toolset=gcc \
      python=3.7 \
      cflags="${CPPFLAGS} ${CFLAGS} -O3" linkflags="${LDFLAGS}" \
      --prefix=${_stagedir} \
      --layout=system \
      ${JOBS} \
      install
}

package_boost() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Development"
    depends=("boost-libs=${pkgver}" 'python3')

    install -d -m 755 ${pkgdir}/usr
    cp -a ${_stagedir}/{bin,include,share} ${pkgdir}/usr
    
    install -d ${pkgdir}/usr/lib
    cp -a ${_stagedir}/lib/*.a ${pkgdir}/usr/lib/
    cp -a ${_stagedir}/lib/cmake ${pkgdir}/usr/lib/

    # license
    install -D -m644 ${pkgbase}_${_pkgver}/LICENSE_1_0.txt ${pkgdir}/usr/share/licenses/boost/LICENSE_1_0.txt
        
    ln -s /usr/bin/b2 ${pkgdir}/usr/bin/bjam
}

package_boost-libs() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime"
    depends=('gcc-libs' 'bzip2' 'zlib' 'icu')
    optdepends=('openmpi: for mpi support')
    options=('!staticlibs')

    install -d -m 755 ${pkgdir}/usr
    cp -a ${_stagedir}/lib ${pkgdir}/usr
    # included in boost:
    rm ${pkgdir}/usr/lib/*.a
    rm -r ${pkgdir}/usr/lib/cmake
    
    # issue with finding new python lib versioning, vigra exposed, add link for old scheme
    ln -s /usr/lib/libboost_python37.so.${pkgver} ${pkgdir}/usr/lib/libboost_python3.so

    # license
    install -D -m644 ${pkgbase}_${_pkgver}/LICENSE_1_0.txt ${pkgdir}/usr/share/licenses/boost-libs/LICENSE_1_0.txt
}

