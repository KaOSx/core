 
pkgbase=boost
pkgname=('boost-libs' 'boost')
pkgver=1.55.0
_boostver=${pkgver//./_}
pkgrel=1
pkgdesc="Free peer-reviewed portable C++ source libraries"
arch=('x86_64')
url="http://www.boost.org/"
license=('custom')
makedepends=('icu>=52.1' 'python2' 'python3' 'bzip2' 'zlib' 'openmpi') 
source=("http://downloads.sourceforge.net/${pkgbase}/${pkgbase}_${_boostver}.tar.gz"
        '001-log_fix_dump_avx2.patch')
license=('custom')
md5sums=('93780777cfbf999a600f62883bd54b17'
         'acbc980cd1e8c7dfdbaa380abf95bdc4')

_stagedir="${srcdir}/stagedir"

build() {
  local JOBS="$(sed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"
  
  cd "${srcdir}/${pkgbase}_${_boostver}"
  patch -p0 -i ${srcdir}/001-log_fix_dump_avx2.patch
  
   # Add an extra python version. This does not replace anything and python 2.x need to be the default.
   echo "using python : 3.3 : /usr/bin/python3 : /usr/include/python3.3m : /usr/lib ;" >> ./tools/build/v2/user-config.jam
   # Support for OpenMPI
   echo "using mpi ;" >> ./tools/build/v2/user-config.jam

  cd "${srcdir}"/${pkgbase}_${_boostver}
  ./bootstrap.sh --with-toolset=gcc --with-icu --with-python=/usr/bin/python2

  # setup tools
  install -d -m 755 "${_stagedir}"/bin
  install tools/build/v2/engine/bin.linuxx86_64/b2 "${_stagedir}"/bin/b2

  # build & copy the tools
  pushd tools
   for _tool in bcp inspect quickbook compiler_status process_jam_log wave; do
      "${_stagedir}"/bin/b2 --toolset=gcc cflags="${CPPFLAGS} ${CFLAGS} -std=gnu++11 -O3" linkflags="${LDFLAGS}" $_tool
   done
      "${_stagedir}"/bin/b2 --toolset=gcc cflags="${CPPFLAGS} ${CFLAGS} -O3" linkflags="${LDFLAGS}" library_status
   popd
   cp -a dist/bin/* "${_stagedir}"/bin

  #boostbook needed by quickbook
  install -d -m 755 "${_stagedir}"/share/boostbook
  cp -a tools/boostbook/{xsl,dtd} "${_stagedir}"/share/boostbook/

  # default "minimal" install: "release link=shared
  # runtime-link=shared threading=single,multi"
  # --layout=tagged will add the "-mt" suffix for multithreaded libraries
  # and installs includes in /usr/include/boost.
  # --layout=system no longer adds the -mt suffix for multi-threaded libs.
  # install to ${_stagedir} in preparation for split packaging

  "${_stagedir}"/bin/b2 \
      variant=release \
      debug-symbols=off \
      threading=multi \
      runtime-link=shared \
      link=shared,static \
      toolset=gcc \
      python=2.7 \
      cflags="${CPPFLAGS} ${CFLAGS} -O3" linkflags="${LDFLAGS}" \
      --prefix="${_stagedir}" \
      --layout=system \
      ${JOBS} \
      install
}

package_boost() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Development"
    depends=("boost-libs=${pkgver}")
    optdepends=('python3: for python3 bindings'
                'python2: for python2 bindings')

    install -d -m 755 "${pkgdir}"/usr
    cp -a "${_stagedir}"/{bin,include,share} "${pkgdir}"/usr
    
    install -d "${pkgdir}"/usr/lib
    find "${_stagedir}"/lib -name \*.a -exec mv {} "${pkgdir}"/usr/lib \;

    # license
    install -D -m644 "${srcdir}/${pkgbase}_${_boostver}/LICENSE_1_0.txt" \
        "${pkgdir}"/usr/share/licenses/boost/LICENSE_1_0.txt
        
    ln -s /usr/bin/b2 "$pkgdir"/usr/bin/bjam
}

package_boost-libs() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime"
    depends=('gcc-libs' 'bzip2' 'zlib' 'icu')
    optdepends=('openmpi: for mpi support')
    options=('!staticlibs')

    install -d -m 755 "${pkgdir}"/usr
    cp -a "${_stagedir}"/lib "${pkgdir}"/usr

    # license
    install -D -m644 "${srcdir}/${pkgbase}_${_boostver}/LICENSE_1_0.txt" \
        "${pkgdir}"/usr/share/licenses/boost-libs/LICENSE_1_0.txt
}

