
pkgname=lzo2
pkgver=2.09
pkgrel=2
pkgdesc="Portable lossless data compression library"
arch=('x86_64')
url="http://www.oberhumer.com/opensource/lzo"
license=('GPL')
depends=('glibc')
conflicts=('lzo')
replaces=('lzo')
provides=('lzo')
source=("http://www.oberhumer.com/opensource/lzo/download/lzo-${pkgver}.tar.gz")
options=('!libtool')
md5sums=('c7ffc9a103afe2d1bba0b015e7aa887f')

build() {
  cd "${srcdir}/lzo-${pkgver}"

  ./configure --prefix=/usr --enable-shared
  make

  # minilzo
  gcc $CFLAGS -fpic -Iinclude/lzo -o minilzo/minilzo.o -c minilzo/minilzo.c
  gcc -g -shared -o libminilzo.so.0 -Wl,-soname,libminilzo.so.0 minilzo/minilzo.o
}

check() {
  cd "${srcdir}/lzo-${pkgver}"
  
  make test 
  make check
}

package() {
  cd "${srcdir}/lzo-${pkgver}"
  make DESTDIR=${pkgdir} install

  # minilzo
  install -m 755 libminilzo.so.0 ${pkgdir}/usr/lib
  install -p -m 644 minilzo/minilzo.h ${pkgdir}/usr/include/lzo
  cd ${pkgdir}/usr/lib
  ln -s libminilzo.so.0 libminilzo.so
}
