
pkgname=tzdata
pkgver=2023c
pkgrel=1
pkgdesc="Sources for time zone and daylight saving time data"
arch=('x86_64')
url="https://www.iana.org/time-zones"
license=('GPL')
options=('!emptydirs')
source=("https://www.iana.org/time-zones/repository/releases/${pkgname}${pkgver}.tar.gz")
sha512sums=('608bd286ebcbd0004cfdc1da183273f08aff61f90c8867661154453d77a05d421e4c46ad6d066a1fe2e87d5c82ec0f1c0224667a3b35f3180a3eb7f6ff84cbf5')

timezones=('africa' 'antarctica' 'asia' 'australasia'
           'europe' 'northamerica' 'southamerica'
           'etcetera' 'backward' 'factory')

package() {
  cd ${srcdir}
  zic -d ${pkgdir}/usr/share/zoneinfo ${timezones[@]}
  zic -d ${pkgdir}/usr/share/zoneinfo/posix ${timezones[@]}
  zic -d ${pkgdir}/usr/share/zoneinfo/right -L leapseconds ${timezones[@]}
  zic -d ${pkgdir}/usr/share/zoneinfo -p America/New_York
  install -m444 -t ${pkgdir}/usr/share/zoneinfo iso3166.tab zone.tab zone1970.tab
}
