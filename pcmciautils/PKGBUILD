
pkgname=pcmciautils
pkgver=018
pkgrel=10
pkgdesc="Utilities for inserting and removing PCMCIA cards"
arch=('x86_64')
url="https://mirrors.edge.kernel.org/pub/linux/utils/kernel/pcmcia/"
license=('GPL')
groups=('base')
depends=('sysfsutils' 'systemd')
conflicts=('pcmcia-cs')
options=('!makeflags')
source=("https://mirrors.edge.kernel.org/pub/linux/utils/kernel/pcmcia/${pkgname}-${pkgver}.tar.xz"
        'initcpio-install-pcmcia')
sha256sums=('57c27be8f04ef4d535bcfa988567316cc57659fe69068327486dab53791e6558'
            'dc3fd927dccebc4eac3fe920b3b6d8f81c177ec67a1d2996fc88391f12d2d79d')

build() {
  cd ${pkgname}-${pkgver}
  sed -i -e 's|/usr/bin/install|install|g;
             s|/lib/udev|/usr/lib/udev|g' Makefile
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -D -m644 ../initcpio-install-pcmcia ${pkgdir}/usr/lib/initcpio/install/pcmcia
}

