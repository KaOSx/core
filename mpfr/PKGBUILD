
pkgname=mpfr
_pkgver=3.1.5
_patchlevel=p5
pkgver=${_pkgver}.${_patchlevel}
#pkgver=${_pkgver}
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=('x86_64')
url="http://www.mpfr.org/"
license=('LGPL')
depends=('gmp>=6.0')
options=('!libtool')
source=("http://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz"
        # http://www.mpfr.org/mpfr-3.1.5/allpatches  http://www.mpfr.org/mpfr-3.1.5/#bugs
        "mpfr-${_pkgver}-${_patchlevel}.patch")
md5sums=('c4ac246cf9795a4491e7766002cd528f'
         'ddc463e02c11e2d03443c09dea7978b5')

build() {
  cd ${pkgname}-${_pkgver}
  patch -p1 -i $srcdir/mpfr-${_pkgver}-${_patchlevel}.patch
  
  ./configure --prefix=/usr --enable-thread-safe --enable-shared
  make
}

check() {
  cd ${pkgname}-${_pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${_pkgver}
  
  make DESTDIR=${pkgdir} install
}
