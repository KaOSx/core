
pkgname=pptpclient 
pkgver=1.10.0
pkgrel=2
pkgdesc="Client for the proprietary Microsoft Point-to-Point Tunneling Protocol, PPTP."
url="http://pptpclient.sourceforge.net/"
arch=('x86_64')
license=('GPL')
depends=('glibc' 'ppp' 'iproute2')
backup=('etc/ppp/options.pptp')
source=("https://sourceforge.net/projects/pptpclient/files/pptp/pptp-${pkgver}/pptp-${pkgver}.tar.gz")
md5sums=('8d25341352fdae5ad5b36b9f18254908')

 
build() { 
  cd pptp-${pkgver}
  
  sed -i -e "/CFLAGS  =/ c\CFLAGS = ${CFLAGS}" Makefile
  sed -i -e "/LDFLAGS =/ c\LDFLAGS = ${LDFLAGS}" Makefile
  sed -i 's|\/bin\/ip|\/usr\/sbin\/ip|g' routing.c 
  
  make
}

package() {
  cd pptp-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
