
pkgname=zstd
pkgver=1.4.9
pkgrel=1
pkgdesc='Zstandard is a real-time compression algorithm, providing high compression ratios.'
arch=('x86_64')
url='https://facebook.github.io/zstd/'
license=('BSD' 'GPL2')
depends=('lz4' 'xz' 'zlib')
makedepends=('googletest' 'cmake')
source=("https://github.com/facebook/zstd/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('29ac74e19ea28659017361976240c4b5c5c24db3b89338731a6feb97c038d293')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver}/build/cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DZSTD_BUILD_CONTRIB=ON \
    -DZSTD_BUILD_TESTS=ON
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ../${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
