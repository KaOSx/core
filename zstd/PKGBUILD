
pkgname=zstd
pkgver=1.5.5
pkgrel=1
pkgdesc='Zstandard is a real-time compression algorithm, providing high compression ratios.'
arch=('x86_64')
url='https://facebook.github.io/zstd/'
license=('BSD' 'GPL2')
depends=('lz4' 'xz' 'zlib')
makedepends=('googletest' 'cmake')
source=("https://github.com/facebook/zstd/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('9c4396cc829cfae319a6e2615202e82aad41372073482fce286fac78646d3ee4')

build() {
  cmake -B build -S ${pkgname}-${pkgver}/build/cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DZSTD_BUILD_CONTRIB=ON \
    -DZSTD_LZ4_SUPPORT=ON \
    -DZSTD_LZMA_SUPPORT=ON \
    -DZSTD_ZLIB_SUPPORT=ON \
    -DZSTD_BUILD_TESTS=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
