
pkgname=util-linux
pkgver=2.29
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux"
url="http://www.kernel.org/pub/linux/utils/util-linux/"
arch=('x86_64')
groups=('base')
depends=('pam' 'shadow' 'coreutils' 'glibc')
makedepends=('bc' 'systemd' 'python3')
conflicts=('util-linux-ng' 'eject')
provides=("util-linux-ng=${pkgver}" 'eject')
license=('GPL2')
options=('!libtool')
source=("ftp://ftp.kernel.org/pub/linux/utils/${pkgname}/v2.29/${pkgname}-${pkgver}.tar.xz"
        'uuidd.tmpfiles'
        'pam-login'
        'pam-common'
        'pam-su'
        'system-auth'
        'system-local-login'
        'system-login'
        'system-remote-login'
        'system-services')
backup=(etc/pam.d/chfn
        etc/pam.d/chsh
        etc/pam.d/login
        etc/pam.d/su
        etc/pam.d/su-l)
install=util-linux.install
md5sums=('07b6845f48a421ad5844aa9d58edb837'
         'a39554bfd65cccfd8254bb46922f4a67'
         '4368b3f98abd8a32662e094c54e7f9b1'
         'a31374fef2cba0ca34dfc7078e2969e4'
         'fa85e5cce5d723275b14365ba71a8aad'
         '6116b8e199a3dfd26a085a67a718435d'
         '477237985820117a0e6e1b13a86eb599'
         '34959361be0e1c13c24ce6af84e1eb22'
         '477237985820117a0e6e1b13a86eb599'
         '6969307eef026979703a6eba33c2e3eb')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --localstatedir=/run \
              --enable-fs-paths-extra=/usr/bin:/usr/sbin \
              --enable-write \
              --enable-raw \
              --enable-vipw \
              --enable-newgrp \
              --enable-chfn-chsh \
              --enable-mesg \
              --enable-socket-activation \
              --with-python=3
              #--enable-libmount-force-mountinfo systemd 230 need?

  make
}

# only 1 libmount: tab files-py subtest fails, leaving dep to test the rest
check() {
  make -C ${pkgname}-${pkgver} check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  cd ${pkgdir}
  
  chmod 4755 ${pkgdir}/usr/bin/{newgrp,ch{sh,fn}}

  install -Dm644 ${srcdir}/pam-common ${pkgdir}/etc/pam.d/chfn
  install -m644 ${srcdir}/pam-common ${pkgdir}/etc/pam.d/chsh
  install -m644 ${srcdir}/pam-login ${pkgdir}/etc/pam.d/login
  install -m644 ${srcdir}/pam-su ${pkgdir}/etc/pam.d/su
  install -m644 ${srcdir}/pam-su ${pkgdir}/etc/pam.d/su-l
  # Pam helper files
  install -m644 ${srcdir}/system-auth ${pkgdir}/etc/pam.d/system-auth
  install -m644 ${srcdir}/system-local-login ${pkgdir}/etc/pam.d/system-local-login
  install -m644 ${srcdir}/system-login ${pkgdir}/etc/pam.d/system-login
  install -m644 ${srcdir}/system-remote-login ${pkgdir}/etc/pam.d/system-remote-login
  install -m644 ${srcdir}/system-services ${pkgdir}/etc/pam.d/system-services

  install -Dm644 ${srcdir}/uuidd.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/uuidd.conf
  # fdisk fix for live-mode, until all moves to /usr
  ln -s /sbin/fdisk ${pkgdir}/usr/bin/fdisk
  # udisks2 fixes until all moves to /usr
  ln -s /bin/mount ${pkgdir}/usr/bin/mount
  ln -s /bin/umount ${pkgdir}/usr/bin/umount
}



