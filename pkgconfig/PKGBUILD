
pkgname=pkg-config
pkgver=0.29.1
pkgrel=2
pkgdesc="A system for managing library compile/link flags"
arch=('x86_64')
url="http://pkgconfig.freedesktop.org/wiki/"
license=('GPL')
groups=('base-devel')
depends=('glibc' 'glib2')
provides=("pkgconfig=${pkgver}")
conflicts=('pkgconfig')
replaces=('pkgconfig')
source=("http://pkg-config.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('f739a28cae4e0ca291f82d1d41ef107d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Use system popt
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
