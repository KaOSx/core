

pkgbase=glib2
pkgname=('glib2' 'glib2-docs')
pkgver=2.36.4
pkgrel=1
pkgdesc="Common C routines used by GTK+ and other libs"
url="http://www.gtk.org/"
arch=('x86_64')
makedepends=('pkg-config' 'python2' 'libxslt' 'docbook-xml' 'pcre' 'libffi')
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/2.36/glib-$pkgver.tar.xz"
        'revert-warn-glib-compile-schemas.patch')
md5sums=('2f4b15f7ef43d8702d067ab987bf7aba'
         '3265d1afbcf1f5f308e26c3f9f6ca899')

prepare() {
  cd glib-$pkgver
  
  patch -Rp1 -i ../revert-warn-glib-compile-schemas.patch
  touch -r NEWS gtk-doc.make
}
  
build() {
  cd glib-$pkgver
  
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --libdir=/usr/lib \
      --sysconfdir=/etc \
      --with-pcre=system \
      --disable-fam
  make
}

package_glib2() {
  depends=('pcre' 'libffi')
  options=('!docs' '!libtool' '!emptydirs')
  license=('LGPL')

  cd glib-$pkgver
  make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install

  for _i in "$pkgdir/usr/share/bash-completion/completions/"*; do
      chmod -x "$_i"
  done

  rm -rf "$pkgdir/usr/share/gdb/"
}

package_glib2-docs() {
  pkgdesc="Documentation for glib2"
  license=('custom')
  options=('docs' '!emptydirs')
  
  cd glib-$pkgver/docs
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/glib2-docs"
  install -m644 reference/COPYING "${pkgdir}/usr/share/licenses/glib2-docs/"

  rm -rf "${pkgdir}/usr/share/man"
}
