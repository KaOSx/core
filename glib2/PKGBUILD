
pkgname=glib2
pkgver=2.66.1
_pkgver=2.66
pkgrel=1
pkgdesc="Common C routines used by GTK+ and other libs"
url="https://www.gtk.org/overview.php"
arch=('x86_64')
license=('LGPL')
depends=('pcre' 'libffi')
makedepends=('pkg-config' 'python3' 'libxslt' 'docbook-xml' 'elfutils' 'meson' 'ninja')
options=('!docs' '!libtool' '!emptydirs')
conflicts=('glib2-docs')
provides=('glib2-docs')
source=("https://ftp.gnome.org/pub/GNOME/sources/glib/${_pkgver}/glib-${pkgver}.tar.xz"
        'gio-querymodules.hook'
        'glib-compile-schemes.hook'
        'revert_wakeups.patch')
sha256sums=('a269ffe69fbcc3a21ff1acb1b6146b2a5723499d6e2de33ae16ccb6d2438ef60'
            '557c88177f011ced17bdeac1af3f882b2ca33b386a866fdf900b35f927a2bbe8'
            '64ae5597dda3cc160fc74be038dbe6267d41b525c0c35da9125fbf0de27f9b25'
            '05635ffbeb5875fc5cb4adb4c8c85450693c36fc15e4c85f2f04fdbaf7e9d1e8')

prepare() {
  cd glib-${pkgver}
  
  #patch -p1 -i ../revert_wakeups.patch
}
  
build() {
  mkdir -p build
  cd build
  
  meson ../glib-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -D man=true \
    -D selinux=disabled
  ninja
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install
  
  install -dm755 ${pkgdir}/usr/share/libalpm/hooks/
  install -m644 ../glib-compile-schemes.hook ${pkgdir}/usr/share/libalpm/hooks/
  install -m644 ../gio-querymodules.hook ${pkgdir}/usr/share/libalpm/hooks/
}

