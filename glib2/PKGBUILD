
pkgname=glib2
pkgver=2.34.3
pkgrel=3
pkgdesc="Common C routines used by GTK+ and other libs"
url="http://www.gtk.org/"
arch=('x86_64')
license=('LGPL')
depends=('pcre' 'libffi')
makedepends=('pkgconfig' 'python2' 'libxslt' 'docbook-xml')
options=('!libtool' '!docs')
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/2.34/glib-${pkgver}.tar.xz"
        'revert-warn-glib-compile-schemas.patch'
        'gcc48.patch')
md5sums=('a4ca31e258273c3761e3de2edd607661'
         '3265d1afbcf1f5f308e26c3f9f6ca899'
         '90674fbce9f11680be839f6e6332b1f7')

build() {
  cd "${srcdir}/glib-${pkgver}"
  patch -Rp1 -i "$srcdir/revert-warn-glib-compile-schemas.patch"
  patch -p1 -i "${srcdir}/gcc48.patch"
  
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --libdir=/usr/lib \
      --sysconfdir=/etc \
      --with-pcre=system \
      --disable-fam
  make
}

package() {
  cd "${srcdir}/glib-${pkgver}"
  make completiondir=/usr/share/bash-completion/completions DESTDIR="${pkgdir}" install

  for _i in "${pkgdir}/usr/share/bash-completion/completions/"*; do
      chmod -x "${_i}"
  done
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir"/usr/bin/gdbus-codegen
}

