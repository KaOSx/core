
pkgname=glib2
pkgver=2.82.2
_pkgver=2.82
pkgrel=1
pkgdesc="Common C routines used by GTK+ and other libs"
url="https://www.gtk.org/"
arch=('x86_64')
license=('LGPL')
depends=('pcre2' 'libffi')
makedepends=('pkg-config' 'python3-docutils' 'python3-packaging' 'libxslt' 'docbook-xml' 'elfutils'
             'meson' 'ninja' 'gobject-introspection')
options=('!docs' '!libtool' '!emptydirs')
conflicts=('glib2-docs')
provides=('glib2-docs')
source=("https://ftp.gnome.org/pub/GNOME/sources/glib/${_pkgver}/glib-${pkgver}.tar.xz"
        'gio-querymodules.hook'
        'glib-compile-schemes.hook'
        'revert_wakeups.patch')
sha256sums=('ab45f5a323048b1659ee0fbda5cecd94b099ab3e4b9abf26ae06aeb3e781fd63'
            '557c88177f011ced17bdeac1af3f882b2ca33b386a866fdf900b35f927a2bbe8'
            'fe31399eb057d24a37062bcae6f88ca0778a91b85737f8110a03baa8bfc64fec'
            '05635ffbeb5875fc5cb4adb4c8c85450693c36fc15e4c85f2f04fdbaf7e9d1e8')

prepare() {
  cd glib-${pkgver}

  #patch -p1 -i ../revert_wakeups.patch
}
  
build() {
  mkdir -p build
  cd build

  meson setup ../glib-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -D man-pages=enabled \
    -D selinux=disabled \
    -D glib_debug=disabled \
    -D introspection=enabled
  ninja
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  install -dm755 ${pkgdir}/usr/share/libalpm/hooks/
  install -m644 ../glib-compile-schemes.hook ${pkgdir}/usr/share/libalpm/hooks/
  install -m644 ../gio-querymodules.hook ${pkgdir}/usr/share/libalpm/hooks/
}

