
pkgname=e2fsprogs
pkgver=1.46.5
pkgrel=3
pkgdesc="Ext2/3/4 filesystem utilities"
arch=('x86_64')
license=('GPL' 'LGPL' 'MIT')
url="https://e2fsprogs.sourceforge.net"
groups=('base')
depends=('sh' 'util-linux')
makedepends=('bc')
source=("https://www.kernel.org/pub/linux/kernel/people/tytso/e2fsprogs/v${pkgver}/${pkgname}-${pkgver}.tar.xz"
#source=("https://sourceforge.net/projects/e2fsprogs/files/e2fsprogs/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'MIT-LICENSE')
backup=('etc/mke2fs.conf')
sha256sums=('2f16c9176704cf645dc69d5b15ff704ae722d665df38b2ed3cfc249757d8d81e'
            'cc45386c1d71f438ad648fd7971e49e3074ad9dbacf9dd3a5b4cb61fd294ecbb')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
      --with-root-prefix="" \
      --libdir=/usr/lib \
      --enable-elf-shlibs \
      --disable-fsck \
      --disable-uuidd \
      --disable-libuuid \
      --disable-libblkid \
      --enable-blkid-debug \
      --enable-verbose-makecmds \
      --enable-e2initrd-helper

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make -j1 DESTDIR="${pkgdir}" install install-libs

  # remove 64bit ref, new for e2fsprogs 1.43
  # possible breaks calamares installs https://github.com/calamares/calamares/wiki/Deployment-Practices
  sed -i -e 's|huge_file,flex_bg,64bit,dir_nlink|huge_file,flex_bg,dir_nlink|' ${pkgdir}/etc/mke2fs.conf
  
  # addition for zulucrypt
  ln -sf /sbin/mkfs.ext4 ${pkgdir}/usr/bin/mkfs.ext4

  install -Dm644 ${srcdir}/MIT-LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/MIT-LICENSE
}
