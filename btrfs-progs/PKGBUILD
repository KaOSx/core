
pkgname=btrfs-progs
pkgver=6.0.1
pkgrel=1
pkgdesc="Btrfs filesystem utilities."
arch=('x86_64')
url="https://btrfs.wiki.kernel.org/"
license=('GPL2')
depends=('glibc' 'e2fsprogs' 'lzo2' 'python3' 'zstd')
makedepends=('asciidoc' 'git' 'xmlto' 'python3-setuptools')
options=('!staticlibs')
source=("https://www.kernel.org/pub/linux/kernel/people/kdave/btrfs-progs/${pkgname}-v${pkgver}.tar.xz"
        'initcpio-install-btrfs'
        'initcpio-hook-btrfs')
sha256sums=('b5316fff1d811e2ad3a067973109eb912bb0d92805735625fd8b82e700201c48'
            '34e1472c028875bb8cc7d06fa18aad9c1c90e3719de416733e9baac82a12cd94'
            '116a8fac508a3872b77f249e71d9de4f9b61cd20edfde01c99a5deba143769df')

build() {
  cd ${pkgname}-v${pkgver}

  ./configure --prefix=/usr \
    --disable-documentation
  make 
}

package() {
  cd ${pkgname}-v${pkgver}
  make prefix=${pkgdir}/usr udevdir=${pkgdir}/usr/lib/udev install

  mkdir -p ${pkgdir}/sbin
  ln -sf /usr/bin/btrfs ${pkgdir}/sbin/btrfs

  install -Dm644 ${srcdir}/initcpio-install-btrfs ${pkgdir}/usr/lib/initcpio/install/btrfs
  install -Dm644 ${srcdir}/initcpio-hook-btrfs ${pkgdir}/usr/lib/initcpio/hooks/btrfs
}
