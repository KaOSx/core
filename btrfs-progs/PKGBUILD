
pkgname=btrfs-progs
pkgver=6.11
pkgrel=1
pkgdesc="Btrfs filesystem utilities."
arch=('x86_64')
url="https://btrfs.readthedocs.io/en/latest/"
license=('GPL2')
depends=('glibc' 'e2fsprogs' 'lzo2' 'python3' 'zstd')
makedepends=('asciidoc' 'git' 'xmlto' 'python3-setuptools')
options=('!staticlibs')
source=("https://www.kernel.org/pub/linux/kernel/people/kdave/btrfs-progs/${pkgname}-v${pkgver}.tar.xz"
        'initcpio-install-btrfs'
        'initcpio-hook-btrfs')
sha256sums=('ff9ae91521303a90d87e1c4be230f0121f39c44ddbe52c2aeae263c6fecfa099'
            '34e1472c028875bb8cc7d06fa18aad9c1c90e3719de416733e9baac82a12cd94'
            '116a8fac508a3872b77f249e71d9de4f9b61cd20edfde01c99a5deba143769df')

build() {
  cd ${pkgname}-v${pkgver}
  #cp config.h.in include/

  ./configure --prefix=/usr \
    --disable-documentation
  make 
}

package() {
  cd ${pkgname}-v${pkgver}
  make prefix=${pkgdir}/usr udevdir=${pkgdir}/usr/lib/udev install

  mkdir -p ${pkgdir}/sbin
  ln -sf /usr/bin/btrfs ${pkgdir}/sbin/btrfs

  install -Dm644 ${srcdir}/initcpio-install-btrfs ${pkgdir}/usr/lib/initcpio/install/btrfs
  install -Dm644 ${srcdir}/initcpio-hook-btrfs ${pkgdir}/usr/lib/initcpio/hooks/btrfs
}
