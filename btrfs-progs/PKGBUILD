
pkgname=btrfs-progs
pkgver=3.18.1
pkgrel=1
pkgdesc="Btrfs filesystem utilities."
arch=('x86_64')
url="http://btrfs.wiki.kernel.org/"
license=('GPL2')
depends=('glibc' 'e2fsprogs' 'lzo2' 'asciidoc')
makedepends=('git' 'xmlto')
options=('!staticlibs')
source=("https://www.kernel.org/pub/linux/kernel/people/kdave/btrfs-progs/${pkgname}-v${pkgver}.tar.xz"
	'initcpio-install-btrfs'
	'initcpio-hook-btrfs')
md5sums=('dc7afb7e2dc5002344b99d5e7b14510c'
         '5642e1524eefddf9195693cd114ecc65'
         '85ff8daca50a5ba3dff3c3e641d38b1e')

build() {
   cd $srcdir/${pkgname}-v${pkgver}
   
   make CFLAGS="$CFLAGS" 
   make CFLAGS="$CFLAGS" btrfs-select-super
}

package() {
   cd $srcdir/${pkgname}-v${pkgver}
   make prefix=$pkgdir/usr install
   install -Dm755 btrfs-select-super $pkgdir/usr/bin

   mkdir -p ${pkgdir}/sbin
   ln -sf /usr/bin/btrfs ${pkgdir}/sbin/btrfs

   install -Dm644 "$srcdir/initcpio-install-btrfs" "$pkgdir/usr/lib/initcpio/install/btrfs"
   install -Dm644 "$srcdir/initcpio-hook-btrfs" "$pkgdir/usr/lib/initcpio/hooks/btrfs"
}
