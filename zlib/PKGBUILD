
pkgname=zlib
pkgver=1.3
pkgrel=1
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=('x86_64')
license=('custom')
url="http://www.zlib.net/"
depends=('glibc')
#makedepends=('clang')
source=("http://zlib.net/zlib-${pkgver}.tar.xz"
        "https://github.com/madler/zlib/pull/229/commits/e490ddad3091574a0c2e3b5a66a8fee9a7ab212f.diff")
sha256sums=('8a9ba2898e1d0d774eca6ba5b4627a11e5588ba85c8851336eb38de4683050a7'
			'8353c6e0242d63e24e12be4c88a4a8adce590f05b6e04488f44c5ee123281170')

build() {

    #export CC=/usr/bin/clang
    #export CXX=/usr/bin/clang++

	cd zlib-${pkgver}
	# presence of crypt.h causes sigil build fail
	# https://bugzilla.redhat.com/show_bug.cgi?id=1424609
	patch -p1 -i ${srcdir}/e490ddad3091574a0c2e3b5a66a8fee9a7ab212f.diff

	./configure --prefix=/usr
	make

	grep -A 24 '^  Copyright' zlib.h > LICENSE

	cd contrib/minizip
	cp Makefile Makefile.orig

	autoreconf --install
	./configure --prefix=/usr
	make
}

check() {
	cd zlib-${pkgver}

	make test
}

package() {
	cd zlib-${pkgver}

	make install DESTDIR=${pkgdir}
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/zlib/LICENSE

	cd contrib/minizip
	make install DESTDIR=${pkgdir}
}
