
pkgname=wget
pkgver=1.15
pkgrel=1
pkgdesc="A network utility to retrieve files from the Web"
arch=('x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
groups=('base')
depends=('glibc' 'openssl' 'libidn' 'util-linux' 'pcre')
optdepends=('ca-certificates: HTTPS downloads')
backup=('etc/wgetrc')
install=wget.install
source=("ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('7a279d5ac5594919124d5526e7143e28')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cat >> doc/sample.wgetrc <<EOF

# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF

  ./configure --prefix=/usr --sysconfdir=/etc -with-ssl=openssl --enable-nls
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
}
