
pkgname=perl-xml-parser
_pkgname=XML-Parser
pkgver=2.44.01
_pkgver=2.44_01
pkgrel=2
pkgdesc="Expat-based XML parser module for perl"
arch=('x86_64')
license=('GPL' 'PerlArtistic')
url="https://metacpan.org/release/XML-Parser"
depends=('perl' 'expat')
replaces=('perlxml')
provides=("perlxml=${pkgver}")
conflicts=('perlxml')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/T/TO/TODDR/${_pkgname}-${_pkgver}.tar.gz")
md5sums=('223f524d4ab3896e5149ec656f8561cd')

build() {
  cd ${_pkgname}-${_pkgver}

  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_pkgname}-${_pkgver}
  
  make test
}

package() {
  cd ${_pkgname}-${_pkgver}
  
  make install DESTDIR=${pkgdir}
}
