
pkgname=pcre2
pkgver=10.39
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions, next generation."
arch=('x86_64')
url="https://www.pcre.org/"
license=('BSD')
depends=('gcc-libs' 'readline' 'zlib' 'bzip2' 'libedit')
options=('!libtool')
#source=("https://ftp.pcre.org/pub/pcre/${pkgname}-${pkgver}.tar.bz2")
source=("https://github.com/PhilipHazel/pcre2/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('4a765b1419c2e7a01263c3260abca87a')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
        --enable-unicode \
        --enable-jit \
        --enable-pcre2-16 \
        --enable-pcre2-32 \
        --enable-pcre2grep-libz \
        --enable-pcre2grep-libbz2 \
        --enable-pcre2test-libreadline
  make
}

check() {
  cd ${pkgname}-${pkgver} 

  make -j1 check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 LICENCE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
