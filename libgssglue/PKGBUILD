
pkgname=libgssglue
pkgver=0.5
pkgrel=1
pkgdesc="Exports a gssapi interface which calls other random gssapi libraries"
arch=('x86_64')
url="http://www.citi.umich.edu/projects/nfsv4/linux/"
license=('BSD')
depends=('glibc')
makedepends=('pkgconfig' 'autoconf')
backup=(etc/gssapi_mech.conf)
options=('!libtool')
source=("https://gitlab.com/gsasl/libgssglue/-/archive/${pkgname}-${pkgver}/${pkgname}-${pkgname}-${pkgver}.tar.bz2"
        'gssapi_mech.conf')
md5sums=('8fb01539fb7b251a73e5dd7f51be2f13'
         '080be866717e4e06fa6f7d6f43cb395a')

build() {
  cd ${pkgname}-${pkgname}-${pkgver}

  ./bootstrap
  ./configure --prefix=/usr 
  make
}

package() {
  cd ${pkgname}-${pkgname}-${pkgver}
  make DESTDIR=${pkgdir}/ install

  install -Dm644 ../gssapi_mech.conf ${pkgdir}/etc/gssapi_mech.conf
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/libgssglue/COPYING
}
