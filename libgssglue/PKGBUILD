
pkgname=libgssglue
pkgver=0.4
pkgrel=7
pkgdesc="Exports a gssapi interface which calls other random gssapi libraries"
arch=('x86_64')
url="http://www.citi.umich.edu/projects/nfsv4/linux/"
license=('BSD')
depends=('glibc')
makedepends=('pkgconfig' 'autoconf')
backup=(etc/gssapi_mech.conf)
options=('!libtool')
source=("http://www.citi.umich.edu/projects/nfsv4/linux/libgssglue/${pkgname}-${pkgver}.tar.gz"
        'gssapi_mech.conf')
md5sums=('088797f3180702fa54e786496b32e750'
         '080be866717e4e06fa6f7d6f43cb395a')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr 
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir}/ install

  install -Dm644 ../gssapi_mech.conf ${pkgdir}/etc/gssapi_mech.conf
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/libgssglue/COPYING
}
