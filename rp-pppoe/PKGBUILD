
pkgname=rp-pppoe
pkgver=4.0
pkgrel=1
pkgdesc="Roaring Penguin's Point-to-Point Protocol over Ethernet client"
arch=('x86_64')
url="https://dianne.skoll.ca/projects/rp-pppoe/"
license=('GPL')
groups=('base')
depends=('ppp' 'sh')
options=('!makeflags')
backup=(etc/ppp/pppoe-server-options)
source=("https://dianne.skoll.ca/projects/rp-pppoe/download/rp-pppoe-${pkgver}.tar.gz"
        'adsl.service')
md5sums=('063d21dd94d9c80503470c830a97d12a'
         'ae14bb45cdd6aad31395a301f96ddd82')

build() {
  cd ${pkgname}-${pkgver}/src
  #patch -p2 -i ${srcdir}/kmode.patch

  ./configure --prefix=/usr \
    --enable-plugin
  make
}

package() {
  cd ${pkgname}-${pkgver}/src
  make PLUGIN_DIR="/usr/lib/rp-pppoe" DESTDIR=${pkgdir} install

  install -Dm644 ../../adsl.service ${pkgdir}/usr/lib/systemd/system/adsl.service
}
