
_extramodules=5.18.6-1-next

pkgname=zfs-kmod-next
pkgver=2.1.4
_commit=05147319b0821f61fcff743e20605e191d523906
pkgrel=17
pkgdesc="Kernel module for the Zettabyte File System."
arch=('x86_64')
url="https://zfsonlinux.org/"
license=('CDDL')
depends=('kmod' 'linux-next' "zfs=${pkgver}")
makedepends=('linux-next-headers' 'elfutils')
#source=("https://github.com/zfsonlinux/zfs/releases/download/zfs-${pkgver}/zfs-${pkgver}.tar.gz")
source=("https://github.com/openzfs/zfs/archive/${_commit}.zip")
sha256sums=("ee291663ae19098d4d67f87c533bfe4d98b8a80505281438de8cc2301a1726a6")

build() {
    cd zfs-${_commit}

    ./autogen.sh
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --datadir=/usr/share \
        --libdir=/lib \
        --datadir=/usr/share \
        --includedir=/usr/include \
        --with-udevdir=/usr/lib/udev \
        --libexecdir=/usr/lib \
        --with-config=kernel \
        --with-linux=/lib/modules/${_extramodules}/build \
        --with-linux-obj=/lib/modules/${_extramodules}/build
    make
}

package() {
    cd zfs-${_commit}

    make DESTDIR=${pkgdir} install
    #remove references to the buildsystem
    sed -i "s|${srcdir}||" ${pkgdir}/usr/src/zfs-*/${_extramodules}/Module.symvers

    install -D -m0644 LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}
}
