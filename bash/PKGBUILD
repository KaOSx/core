
pkgname=bash
_basever=5.2
_patchlevel=037
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc="The GNU Bourne Again shell"
arch=('x86_64')
license=('GPL')
url="https://www.gnu.org/software/bash/bash.html"
groups=('base')
backup=(etc/bash.bash{rc,_logout} etc/skel/.bash{rc,_profile,_logout})
depends=('readline' 'glibc')
provides=('sh')
source=("https://ftp.gnu.org/gnu/bash/bash-${_basever}.tar.gz"
        'dot.bashrc'
        'dot.bash_profile'
        'dot.bash_logout'
        'system.bashrc'
        'system.bash_logout')
if [ ${_patchlevel} -gt 000 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} https://ftp.gnu.org/gnu/bash/bash-${_basever}-patches/bash${_basever//./}-$(printf "%03d" $p))
    done
fi
md5sums=('cfb4cf795fc239667f187b3d6b3d396f'
         '027d6bd8f5f6a06b75bb7698cb478089'
         '2902e0fee7a9168f3a4fd2ccd60ff047'
         '42f4400ed2314bd7519c020d0187edc5'
         '3546099a1b2f667adc9794f52e78e35b'
         '472f536d7c9e8250dc4568ec4cfaf294'
         '8b03b019783c7bbed9ace21f70155716'
         'd8f7085bfd9f2ebf574858b2e1110c3b'
         '4c27b778445b21e28cf4f6aba76b5171'
         '40b2db2180d772c9ba72f773e963d211'
         '05dde44788d0d181206f0a15afd38501'
         'af9b98aef8fc987cd398ae9be01a6cdf'
         '9919243f7fcc1812eb88efe028da7ce0'
         '99bc0f731b3ddb1368e676327246cb21'
         '57f589db109d2c72c7cefff1df0819e1'
         'c1d97dc661639ab0e4648fa50164881e'
         'e9bc4d7ab21a195f9c6cc78547148112'
         '0e836c596d19fce655acc9862522423d'
         '62a7f7bf4ad91a403cb3873f7e1d288b'
         '7022d611b94ae8fda6a076c0f33ce00b'
         'a5f9afd119d374ff8f9e1a37ebf11b15'
         '3fbb82e5500d4d8fc93268a98ea2e474'
         '4bfdc876c82eda73f563b7aef431ef2f'
         'c677f65b83d782b6478d5ad75acff03a'
         'b5feb40e8986a435a9be8ec7b08b0505'
         'fa530069a8d1075cf0995d95a59732cb'
         '4773cce94d61b3b7f8482779a4e3c48a'
         'a547f36f2c50f3cb4f8a6ef717f2dfbf'
         'e33fc4c60dbdffd46311d963d1fd4d52'
         '9cf13adeb43920df0ce4ff621371988c'
         '9de0025a5067cfedddffade2bf059ba7'
         '3e1bede019794844bd51312bc8675438'
         'ac8030fc00b0911037593436eac6072e'
         '0a9c1e9704f4351b8dfd1cc310070e5f'
         '905c17b49c4804e7828009ca2ff09a39'
         'a63c83679ebf5f720ecd98d41913ce50'
         'eab1eb23fe1a5f352d4310a2dd70aacb'
         '3ef1743a21ac41e54a2af77ab557298f'
         '2ce1f0d04f127289f47cae3b2e1842db'
         'b56d4acb61f716ef940d6c3c2f62bfa6'
         '517ee10a98b1d97ddd7f560f6c004601'
         '5d80694f5f8446cc9f4f84ed773f0ecb'
         '5b8e87f69bd02792041e2c472f4d8f43')

build() {
  cd ${pkgname}-${_basever}
  for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
    msg "applying patch bash${_basever//./}-$(printf "%03d" $p)"
    patch -p0 -i $srcdir/bash${_basever//./}-$(printf "%03d" $p)
  done

  _bashconfig=(-DDEFAULT_PATH_VALUE=\'\"/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/usr/sbin:/sbin\"\'
               -DSTANDARD_UTILS_PATH=\'\"/usr/bin:/bin:/usr/sbin:/sbin\"\'
               -DSYS_BASHRC=\'\"/etc/bash.bashrc\"\'
               -DSYS_BASH_LOGOUT=\'\"/etc/bash.bash_logout\"\')
  export CFLAGS="${CFLAGS} ${_bashconfig[@]}"

  ./configure --prefix=/usr \
    --with-curses \
    --enable-readline \
    --without-bash-malloc \
    --with-installed-readline \
    --bindir=/bin \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info
  make
}

check() {
  cd ${pkgname}-${_basever}

  make check
}

package() {
  cd ${pkgname}-${_basever}
  make DESTDIR=${pkgdir} install

  # bash is default /bin/sh
  cd ${pkgdir}/bin
  ln -s bash sh

  install -dm755 ${pkgdir}/etc/skel/

  # system configuration files
  install -m644 ${srcdir}/system.bashrc ${pkgdir}/etc/bash.bashrc
  install -m644 ${srcdir}/system.bash_logout ${pkgdir}/etc/bash.bash_logout

  # user configuration files
  install -m644 ${srcdir}/dot.bashrc ${pkgdir}/etc/skel/.bashrc
  install -m644 ${srcdir}/dot.bash_profile ${pkgdir}/etc/skel/.bash_profile
  install -m644 ${srcdir}/dot.bash_logout ${pkgdir}/etc/skel/.bash_logout
}
