
pkgname=coreutils
pkgver=9.5
pkgrel=1
pkgdesc="The basic file, shell and text manipulation utilities of the GNU operating system"
arch=('x86_64')
license=('GPL3')
url="https://www.gnu.org/software/coreutils"
groups=('base')
depends=('glibc' 'pam' 'acl' 'gmp' 'libcap')
source=("https://ftp.gnu.org/gnu/coreutils/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/coreutils/coreutils/commit/76f2fb627118a26c25003dbd98c22c153b7ee1d2.diff")
sha1sums=('867fed7ce2ee15c5150a355a5f3a3b50578cf78d'
          'f2b8b02322efa0a9d026b83a5cff20ac222f7316')

build() {
  cd ${pkgname}-${pkgver}
  # https://debbugs.gnu.org/cgi/bugreport.cgi?bug=62403
  #patch -p1 -i ${srcdir}/76f2fb627118a26c25003dbd98c22c153b7ee1d2.diff

  ./configure --prefix=/usr --libexecdir=/usr/lib \
              --enable-no-install-program=groups,hostname,kill,uptime
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # no fail
  make RUN_EXPENSIVE_TESTS=no check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  cd ${pkgdir}/usr/bin
  install -dm755 ${pkgdir}/bin
  # makepkg requirement
  ln -s /usr/bin/du ${pkgdir}/bin/du
  # binaries required by FHS
  _fhs=('cat' 'chgrp' 'chmod' 'chown' 'cp' 'date' 'dd' 'df' 'echo' 'false'
        'ln' 'ls' 'mkdir' 'mknod' 'mv' 'pwd' 'rm' 'rmdir' 'stty' 'sync'
        'true' 'uname')
  for i in ${_fhs[@]}; do
    ln -s ../usr/bin/$i ${pkgdir}/bin/$i
  done
}
