
pkgname=icu
pkgver=57.1
_pkgver=57_1
pkgrel=1
pkgdesc="International Components for Unicode library"
arch=('x86_64')
url="http://www.icu-project.org/"
license=('custom:"icu"')
depends=('gcc-libs' 'sh')
source=("http://download.icu-project.org/files/icu4c/${pkgver}/${pkgname}4c-${_pkgver}-src.tgz"
        'icu.8198.revert.icu5431.patch')
md5sums=('976734806026a4ef8bdd17937c8898b9'
         'ebd5470fc969c75e52baf4af94a9ee82')

build() {
  cd ${srcdir}/icu/source
  # fix Malayalam encoding https://bugzilla.redhat.com/show_bug.cgi?id=654200
  patch -Rp3 -i ${srcdir}/icu.8198.revert.icu5431.patch
  
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man
  make 
}

check() {
  cd "$srcdir/icu/source"
  
  make -k check 
}

package() {
  cd ${srcdir}/icu/source
  make -j1 DESTDIR=${pkgdir} install 

  install -Dm644 ${srcdir}/icu/license.html ${pkgdir}/usr/share/licenses/icu/license.html
}
