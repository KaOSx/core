
pkgname=iproute2
pkgver=6.3.0
pkgrel=1
pkgdesc="IP Routing Utilities"
arch=('x86_64')
license=('GPL2')
url="https://wiki.linuxfoundation.org/networking/start"
depends=('linux-atm' 'perl')
makedepends=('iptables')
options=('!makeflags')
backup=('etc/iproute2/ematch_map' 'etc/iproute2/rt_dsfield' 'etc/iproute2/rt_protos' \
        'etc/iproute2/rt_realms' 'etc/iproute2/rt_scopes' 'etc/iproute2/rt_tables')
source=("https://www.kernel.org/pub/linux/utils/net/iproute2/${pkgname}-${pkgver}.tar.xz"
        'iproute2-fhs.patch')
sha256sums=('dfb2a98db96e7a653cffc6693335a1a466e29a34b6ac528be48f35e1d2766732'
            '532eb9e8b6cb0dc043934e7ce1447c30e16acbe07bab6348f9a338287db9d364')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/iproute2-fhs.patch

  sed -i 's|-Werror||' Makefile
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure
   make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # allow loopback to be started before /usr is mounted
  mkdir -p ${pkgdir}/sbin
  mv ${pkgdir}/usr/sbin/ip ${pkgdir}/sbin/ip
  ln -s /sbin/ip ${pkgdir}/usr/sbin/ip

  # add libnetlink
  install -Dm644 include/libnetlink.h ${pkgdir}/usr/include/libnetlink.h
  install -Dm644 lib/libnetlink.a ${pkgdir}/usr/lib/libnetlink.a
}
