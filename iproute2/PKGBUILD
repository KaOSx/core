
pkgname=iproute2
pkgver=6.6.0
pkgrel=1
pkgdesc="IP Routing Utilities"
arch=('x86_64')
license=('GPL2')
url="https://wiki.linuxfoundation.org/networking/start"
depends=('linux-atm' 'perl')
makedepends=('iptables')
options=('!makeflags')
source=("https://www.kernel.org/pub/linux/utils/net/iproute2/${pkgname}-v${pkgver}.tar.xz"
        'iproute2-fhs.patch')
sha256sums=('77e49171a58ae1a507d890f76336a0b4704bb5057be5bb9b4d721c4b1d199d56'
            '84234a9a89e3af00cb8f2a3f175497f5875fcb99fcf1e4612230dfd18d7d079a')

prepare() {
  cd ${pkgname}-v${pkgver}
  patch -p1 -i ${srcdir}/iproute2-fhs.patch

  sed -i 's|-Werror||' Makefile
}

build() {
  cd ${pkgname}-v${pkgver}

  ./configure
   make
}

package() {
  cd ${pkgname}-v${pkgver}
  make DESTDIR=${pkgdir} install

  # allow loopback to be started before /usr is mounted
  mkdir -p ${pkgdir}/sbin
  mv ${pkgdir}/usr/sbin/ip ${pkgdir}/sbin/ip
  ln -s /sbin/ip ${pkgdir}/usr/sbin/ip

  # add libnetlink
  install -Dm644 include/libnetlink.h ${pkgdir}/usr/include/libnetlink.h
  install -Dm644 lib/libnetlink.a ${pkgdir}/usr/lib/libnetlink.a
}
