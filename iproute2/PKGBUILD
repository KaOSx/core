
pkgname=iproute2
pkgver=4.10.0
pkgrel=1
pkgdesc="IP Routing Utilities"
arch=('x86_64')
license=('GPL2')
url="http://www.linux-foundation.org/en/Net:Iproute2"
depends=('linux-atm' 'perl')
makedepends=('iptables')
options=('!makeflags')
backup=('etc/iproute2/ematch_map' 'etc/iproute2/rt_dsfield' 'etc/iproute2/rt_protos' \
        'etc/iproute2/rt_realms' 'etc/iproute2/rt_scopes' 'etc/iproute2/rt_tables')
source=("https://www.kernel.org/pub/linux/utils/net/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        'iproute2-fhs.patch')
md5sums=('b94a2b0edefaeac124dc8f5d006931b9'
         '80f2f7a2c7dd2a01d6c8234145d94561')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/iproute2-fhs.patch

  sed -i 's|-Werror||' Makefile
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure
   make
}
   
package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=$pkgdir install
  
  # allow loopback to be started before /usr is mounted
  mkdir -p ${pkgdir}/sbin
  mv ${pkgdir}/usr/sbin/ip ${pkgdir}/sbin/ip
  ln -s /sbin/ip ${pkgdir}/usr/sbin/ip

  # add libnetlink
  install -Dm644 include/libnetlink.h ${pkgdir}/usr/include/libnetlink.h
  install -Dm644 lib/libnetlink.a ${pkgdir}/usr/lib/libnetlink.a
}
