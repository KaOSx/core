
pkgname=iproute2
pkgver=6.7.0
pkgrel=1
pkgdesc="IP Routing Utilities"
arch=('x86_64')
license=('GPL2')
url="https://wiki.linuxfoundation.org/networking/start"
depends=('linux-atm' 'perl')
makedepends=('iptables')
options=('!makeflags')
source=("https://www.kernel.org/pub/linux/utils/net/iproute2/${pkgname}-${pkgver}.tar.xz"
        'iproute2-fhs.patch')
sha256sums=('ff942dd9828d7d1f867f61fe72ce433078c31e5d8e4a78e20f02cb5892e8841d'
            'e96c7bd15275faedc0eefcedaa7c56a73c581d3db378522b9b53feb11b34477c')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/iproute2-fhs.patch

  sed -i 's|-Werror||' Makefile
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure
   make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # allow loopback to be started before /usr is mounted
  mkdir -p ${pkgdir}/sbin
  mv ${pkgdir}/usr/sbin/ip ${pkgdir}/sbin/ip
  ln -s /sbin/ip ${pkgdir}/usr/sbin/ip

  # add libnetlink
  install -Dm644 include/libnetlink.h ${pkgdir}/usr/include/libnetlink.h
  install -Dm644 lib/libnetlink.a ${pkgdir}/usr/lib/libnetlink.a
}
