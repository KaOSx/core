
pkgname=iproute2
pkgver=4.1.0
pkgrel=1
pkgdesc="IP Routing Utilities"
arch=('x86_64')
license=('GPL2')
url="http://www.linux-foundation.org/en/Net:Iproute2"
depends=('linux-atm' 'perl')
makedepends=('iptables')
options=('!makeflags')
backup=('etc/iproute2/ematch_map' 'etc/iproute2/rt_dsfield' 'etc/iproute2/rt_protos' \
	'etc/iproute2/rt_realms' 'etc/iproute2/rt_scopes' 'etc/iproute2/rt_tables')
source=("http://www.kernel.org/pub/linux/utils/net/$pkgname/$pkgname-$pkgver.tar.xz"
	'iproute2-fhs.patch')
md5sums=('37efb479b3943b60c449ea02362b5ffc'
         '1b15a4b4eccbc5d573b7a542de73ed6b')

prepare() {
  cd $srcdir/iproute2-$pkgver
  patch -p1 -i ${srcdir}/iproute2-fhs.patch

  # do not treat warnings as errors
  sed -i 's/-Werror//' Makefile
}

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure
   make
}
   
package() {
  cd $srcdir/iproute2-$pkgver
  make DESTDIR=$pkgdir install
  
  # allow loopback to be started before /usr is mounted
  mkdir -p "$pkgdir/sbin"
  mv "$pkgdir/usr/sbin/ip" "$pkgdir/sbin/ip"
  ln -s /sbin/ip "$pkgdir/usr/sbin/ip"

  # libnetlink isn't installed
  install -Dm644 include/libnetlink.h ${pkgdir}/usr/include/libnetlink.h
  install -Dm644 lib/libnetlink.a ${pkgdir}/usr/lib/libnetlink.a
}
