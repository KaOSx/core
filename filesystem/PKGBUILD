#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dog]org>
# maintainer Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=filesystem
pkgver=2013.03
pkgrel=1
_codename=Benz
pkgdesc='Base filesystem'
arch=('x86_64')
license=('GPL')
url='http://www.chakra-project.org'
groups=('base')
install='filesystem.install'
depends=('iana-etc' 'bash' 'coreutils')
makedepends=('asciidoc')
backup=('etc/fstab' 'etc/crypttab' 'etc/group' 'etc/hosts' 'etc/ld.so.conf' 'etc/passwd'
        'etc/shadow' 'etc/gshadow' 'etc/resolv.conf' 'etc/motd' 'etc/nsswitch.conf'
        'etc/shells' 'etc/host.conf' 'etc/securetty' 'etc/profile' 'etc/issue' 'etc/hostname'
        'etc/machine-info' 'etc/vconsole.conf')
source=('group' 'issue' 'nsswitch.conf' 'securetty' 'host.conf' 'ld.so.conf'
        'passwd' 'shadow' 'fstab' 'crypttab' 'hosts' 'hostname' 'motd' 'resolv.conf' 'shells'
        'gshadow' 'profile' 'modprobe.d.usb-load-ehci-first' 'machine-info' 'os-release'
        'vconsole.conf' 'locale.sh')
md5sums=('3b1d2710d902b222bb345433f35b766a'
         '8266bc7566e99f2b5f5e0b5b1ac17241'
         '13753e4e0964f3652b0cc60a28528bdf'
         '4c4540eeb748bf1f71d631b8c1dcf0b3'
         'f28150d4c0b22a017be51b9f7f9977ed'
         '6e488ffecc8ba142c0cf7e2d7aeb832e'
         '455b78cada80f40b6f6968f5cbd97a2e'
         '01249bb0ba4468f95e2cc3a627f5e6e3'
         'ca716f853860199c1286e7939b2f2666'
         'a367150bd0fe6b02114365121de64a53'
         '7bc65f234dfb6abf24e7c3b03e86f4ff'
         'b7244a8455581eeb441edeec03495054'
         'd41d8cd98f00b204e9800998ecf8427e'
         '6f48288b6fcaf0065fcb7b0e525413e0'
         '22518e922891f9359f971f4f5b4e793c'
         '677523dbe94b79299aa91b35ed8203b6'
         'f3b6ae7db8adffaaa4bffc6099dcbd50'
         'a8a962370cd0128465d514e6a1f74130'
         '8aba0b33527e812211b9e5f9b87887ae'
         '290d1cf12295c9c011cf2a112fdd5cf7'
         '201bbb8b4734d2f46afe1e24d7cc6173'
         '45100e8a4a6d8d4b3a7dcd34b0fa4281')

package() {
	cd ${pkgdir}

	#
	# setup root filesystem
	#
	for d in boot dev etc home media mnt usr var opt srv/http run; do
		install -d -m755 ${d}
	done
	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp
	# vsftpd won't run with write perms on /srv/ftp
	install -d -m555 -g ftp srv/ftp

	# setup /etc
	install -d etc/{ld.so.conf.d,skel,profile.d}
	for f in fstab group host.conf hostname hosts issue ld.so.conf motd nsswitch.conf passwd resolv.conf securetty shells profile machine-info os-release vconsole.conf; do
		install -m644 ${srcdir}/${f} etc/
	done
	ln -s /proc/self/mounts etc/mtab
	for f in gshadow shadow crypttab; do
		install -m600 ${srcdir}/${f} etc/
	done
	touch etc/chakra-release
	install -D -m644 ${srcdir}/modprobe.d.usb-load-ehci-first usr/lib/modprobe.d/usb-load-ehci-first.conf
	install -m755 ${srcdir}/locale.sh etc/profile.d/locale.sh

	# setup /var
	for d in cache/man local opt log/old lib/misc empty; do
		install -d -m755 var/${d}
	done
	install -d -m1777 var/{tmp,spool/mail}
	# allow setgid games to write scores
	install -d -m775 -g games var/games
	ln -s spool/mail var/mail
	ln -s ../run var/run
	ln -s ../run/lock var/lock

	#
	# setup /usr hierarchy
	#
	for d in bin include lib sbin share/misc src; do
		install -d -m755 usr/${d}
	done
	for d in $(seq 8); do
		install -d -m755 usr/share/man/man${d}
	done


	#
	# setup /usr/local hierarchy
	#
	for d in bin etc games include lib man sbin share src; do
		install -d -m755 usr/local/${d}
	done
	ln -s ../man usr/local/share/man
}

