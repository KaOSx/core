
pkgname=isl
pkgver=0.12.2
pkgrel=2
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('x86_64')
url="https://www.ohloh.net/p/isl"
depends=('gmp')
license=('MIT')
options=('!libtool')
source=("http://isl.gforge.inria.fr/$pkgname-$pkgver.tar.bz2")
md5sums=('e039bfcfb6c2ab039b8ee69bf883e824')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  
  install -dm755 $pkgdir/usr/share/gdb/auto-load/usr/lib/
  mv $pkgdir/{,/usr/share/gdb/auto-load/}usr/lib/libisl.so.10.2.2-gdb.py
  
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/isl/LICENSE
}

