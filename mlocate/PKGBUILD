
pkgname=mlocate
pkgver=0.26
pkgrel=2
pkgdesc="Faster merging drop-in for slocate"
arch=('x86_64')
url="http://carolina.mff.cuni.cz/~trmac/blog/mlocate"
license=('GPL')
depends=('glibc' 'coreutils' 'sh')
backup=('etc/updatedb.conf'
        'etc/cron.daily/updatedb')
source=("https://fedorahosted.org/releases/m/l/mlocate/mlocate-$pkgver.tar.xz"
	'updatedb.conf'
	'updatedb.cron')
md5sums=('539e6f86bf387358aa2b14d5f880e49a'
         '66898efb290eff4efd44a0b94fe3927d'
         '4298849f2cdfc43f5403fda15581e265')

build() {
  cd $srcdir/$pkgname-$pkgver

  sed -i '/^groupname /s/mlocate/locate/' Makefile.in
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var/lib
  make

}

check() {
  cd $srcdir/$pkgname-$pkgver
  make check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  
  # Set up a default updatedb.conf and a daily cronjob
  install -Dm644 ${srcdir}/updatedb.conf $pkgdir/etc/updatedb.conf
  install -Dm744 ${srcdir}/updatedb.cron $pkgdir/etc/cron.daily/updatedb

  # Install Mlocate
  make DESTDIR=$pkgdir install

  ln -sv locate $pkgdir/usr/bin/slocate
  chgrp -v 21 $pkgdir/usr/bin/locate
  chmod -v 2755 $pkgdir/usr/bin/locate

  install -dm755 $pkgdir/var/lib
  install -d -m750 -g21 $pkgdir/var/lib/locate
}
