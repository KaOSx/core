
pkgname=usbutils
pkgver=017
pkgrel=1
pkgdesc="USB Device Utilities"
arch=('x86_64')
url="https://linux-usb.sourceforge.net/"
license=('GPL')
groups=('base')
depends=('libusb' 'hwids')
source=("https://www.kernel.org/pub/linux/utils/usb/usbutils/${pkgname}-${pkgver}.tar.xz")
#source=("https://github.com/gregkh/usbutils/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('a6a25ffdcf9103e38d7a44732aca17073f4e602b92e4ae55625231a82702e05b')

build() {
  cd ${pkgname}-${pkgver}

  #./autogen.sh
  ./configure --prefix=/usr \
    --datadir=/usr/share/hwdata \
    --disable-zlib
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # part of hwids 
  rm -rf ${pkgdir}/usr/{share/hwdata,sbin}
}

