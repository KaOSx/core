
pkgname=usbutils
pkgver=007
pkgrel=1
pkgdesc="USB Device Utilities"
arch=('x86_64')
license=('GPL')
groups=('base')
depends=('libusbx' 'hwids')
optdepends=('python2: for lsusb.py usage'
            'coreutils: for lsusb.py usage'
            'wget: for update-usbids.sh usage')
url="http://linux-usb.sourceforge.net/"
source=("https://www.kernel.org/pub/linux/utils/usb/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        'fix-python2.patch')
md5sums=('c9df5107ae9d26b10a1736a261250139'
         '45766196895b4cc50b53cd56e1bbf3d1')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 -i $srcdir/fix-python2.patch
  
  ./configure --prefix=/usr --datadir=/usr/share/hwdata --disable-zlib
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  
  # this is now in the hwids package
  rm -rf $pkgdir/usr/{share/hwdata,sbin}
}

