
pkgname=usbutils
pkgver=015
pkgrel=1
pkgdesc="USB Device Utilities"
arch=('x86_64')
url="https://linux-usb.sourceforge.net/"
license=('GPL')
groups=('base')
depends=('libusb' 'hwids')
#source=("https://www.kernel.org/pub/linux/utils/usb/usbutils/${pkgname}-${pkgver}.tar.xz")
source=("https://github.com/gregkh/usbutils/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('659669e08cbc3961bc085c33f328f950a9570400519c3213b6908f3cc5f44227')

build() {
  cd ${pkgname}-${pkgver}

  ./autogen.sh
  ./configure --prefix=/usr \
    --datadir=/usr/share/hwdata \
    --disable-zlib
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # part of hwids 
  rm -rf ${pkgdir}/usr/{share/hwdata,sbin}
}

