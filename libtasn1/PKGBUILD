
pkgname=libtasn1
pkgver=4.11
pkgrel=1
pkgdesc="The ASN.1 library used in GNUTLS"
arch=('x86_64')
license=('GPL3' 'LGPL')
url="https://www.gnu.org/software/libtasn1/"
depends=('glibc' 'texinfo')
checkdepends=('valgrind')
options=('!libtool')
source=("https://ftp.gnu.org/gnu/libtasn1/${pkgname}-${pkgver}.tar.gz")
md5sums=('1603a941df4902dd5d33ba8c471bb815')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
      --with-packager=KaOS \
      --with-packager-bug-reports="https://github.com/KaOSx/core/issues" \
      --with-packager-version=${pkgver}-${pkgrel}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
