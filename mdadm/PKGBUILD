
pkgname=mdadm
pkgver=3.4
pkgrel=1
pkgdesc="A tool for managing/monitoring Linux md device arrays, also known as Software RAID"
arch=('x86_64')
license=('GPL')
url="http://www.cse.unsw.edu.au/~neilb/source/mdadm/"
groups=('base')
conflicts=('mkinitcpio<0.7')
depends=('glibc')
backup=('etc/mdadm.conf')
source=("ftp://ftp.kernel.org/pub/linux/utils/raid/mdadm/mdadm-$pkgver.tar.xz"
        'mdadm.conf' 
        'mdadm_install'
        'mdadm_hook'
        'mdadm_udev_install'
        'disable-werror.patch')
md5sums=('04b8b21f637540350f8517c7e68d3c63'
         '5a37c112aa07dccdde62f9fa5b888607'
         '6e1a3377e2ac33e9fb222ab38050c654'
         '8e02ee595dfa4e5178ed8886e822cf3a'
         '7b052d051565d8128b09ca6f673d1b08'
         '599745ed2bec4489e83991cff89c46ee')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p0 -i ../disable-werror.patch
  
  make CXFLAGS="$CFLAGS"
  make MDASSEMBLE_AUTO=1 mdassemble

}

package() {
  cd $srcdir/$pkgname-$pkgver
  make INSTALL=/usr/bin/install DESTDIR=$pkgdir install
  make SYSTEMD_DIR=$pkgdir/usr/lib/systemd/system install-systemd
  
  install -D -m755 mdassemble $pkgdir/sbin/mdassemble
  install -D -m644 ../mdadm.conf $pkgdir/etc/mdadm.conf
  install -D -m644 ../mdadm_install $pkgdir/usr/lib/initcpio/install/mdadm
  install -D -m644 ../mdadm_hook $pkgdir/usr/lib/initcpio/hooks/mdadm
  install -D -m644 ../mdadm_udev_install $pkgdir/usr/lib/initcpio/install/mdadm_udev
}
