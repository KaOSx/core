
# KEEP LIBTOOL FILES!
pkgname=neon
pkgver=0.31.0
pkgrel=1
pkgdesc="HTTP and WebDAV client library with a C interface"
arch=('x86_64')
url="http://www.webdav.org/neon/"
license=('GPL' 'LGPL')
depends=('krb5' 'expat' 'ca-certificates')
options=('libtool')
source=("http://www.webdav.org/neon/${pkgname}-${pkgver}.tar.gz")
sha256sums=('80556f10830431476d1394c1f0af811f96109c4c4d119f0a9569b28c7526bda5')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --with-expat \
    --enable-shared \
    --disable-static \
    --with-ssl=openssl \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
