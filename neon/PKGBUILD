
# KEEP LIBTOOL FILES!
pkgname=neon
pkgver=0.32.4
pkgrel=1
pkgdesc="HTTP and WebDAV client library with a C interface"
arch=('x86_64')
url="https://notroj.github.io/neon/"
license=('GPL' 'LGPL')
depends=('krb5' 'expat' 'ca-certificates')
makedepends=('xmlto')
options=('libtool')
source=("https://notroj.github.io/neon/${pkgname}-${pkgver}.tar.gz")
sha256sums=('b1e2120e4ae07df952c4a858731619733115c5f438965de4fab41d6bf7e7a508')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --with-expat \
    --enable-shared \
    --disable-static \
    --with-ssl=openssl \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make 
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
