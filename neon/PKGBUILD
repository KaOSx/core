
# KEEP LIBTOOL FILES!
pkgname=neon
pkgver=0.31.1
pkgrel=1
pkgdesc="HTTP and WebDAV client library with a C interface"
arch=('x86_64')
url="https://notroj.github.io/neon/"
license=('GPL' 'LGPL')
depends=('krb5' 'expat' 'ca-certificates')
options=('libtool')
source=("https://notroj.github.io/neon/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c9dfcee723050df37ce18ba449d7707b78e7ab8230f3a4c59d9112e17dc2718d')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --with-expat \
    --enable-shared \
    --disable-static \
    --with-ssl=openssl \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
