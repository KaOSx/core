
# KEEP LIBTOOL FILES!
pkgname=neon
pkgver=0.32.0
pkgrel=1
pkgdesc="HTTP and WebDAV client library with a C interface"
arch=('x86_64')
url="https://notroj.github.io/neon/"
license=('GPL' 'LGPL')
depends=('krb5' 'expat' 'ca-certificates')
makedepends=('xmlto')
options=('libtool')
source=("https://notroj.github.io/neon/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c8b3df54aec197b17d5b164cf97d843b7c5bdad1099a732983edb52e36d73dc4')

build() {
  cd ${pkgname}-${pkgver}
  sed -i '/CFLAGS=.*save_CPPFLAGS/s/CPP/C/' configure

  ./configure --prefix=/usr \
    --with-expat \
    --enable-shared \
    --disable-static \
    --with-ssl=openssl \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make 
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
