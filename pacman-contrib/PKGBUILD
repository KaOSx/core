
pkgname=pacman-contrib
pkgver=1.4.0
pkgrel=1
pkgdesc="Contributed scripts and tools for pacman systems, including paccache and rank-mirrors"
arch=('x86_64')
url="https://git.archlinux.org/pacman-contrib.git/about/"
license=('GPL')
depends=('pacman' 'mlocate' 'fakeroot' 'perl')
makedepends=('asciidoc')
source=("https://git.archlinux.org/pacman-contrib.git/snapshot/${pkgname}-${pkgver}.tar.gz")
md5sums=('3a88def1276abe45d0705babfa7ad95d')

build() {
  cd ${pkgname}-${pkgver}
  
  ./autogen.sh
  ./configure --prefix=/usr \
    --sysconfdir=/etc 
  make 
}

check() {
  make -C ${pkgname}-${pkgver} check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
  
  # stop rankmirrors fail
  sed -e 's|/core}"|/core}" echo|' -i ${pkgdir}/usr/bin/rankmirrors
}

