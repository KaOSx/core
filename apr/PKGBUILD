
pkgname=apr
pkgver=1.7.4
pkgrel=1
pkgdesc="The Apache Portable Runtime"
arch=('x86_64')
url="https://apr.apache.org/"
depends=('util-linux')
options=('!libtool')
license=('APACHE')
source=("https://www.apache.org/dist/apr/apr-${pkgver}.tar.bz2")
sha256sums=('fc648de983f3a2a6c9e78dea1f180639bd2fad6c06d556d4367a701fe5c35577')

build() {
  cd apr-${pkgver}

  ./configure --prefix=/usr \
    --includedir=/usr/include/apr-1 \
    --with-installbuilddir=/usr/share/apr-1/build \
    --enable-nonportable-atomics \
    --with-devrandom=/dev/urandom \
    --disable-static
  make
}

check() {
  cd apr-${pkgver}

  make -j1 check
}

package() {
  cd apr-${pkgver}

  make DESTDIR=${pkgdir} install
}
