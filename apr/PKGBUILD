
pkgname=apr
pkgver=1.6.5
pkgrel=2
pkgdesc="The Apache Portable Runtime"
arch=('x86_64')
url="https://apr.apache.org/"
depends=('util-linux')
options=('!libtool')
license=('APACHE')
source=("https://www.apache.org/dist/apr/apr-${pkgver}.tar.bz2")
sha256sums=('a67ca9fcf9c4ff59bce7f428a323c8b5e18667fdea7b0ebad47d194371b0a105')

build() {
  cd apr-${pkgver}

  ./configure --prefix=/usr \
    --includedir=/usr/include/apr-1 \
    --with-installbuilddir=/usr/share/apr-1/build \
    --enable-nonportable-atomics \
    --with-devrandom=/dev/urandom \
    --disable-static
  make
}

check() {
  cd apr-${pkgver}
  
  make -j1 check
}

package() {
  cd apr-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
