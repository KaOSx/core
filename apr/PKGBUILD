
pkgname=apr
pkgver=1.5.1
pkgrel=1
pkgdesc="The Apache Portable Runtime"
arch=('x86_64')
url="http://apr.apache.org/"
depends=('util-linux')
options=('!libtool')
license=('APACHE')
source=("http://www.apache.org/dist/apr/apr-${pkgver}.tar.bz2")
md5sums=('5486180ec5a23efb5cae6d4292b300ab')

build() {
  cd "${srcdir}/apr-${pkgver}"

  ./configure --prefix=/usr \
    --includedir=/usr/include/apr-1 \
    --with-installbuilddir=/usr/share/apr-1/build \
    --enable-nonportable-atomics \
    --with-devrandom=/dev/urandom \
    --disable-static
  make
}

check() {
  cd "${srcdir}/apr-${pkgver}"
  
  make -j1 check
}

package() {
  cd "${srcdir}/apr-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
}
