
pkgname=openssl
pkgver=1.1.1.19
_pkgver=1.1.1s
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=('perl' 'zlib')
optdepends=('ca-certificates')
options=('!makeflags')
backup=('etc/ssl/openssl.cnf')
source=("https://www.openssl.org/source/${pkgname}-${_pkgver}.tar.gz"
        'ca-dir_openssl1.1.patch')
sha256sums=('c5ac01e760ee6ff0dab61d6b2bbd30146724d063eb322180c6f18a6f74e4b6aa'
            'd6dd76398e67f07640e49e7a6b0f0585801dcd6a031b72bf4d19938562512c4d')

build() {
    cd ${pkgname}-${_pkgver}

    # set ca dir to /etc/ssl by default
    patch -p0 -i ${srcdir}/ca-dir_openssl1.1.patch

    ./Configure --prefix=/usr \
        --openssldir=/etc/ssl \
        --libdir=lib \
        shared zlib enable-ec_nistp_64_gcc_128 \
        linux-x86_64 \
        -Wa,--noexecstack ${CFLAGS} ${LDFLAGS} \

    make depend
    make
}

check() {
    cd ${pkgname}-${_pkgver}
    # the test fails due to missing write permissions in /etc/ssl
    # revert this patch for make test
    patch -p0 -R -i ${srcdir}/ca-dir_openssl1.1.patch
    # /test/recipes/30-test_afalg.t                  (Wstat: 256 Tests: 1 Failed: 1)
    make test
    patch -p0 -i ${srcdir}/ca-dir_openssl1.1.patch
}

package() {
    cd ${pkgname}-${_pkgver}
    make DESTDIR=${pkgdir} MANDIR=/usr/share/man MANSUFFIX=ssl install

    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
