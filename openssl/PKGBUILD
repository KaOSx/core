
pkgname=openssl
pkgver=3.4.0
_pkgver=3.4.0
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('x86_64')
url='https://www.openssl.org'
license=('Apache-2.0')
depends=('perl' 'zlib')
optdepends=('ca-certificates')
options=('!makeflags')
backup=('etc/ssl/openssl.cnf')
source=("https://github.com/openssl/openssl/releases/download/${pkgname}-${_pkgver}/${pkgname}-${_pkgver}.tar.gz"
        'CA.diff'
        'openssl.diff')
sha256sums=('e15dda82fe2fe8139dc2ac21a36d4ca01d5313c75f99f46c4e8a27709b7294bf'
            '8f2248556951c398334bd16fe944bf0b5acfcbd5d18f7b7975d6b89ff622e4e0'
            '9e7275812f96caa39d7dc2eddfdb8b020b59154827ba60e665f3701985d645de')

build() {
    cd ${pkgname}-${_pkgver}

    # set ca dir to /etc/ssl by default
    patch -p1 -i ${srcdir}/CA.diff
    patch -p1 -i ${srcdir}/openssl.diff

    ./Configure --prefix=/usr \
        --openssldir=/etc/ssl \
        --libdir=lib \
        shared enable-ktls zlib enable-ec_nistp_64_gcc_128 \
        linux-x86_64 \
        -Wa,--noexecstack ${CFLAGS} ${LDFLAGS} \

    make depend
    make
}

check() {
    cd ${pkgname}-${_pkgver}
    # the test fails due to missing write permissions in /etc/ssl
    # revert this patch for make test
    patch -p1 -R -i ${srcdir}/CA.diff
    patch -p1 -R -i ${srcdir}/openssl.diff
    # no fail
    make test
    patch -p1 -i ${srcdir}/CA.diff
    patch -p1 -i ${srcdir}/openssl.diff
}

package() {
    cd ${pkgname}-${_pkgver}
    make DESTDIR=${pkgdir} MANDIR=/usr/share/man MANSUFFIX=ssl install

    install -D -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
}
