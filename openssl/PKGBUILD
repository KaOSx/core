
pkgname=openssl
pkgver=1.1.1.1
_pkgver=1.1.1a
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=('perl' 'zlib')
optdepends=('ca-certificates')
options=('!makeflags')
backup=('etc/ssl/openssl.cnf')
source=("https://www.openssl.org/source/${pkgname}-${_pkgver}.tar.gz"
        'no-rpath_openssl1.1.patch'
        'ca-dir_openssl1.1.patch')
sha256sums=('fc20130f8b7cbd2fb918b2f14e2f429e109c31ddd0fb38fc5d71d9ffed3f9f41'
            '8218a810ec36a41d0a452fc9a1a6d9a9bef3e5cacd59aa46b6612ac9bf12740e'
            'd6dd76398e67f07640e49e7a6b0f0585801dcd6a031b72bf4d19938562512c4d')

build() {
	cd ${pkgname}-${_pkgver}

	#patch -p0 -i ${srcdir}/no-rpath_openssl1.1.patch
	# set ca dir to /etc/ssl by default
	patch -p0 -i ${srcdir}/ca-dir_openssl1.1.patch
	
	./Configure --prefix=/usr --openssldir=/etc/ssl --libdir=lib \
		shared zlib enable-ec_nistp_64_gcc_128 \
		linux-x86_64 \
		-Wa,--noexecstack ${CFLAGS} ${LDFLAGS} \

	make depend
	make
}

check() {
	cd ${pkgname}-${_pkgver}
	# the test fails due to missing write permissions in /etc/ssl
	# revert this patch for make test
	patch -p0 -R -i ${srcdir}/ca-dir_openssl1.1.patch
	make test
	patch -p0 -i ${srcdir}/ca-dir_openssl1.1.patch
}

package() {
	cd ${pkgname}-${_pkgver}
	make DESTDIR=${pkgdir} MANDIR=/usr/share/man MANSUFFIX=ssl install
	
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
