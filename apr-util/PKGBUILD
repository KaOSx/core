
pkgname=apr-util
pkgver=1.6.1
pkgrel=5
pkgdesc="The Apache Portable Runtime"
arch=('x86_64')
url="https://apr.apache.org/"
depends=('apr' 'gdbm' 'expat' 'db' 'libldap' 'unixodbc')
options=('!libtool')
license=('APACHE')
source=("https://www.apache.org/dist/apr/apr-util-${pkgver}.tar.bz2")
sha256sums=('d3e12f7b6ad12687572a3a39475545a072608f4ba03a6ce8a3778f607dd0035b')

build() {
  cd apr-util-${pkgver}
  ./configure --prefix=/usr \
    --with-apr=/usr \
    --without-pgsql \
    --without-mysql \
    --without-sqlite2 \
    --without-sqlite3 \
    --with-berkeley-db=/usr \
    --with-gdbm=/usr \
    --with-ldap
  make
}

package() {
  cd apr-util-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
