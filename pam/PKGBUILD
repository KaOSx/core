
pkgname=pam
pkgver=1.6.1
pkgrel=2
pkgdesc="PAM (Pluggable Authentication Modules) library"
arch=('x86_64')
license=('GPL2')
url="https://github.com/linux-pam/linux-pam"
depends=('glibc' 'cracklib' 'libnsl' 'libtirpc' 'libxcrypt')
makedepends=('w3m' 'docbook-xml' 'docbook-xsl')
backup=(etc/security/{access.conf,group.conf,limits.conf,namespace.conf,namespace.init,pam_env.conf,time.conf} etc/pam.d/other etc/default/passwd etc/environment)
source=("https://github.com/linux-pam/linux-pam/releases/download/v${pkgver}/Linux-PAM-${pkgver}.tar.xz"
        'other')
options=('!libtool' '!emptydirs')
md5sums=('8ad1e72d1ff6480d8e0af658e2d7b768'
         '6e6c8719e5989d976a14610f340bd33a')

build() {
  cd Linux-PAM-${pkgver}

  ./configure \
    --libdir=/usr/lib \
    --with-systemdunitdir=/usr/lib/systemd/system \
    --enable-db=no \
    --disable-logind \
    --enable-lastlog
  make
}

check() {
  cd Linux-PAM-${pkgver}

  make check
}

package() {
  cd Linux-PAM-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m644 ../other ${pkgdir}/etc/pam.d/other

  chmod +s ${pkgdir}/sbin/unix_chkpwd
}

