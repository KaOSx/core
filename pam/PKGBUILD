
pkgname=pam
pkgver=1.5.1
pkgrel=2
pkgdesc="PAM (Pluggable Authentication Modules) library"
arch=('x86_64')
license=('GPL2')
url="https://github.com/linux-pam/linux-pam"
depends=('glibc' 'db' 'cracklib' 'libtirpc')
makedepends=('w3m' 'docbook-xml' 'docbook-xsl')
backup=(etc/security/{access.conf,group.conf,limits.conf,namespace.conf,namespace.init,pam_env.conf,time.conf} etc/pam.d/other etc/default/passwd etc/environment)
source=("https://github.com/linux-pam/linux-pam/releases/download/v${pkgver}/Linux-PAM-${pkgver}.tar.xz"
        'other')
options=('!libtool' '!emptydirs')
md5sums=('155f2a31d07077b2c63a1f135876c31b'
         '6e6c8719e5989d976a14610f340bd33a')

build() {
  cd Linux-PAM-${pkgver}
  
  ./configure \
    --libdir=/usr/lib
  make
}

check() {
  cd Linux-PAM-${pkgver}
  
  make check
}

package() {
  cd Linux-PAM-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m644 ../other ${pkgdir}/etc/pam.d/other

  chmod +s ${pkgdir}/sbin/unix_chkpwd
}

