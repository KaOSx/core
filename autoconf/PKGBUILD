
pkgname=autoconf
pkgver=2.71
pkgrel=2
pkgdesc="Package of M4 macros that produce shell scripts to automatically configure software source code packages."
arch=('x86_64')
license=('GPL2' 'GPL3' 'custom')
url="https://www.gnu.org/software/autoconf"
groups=('base-devel')
depends=('awk' 'm4' 'diffutils' 'bash')
source=("https://ftp.gnu.org/gnu/autoconf/${pkgname}-${pkgver}.tar.xz")
md5sums=('12cfa1687ffa2606337efe1a64416106')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make 
}

check() {
  cd ${pkgname}-${pkgver}
  # 5 failed (4 expected failures)
  #310: AC_PROG_CPP without warnings                    FAILED (c.at:165)
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install 

  install -Dm644 COPYING.EXCEPTION $pkgdir/usr/share/licenses/autoconf/COPYING.EXCEPTION
  rm -f ${pkgdir}/usr/share/info/standards.info
}
