
pkgname=cloog
pkgver=0.18.1
pkgrel=4
pkgdesc="Library that generates loops for scanning polyhedra"
arch=('x86_64')
url="http://www.bastoul.net/cloog/"
license=('GPL')
depends=('isl' 'gmp')
options=('!libtool')
source=("http://www.bastoul.net/cloog/pages/download/${pkgname}-${pkgver}.tar.gz"
        'isl-1.3-compat.patch')
md5sums=('e34fca0540d840e5d0f6427e98c92252'
         '976c999b44c6e364455a670d12523242')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i $srcdir/isl-1.3-compat.patch
  
  ./configure --prefix=/usr --with-isl=system
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}/" install
}
