
pkgname=dracut
pkgver=059
pkgrel=1
pkgdesc="Event driven initramfs tool used to create an initramfs image by copying tools and files from an installed system and combining it with the dracut framework"
arch=('x86_64')
url="https://github.com/dracutdevs/dracut"
license=('GPLv2')
depends=('binutils' 'cpio' 'kmod' 'util-linux' 'libarchive' 'coreutils'
         'bash' 'findutils' 'grep' 'filesystem' 'gzip' 'systemd' 'pkg-config'
         'busybox')
makedepends=('asciidoc' 'bash-completion' 'libxslt')
backup=('etc/dracut.conf')
provides=('initrd')
#source=("https://mirrors.edge.kernel.org/pub/linux/utils/boot/dracut/${pkgname}-${pkgver}.tar.xz"
source=("https://github.com/dracutdevs/dracut/archive/refs/tags/${pkgver}.tar.gz"
        'kaos.conf'
        'aufs-mount.sh'
        'module-setup.sh')
sha256sums=('eabf0bb685420c1e1d5475b6855ef787104508f0135ff570312845256e0fcecf'
            '1b209a69645925372f6fd5aee9bf23510e5f72c797c2f61a0b8e5aa0e90d27f8'
            '5351ed6cff01c8b6498dde09ad67074432e3bea363d5ee2de752a15ea9f3aaea'
            'd99bbc2b013421eb94b7396126533f047585ca45d6755c167b8c8471d5f4fdfd')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --systemdsystemunitdir=/usr/lib/systemd/system \
    --bashcompletiondir=$(pkg-config --variable=completionsdir bash-completion)
  make
}

package() {
  cd ${pkgname}-${pkgver}

  DESTDIR=${pkgdir} make install
  install -Dm755 ../kaos.conf ${pkgdir}/etc/dracut.conf.d/01-kaos.conf
  # add custom aufs module
  #install -Dm755 ../aufs-mount.sh ${pkgdir}/usr/lib/dracut/modules.d/90aufs/aufs-mount.sh
  #install -Dm755 ../module-setup.sh ${pkgdir}/usr/lib/dracut/modules.d/90aufs/module-setup.sh
}
