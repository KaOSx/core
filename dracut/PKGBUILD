
pkgname=dracut
pkgver=103
pkgrel=2
_commit=4980bad34775da715a2639b736cba5e65a8a2604
pkgdesc="Event driven initramfs tool used to create an initramfs image by copying tools and files from an installed system and combining it with the dracut framework"
arch=('x86_64')
url="https://github.com/dracutdevs/dracut"
license=('GPLv2')
depends=('binutils' 'cpio' 'kmod' 'util-linux' 'libarchive' 'coreutils'
         'bash' 'findutils' 'grep' 'filesystem' 'gzip' 'systemd' 'pkg-config'
         'busybox')
makedepends=('asciidoc' 'bash-completion' 'libxslt')
backup=('etc/dracut.conf')
provides=('initrd')
#source=("https://mirrors.edge.kernel.org/pub/linux/utils/boot/dracut/${pkgname}-${pkgver}.tar.xz"
source=("https://github.com/dracut-ng/dracut-ng/archive/refs/tags/${pkgver}.tar.gz"
        'kaos.conf'
        'aufs-mount.sh'
        'module-setup.sh')
sha256sums=('9a92b4f0643926a65162171d68b9525fc93e6e82f455a4b3938db385a841bda8'
            '1b209a69645925372f6fd5aee9bf23510e5f72c797c2f61a0b8e5aa0e90d27f8'
            '5351ed6cff01c8b6498dde09ad67074432e3bea363d5ee2de752a15ea9f3aaea'
            'd99bbc2b013421eb94b7396126533f047585ca45d6755c167b8c8471d5f4fdfd')

build() {
  cd ${pkgname}-ng-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --systemdsystemunitdir=/usr/lib/systemd/system \
    --bashcompletiondir=$(pkg-config --variable=completionsdir bash-completion)
  make
}

package() {
  cd ${pkgname}-ng-${pkgver}

  DESTDIR=${pkgdir} make install
  install -Dm755 ../kaos.conf ${pkgdir}/etc/dracut.conf.d/01-kaos.conf
  # add custom aufs module
  #install -Dm755 ../aufs-mount.sh ${pkgdir}/usr/lib/dracut/modules.d/90aufs/aufs-mount.sh
  #install -Dm755 ../module-setup.sh ${pkgdir}/usr/lib/dracut/modules.d/90aufs/module-setup.sh
}
