
# with cmake build, always check for .so up
pkgname=libarchive
pkgver=3.7.1
_pkgver=3.7.1
pkgrel=1
pkgdesc="library that can create and read several streaming archive formats"
arch=('x86_64')
url="https://libarchive.org/"
license=('BSD')
depends=('zlib' 'bzip2' 'xz' 'acl' 'openssl' 'expat' 'lzo2' 'attr' 'zstd' 'nettle')
makedepends=('cmake') # 'clang')
source=("https://github.com/libarchive/libarchive/releases/download/v${_pkgver}/${pkgname}-${_pkgver}.tar.xz")
sha256sums=('b17403ce670ff18d8e06fea05a9ea9accf70678c88f1b9392a2e29b51127895f')

build() {
  mkdir -p build
  cd build

  #export CC=/usr/bin/clang
  #export CXX=/usr/bin/clang++

  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_LIBXML2=OFF \
    -DENABLE_LZO=ON
  make
}

check() {
  cd build

  make test
}

package() {
  cd build
  make DESTDIR=${pkgdir} install

  install -D -m644 ../${pkgname}-${_pkgver}/COPYING ${pkgdir}/usr/share/licenses/libarchive/COPYING
}
