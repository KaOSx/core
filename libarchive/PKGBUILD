
pkgname=libarchive
pkgver=3.1.2
pkgrel=5
pkgdesc="library that can create and read several streaming archive formats"
arch=('x86_64')
url="http://libarchive.org/"
license=('BSD')
depends=('zlib' 'bzip2' 'xz' 'acl' 'openssl' 'expat' 'lzo2' 'attr')
source=("http://libarchive.org/downloads/$pkgname-$pkgver.tar.gz"
        '0001-mtree-fix-line-filename-length-calculation.patch'
        "http://pkgs.fedoraproject.org/cgit/libarchive.git/plain/libarchive-3.1.2-acl.patch"
        'archive_write.patch'
        'mtree.patch')
md5sums=('efad5a503f66329bb9d2f4308b5de98a'
         'fda89c145bbcd793a96b06b463ef6a72'
         'a5c995661c62429ceff2c23ea322393b'
         '39ed4bd2c0757abe764091cf4785f564'
         '87ef2c0e9ddbd8293784c0f9d4010198')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i "$srcdir/0001-mtree-fix-line-filename-length-calculation.patch"
  # https://code.google.com/p/libarchive/issues/detail?id=329
  patch -p1 -i "$srcdir/libarchive-3.1.2-acl.patch"
  # CVE-2013-0211 https://github.com/libarchive/libarchive/commit/22531545514043e04633e1c015c7540b9de9dbe4
  patch -p1 -i "$srcdir/archive_write.patch"
  # https://github.com/libarchive/libarchive/commit/977bf2a49484239f7a7b6ce08bfa9da413a27ead
  patch -p1 -i "$srcdir/mtree.patch"

  ./configure --prefix=/usr --without-xml2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}
