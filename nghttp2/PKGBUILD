
pkgname=nghttp2
pkgver=1.64.0
pkgrel=1
pkgdesc='Implementation of HTTP/2 and its header compression algorithm HPACK in C.'
arch=('x86_64')
url='https://nghttp2.org/'
license=('MIT')
depends=('glibc')
options=('!emptydirs')
source=("https://github.com/nghttp2/nghttp2/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('88bb94c9e4fd1c499967f83dece36a78122af7d5fb40da2019c56b9ccc6eb9dd')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --enable-static=no \
    --disable-failmalloc \
    --without-cython \
    --enable-lib-only
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/libnghttp2/COPYING
}
