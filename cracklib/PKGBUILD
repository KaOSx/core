
pkgname=cracklib
pkgver=2.10.1
pkgrel=1
pkgdesc="Password Checking Library"
arch=('x86_64')
license=('GPL')
url="https://github.com/cracklib/cracklib"
depends=('glibc' 'zlib')
options=('!libtool')
source=("https://github.com/cracklib/cracklib/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        "https://github.com/drfiemost/cracklib/commit/f795a7d04a04253a3cb9faa30b57d8eba713b0cc.diff")
md5sums=('8256d5f9e8e7333d7cc8034ef7ef15e6'
         '79ab4eb414820ffa3c568cf3d33a5d5f')

build() {
  cd ${pkgname}-${pkgver}
  # https://github.com/cracklib/cracklib/issues/52
  #patch -p2 -i ${srcdir}/f795a7d04a04253a3cb9faa30b57d8eba713b0cc.diff

  ./configure --prefix=/usr --without-python
  make 
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 dicts/cracklib-small ${pkgdir}/usr/share/dict/cracklib-small
  sh ./util/cracklib-format dicts/cracklib-small \
    | sh ./util/cracklib-packer ${pkgdir}/usr/share/cracklib/pw_dict
}

