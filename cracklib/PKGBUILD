
pkgname=cracklib
pkgver=2.9.7
pkgrel=2
pkgdesc="Password Checking Library"
arch=('x86_64')
license=('GPL')
url="https://github.com/cracklib/cracklib"
depends=('glibc' 'zlib')
options=('!libtool')
source=("https://github.com/cracklib/cracklib/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('0d68de25332cee5660850528a385427f')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr --without-python
  make 
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -Dm644 dicts/cracklib-small ${pkgdir}/usr/share/dict/cracklib-small
  sh ./util/cracklib-format dicts/cracklib-small \
    | sh ./util/cracklib-packer ${pkgdir}/usr/share/cracklib/pw_dict
}

