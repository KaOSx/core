
pkgname=cronie
pkgver=1.7.2
pkgrel=1
pkgdesc='Daemon that runs specified programs at scheduled times and related tools'
url='https://github.com/cronie-crond/cronie'
# should move soon to:
# https://pagure.io/cronie, now available at https://github.com/cronie-crond/cronie
license=('custom:BSD')
arch=('x86_64')
depends=('pam' 'bash' 'run-parts')
optdepends=('pm-utils: defer anacron on battery power')
conflicts=('dcron')
replaces=('dcron')
provides=('cron')
backup=('etc/anacrontab'
        'etc/sysconfig/crond'
        'etc/pam.d/crond'
        'etc/cron.deny')
source=("https://github.com/cronie-crond/cronie/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'cron.deny'
        "https://github.com/cronie-crond/cronie/commit/7700b1465d32ddb1d3988e9af852af6f0f5cd66e.diff")
md5sums=('2dacf4a7198b26dbd497a418cf31443d'
         '2962d9812fbbbdee1bd7f1c1ccb5e372'
         '913cb0c6debedf3899198764b5693206')

build() {
	cd ${pkgname}-${pkgver}
	#patch -p1 -i ${srcdir}/7700b1465d32ddb1d3988e9af852af6f0f5cd66e.diff

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-anacron \
		--with-inotify \
		--with-pam \

	make
}

package() {
	cd ${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install

	chmod u+s ${pkgdir}/usr/bin/crontab
	install -d ${pkgdir}/var/spool/{ana,}cron
	install -d ${pkgdir}/etc/cron.{d,hourly,daily,weekly,monthly}

	install -Dm644 pam/crond ${pkgdir}/etc/pam.d/crond
	install -Dm644 ../cron.deny ${pkgdir}/etc/cron.deny
	install -Dm644 contrib/cronie.systemd ${pkgdir}/usr/lib/systemd/system/cronie.service

	install -Dm644 crond.sysconfig ${pkgdir}/etc/sysconfig/crond
	install -Dm644 contrib/anacrontab ${pkgdir}/etc/anacrontab
	install -Dm644 contrib/0hourly ${pkgdir}/etc/cron.d/0hourly
	install -Dm755 contrib/0anacron ${pkgdir}/etc/cron.hourly/0anacron

	install -Dm644 COPYING ${pkgdir}/usr/share/licenses/cronie/COPYING
}
