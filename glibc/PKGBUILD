# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc
# NOTE: valgrind requires rebuilt with each major glibc version

pkgname=glibc
pkgver=2.17
pkgrel=1
_glibcdate=20120419
pkgdesc="GNU C Library"
arch=('x86_64')
url="http://www.gnu.org/software/libc"
license=('GPL' 'LGPL')
groups=('base')
depends=('linux-api-headers>=3.7' 'tzdata')
makedepends=('gcc>=4.7')
backup=(etc/gai.conf
        etc/locale.gen
        etc/nscd.conf)
options=('!strip')
install=glibc.install
source=(http://ftp.gnu.org/gnu/libc/${pkgname}-${pkgver}.tar.xz{,.sig}
        glibc-2.17-sync-with-linux37.patch
        glibc-2.17-getaddrinfo-stack-overflow.patch
        glibc-2.17-regexp-matcher-overrun.patch
        nscd.service
        nscd.tmpfiles
        locale.gen.txt
        locale-gen
        glibc.conf)
md5sums=('87bf675c8ee523ebda4803e8e1cec638'
         'SKIP'
         'fb99380d94598cc76d793deebf630022'
         '56d5f2c09503a348281a20ae404b7de3'
         '200acc05961b084ee00dde919e64f82d'
         'f3fb741c73ae5fd46e03beae4e6948c9'
         'bccbe5619e75cf1d97312ec3681c605c'
         '07ac979b6ab5eeb778d55f041529d623'
         '476e9113489f93b348b21e144b6a8fcf'
         '8b3298de2392a10905a64ceccf56d2c1')

mksource() {
    git clone git://sourceware.org/git/glibc.git
    pushd glibc
        git checkout -b glibc-${pkgver}-chakra origin/release/${pkgver}/master
    popd
    tar -cvJf glibc-${pkgver}_${_glibcdate}.tar.xz glibc/*
    md5sum glibc-${pkgver}_${_glibcdate}.tar.xz
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # combination of upstream commits 318cd0b, b540704 and fc1abbe
  patch -p1 -i ${srcdir}/glibc-2.17-sync-with-linux37.patch

  # CVE-2013-1914 - upstream commit 1cef1b19
  patch -p1 -i ${srcdir}/glibc-2.17-getaddrinfo-stack-overflow.patch

  # CVE-2013-0242 - upstream commit a445af0b
  patch -p1 -i ${srcdir}/glibc-2.17-regexp-matcher-overrun.patch
  
  cd ${srcdir}
  mkdir glibc-build
  cd glibc-build

  echo "slibdir=/lib" >> configparms
  
  # remove hardening options for building libraries
  CFLAGS=${CFLAGS/-fstack-protector/}
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}

  ${srcdir}/glibc-${pkgver}/configure --prefix=/usr \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --with-headers=/usr/include \
      --enable-add-ons=nptl,libidn \
      --enable-obsolete-rpc \
      --enable-kernel=2.6.32 \
      --enable-bind-now --disable-profile \
      --enable-stackguard-randomization \
      --enable-multi-arch

  # build libraries with hardening disabled
  echo "build-programs=no" >> configparms
  make
  
  # re-enable hardening for programs
  sed -i "/build-programs=/s#no#yes#" configparms
  echo "CC += -fstack-protector -D_FORTIFY_SOURCE=2" >> configparms
  echo "CXX += -fstack-protector -D_FORTIFY_SOURCE=2" >> configparms
  make

  # remove harding in preparation to run test-suite
  sed -i '2,4d' configparms
}

check() {
  # bug to file - the linker commands need to be reordered
  LDFLAGS=${LDFLAGS/--as-needed,/}

  cd ${srcdir}/glibc-build
  make check
}

package() {
  cd ${srcdir}/glibc-build

  install -dm755 ${pkgdir}/etc
  touch ${pkgdir}/etc/ld.so.conf

  make install_root=${pkgdir} install

  rm -f ${pkgdir}/etc/ld.so.{cache,conf}

  install -dm755 ${pkgdir}/{,usr/{sbin,lib/{,locale,systemd/system,tmpfiles.d}}}

  install -m644 ${srcdir}/glibc-${pkgver}/nscd/nscd.conf ${pkgdir}/etc/nscd.conf
  sed -i -e 's/^\tserver-user/#\tserver-user/' ${pkgdir}/etc/nscd.conf
  install -m644 ${srcdir}/nscd.service ${pkgdir}/usr/lib/systemd/system
  install -m644 ${srcdir}/nscd.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/nscd.conf

  install -m755 ${srcdir}/locale-gen ${pkgdir}/usr/sbin
  install -m644 ${srcdir}/glibc-${pkgver}/posix/gai.conf ${pkgdir}/etc/gai.conf

  # create /etc/locale.gen
  install -m644 ${srcdir}/locale.gen.txt ${pkgdir}/etc/locale.gen
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
    ${srcdir}/glibc-${pkgver}/localedata/SUPPORTED >> ${pkgdir}/etc/locale.gen

  # fix for the linker
  sed -i '/RTLDLIST/s%lib64%lib%' ${pkgdir}/usr/bin/ldd
  # Comply with multilib binaries, they look for the linker in /lib64
  mkdir ${pkgdir}/lib64
  cd ${pkgdir}/lib64
  ln -v -s ../lib/ld* .

  cd ${pkgdir}
  strip $STRIP_BINARIES sbin/{ldconfig,sln} \
                        usr/bin/{gencat,getconf,getent,iconv,locale} \
                        usr/bin/{localedef,pcprofiledump,rpcgen,sprof} \
                        usr/lib/getconf/* \
                        usr/sbin/{iconvconfig,nscd}

  strip $STRIP_STATIC usr/lib/*.a 

  strip $STRIP_SHARED lib/{libanl,libBrokenLocale,libcidn,libcrypt}-${pkgver}.so \
                      lib/libnss_{compat,dns,files,hesiod,nis,nisplus}-${pkgver}.so \
                      lib/{libdl,libm,libnsl,libresolv,librt,libutil}-${pkgver}.so \
                      lib/{libmemusage,libpcprofile,libSegFault}.so \
                      usr/lib/{pt_chown,{audit,gconv}/*.so}

  # Add /usr/lib32 to the default library search path
  install -Dm644 "$srcdir/glibc.conf" "$pkgdir/etc/ld.so.conf.d/glibc.conf"
}
