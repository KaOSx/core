
pkgname=perl
pkgver=5.28.2
pkgrel=1
pkgdesc="A highly capable, feature-rich programming language"
arch=('x86_64')
license=('GPL' 'PerlArtistic')
url="https://www.perl.org"
groups=('base')
depends=('gdbm' 'db' 'coreutils' 'glibc' 'sh')
provides=('perl-archive-tar=2.30'
          'perl-attribute-handlers=1.01'
          'perl-autodie=2.29'
          'perl-autoloader=5.74'
          'perl-autouse=1.11'
          'perl-b-debug=1.26'
          'perl-base=2.27'
          'perl-bignum=0.49'
          'perl-carp=1.50'
          'perl-compress-raw-bzip2=2.074'
          'perl-compress-raw-zlib=2.076'
          'perl-config-perl-v=0.29'
          'perl-constant=1.33'
          'perl-cpan-meta-requirements=2.140'
          'perl-cpan-meta-yaml=0.018'
          'perl-cpan-meta=2.150010'
          'perl-cpan=2.20'
          'perl-data-dumper=2.170'
          'perl-db_file=1.840'
          'perl-devel-ppport=3.40'
          'perl-devel-selfstubber=1.06'
          'perl-digest-md5=2.55'
          'perl-digest-sha=6.01'
          'perl-digest=1.17_01'
          'perl-dumpvalue=1.18'
          'perl-encode=2.97'
          'perl-encoding-warnings=0.13'
          'perl-env=1.04'
          'perl-experimental=0.019'
          'perl-exporter=5.73'
          'perl-extutils-cbuilder=0.280230'
          'perl-extutils-constant=0.25'
          'perl-extutils-install=2.14'
          'perl-extutils-makemaker=7.34'
          'perl-extutils-manifest=1.70'
          'perl-extutils-parsexs=3.39'
          'perl-file-fetch=0.56'
          'perl-file-path=2.15'
          'perl-file-temp=0.2304'
          'perl-filter-simple=0.95'
          'perl-filter-util-call=1.58'
          'perl-getopt-long=2.5'
          'perl-http-tiny=0.070'
          'perl-i18n-collate=1.02'
          'perl-i18n-langtags=0.43'
          'perl-if=0.0608'
          'perl-io-compress=2.074'
          'perl-io-socket-ip=0.39'
          'perl-io-zlib=1.10'
          'perl-io=1.39'
          'perl-ipc-cmd=1.00'
          'perl-ipc-sysv=2.07'
          'perl-json-pp=2.97001'
          'perl-lib=0.64'
          'perl-libnet=3.11'
          'perl-locale-codes=3.56'
          'perl-locale-maketext-simple=0.21_01'
          'perl-locale-maketext=1.29'
          'perl-math-bigint-fastcalc=0.5006'
          'perl-math-bigint=1.999811'
          'perl-math-bigrat=0.2613'
          'perl-math-complex=1.5901'
          'perl-memoize=1.03_01'
          'perl-mime-base64=3.15'
          'perl-module-corelist=5.20181129_28'
          'perl-module-load-conditional=0.68'
          'perl-module-load=0.32'
          'perl-module-loaded=0.08'
          'perl-module-metadata=1.000033'
          'perl-net-ping=2.62'
          'perl-params-check=0.38'
          'perl-parent=0.236'
          'perl-pathtools=3.74'
          'perl-perl-ostype=1.010'
          'perl-perlfaq=5.021011'
          'perl-perlio-via-quotedprint=0.08'
          'perl-pod-checker=1.73'
          'perl-pod-escapes=1.07'
          'perl-pod-parser=1.63'
          'perl-pod-perldoc=3.2801'
          'perl-pod-simple=3.35'
          'perl-pod-usage=1.69'
          'perl-podlators=5.006'
          'perl-safe=2.40'
          'perl-scalar-list-utils=1.50'
          'perl-search-dict=1.07'
          'perl-selfloader=1.25'
          'perl-socket=2.027'
          'perl-storable=3.08'
          'perl-sys-syslog=0.35'
          'perl-term-ansicolor=4.06'
          'perl-term-cap=1.17'
          'perl-term-complete=1.403'
          'perl-term-readline=1.17'
          'perl-test-harness=3.42'
          'perl-test-simple=1.302133'
          'perl-test=1.31'
          'perl-text-abbrev=1.02'
          'perl-text-balanced=2.03'
          'perl-text-parsewords=3.30'
          'perl-text-tabs=2013.0523'
          'perl-thread-queue=3.12'
          'perl-thread-semaphore=2.13'
          'perl-threads-shared=1.58'
          'perl-threads=2.22'
          'perl-tie-file=1.02'
          'perl-tie-refhash=1.39'
          'perl-time-hires=1.9759'
          'perl-time-local=1.25'
          'perl-time-piece=1.3204'
          'perl-unicode-collate=1.25'
          'perl-unicode-normalize=1.26'
          'perl-version=0.9923'
          'perl-xsloader=0.30')
source=("http://www.cpan.org/src/5.0/perl-${pkgver}.tar.xz"
        'perlbin.sh'
        'perlbin.csh')
options=('makeflags' '!purge' 'emptydirs')
sha256sums=('0b0189bfa4b2da20e899b4bdd746ac402e8f746a58e4fcf5516484157f2aab07'
            '6681639f18f3a6f3f3d7f978b8d72ad088b27899a6fcc386b86ac104199d857b'
            '69c03f11e98c5c4af3cfd4900840189d19b74fe4fd4534523781312d0a1be810')

build() {
  cd ${pkgname}-${pkgver}

  ./Configure -des -Dusethreads -Duseshrplib -Doptimize="${CFLAGS}" \
    -Dprefix=/usr -Dvendorprefix=/usr \
    -Dprivlib=/usr/share/perl5/core_perl \
    -Darchlib=/usr/lib/perl5/core_perl \
    -Dsitelib=/usr/share/perl5/site_perl \
    -Dsitearch=/usr/lib/perl5/site_perl \
    -Dvendorlib=/usr/share/perl5/vendor_perl \
    -Dvendorarch=/usr/lib/perl5/vendor_perl \
    -Dscriptdir=/usr/bin/core_perl \
    -Dsitescript=/usr/bin/site_perl \
    -Dvendorscript=/usr/bin/vendor_perl \
    -Dinc_version_list=none \
    -Dman1dir=/usr/share/man/man1 -Dman3dir=/usr/share/man/man3 \
    -Dman1ext=1perl -Dman3ext=3perl -Dcccdlflags='-fPIC' \
    -Dlddlflags="-shared ${LDFLAGS}" -Dldflags="${LDFLAGS}"

  make
}

check() {
  cd ${pkgname}-${pkgver}
  TEST_JOBS=$(echo $MAKEFLAGS | sed 's/.*-j\([0-9][0-9]*\).*/\1/') make test_harness || true
 
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  LD_PRELOAD="/usr/lib/libfakeroot/libfakeroot.so" 
  make DESTDIR=${pkgdir} install

  ### Perl Settings ###
  # Change man page extensions for site and vendor module builds, no email address
  sed -e '/^man1ext=/ s/1perl/1p/' -e '/^man3ext=/ s/3perl/3pm/' \
      -e "/^cf_email=/ s/'.*'/''/" \
      -e "/^perladmin=/ s/'.*'/''/" \
      -i ${pkgdir}/usr/lib/perl5/core_perl/Config_heavy.pl

  ### CPAN Settings ###
  # Set CPAN default config to use the site directories.
  sed -e '/(makepl_arg =>/   s/""/"INSTALLDIRS=site"/' \
      -e '/(mbuildpl_arg =>/ s/""/"installdirs=site"/' \
      -i ${pkgdir}/usr/share/perl5/core_perl/CPAN/FirstTime.pm

  # Profile script to set paths to perl scripts.
  install -D -m755 ${srcdir}/perlbin.sh \
                   ${pkgdir}/etc/profile.d/perlbin.sh
  # Profile script to set paths to perl scripts on csh.  
  install -D -m755 ${srcdir}/perlbin.csh \
                  ${pkgdir}/etc/profile.d/perlbin.csh
  
  # add default dir                
  install -d -m755 ${pkgdir}/usr/bin/vendor_perl
  install -d -m755 ${pkgdir}/usr/bin/site_perl
  
  rm ${pkgdir}/usr/bin/perl$pkgver
  (cd ${pkgdir}/usr/bin/core_perl;  ln -sf c2ph pstruct; ln -sf s2p psed)

  # Remove all pod files *except* those under /usr/share/perl5/core_perl/pod/
  rm -f ${pkgdir}/usr/share/perl5/core_perl/*.pod
  for d in ${pkgdir}/usr/share/perl5/core_perl/*; do
    if [ -d $d -a $(basename $d) != "pod" ]; then
      find $d -name *.pod -delete
    fi
  done

  find ${pkgdir}/usr/lib -name *.pod -delete
  find ${pkgdir} -name .packlist -delete
}
