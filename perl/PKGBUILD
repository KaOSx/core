
pkgname=perl
pkgver=5.32.1
pkgrel=5
pkgdesc="A highly capable, feature-rich programming language"
arch=('x86_64')
license=('GPL' 'PerlArtistic')
url="https://www.perl.org"
groups=('base')
depends=('gdbm' 'db' 'coreutils' 'glibc' 'sh' 'libxcrypt')
provides=('perl-archive-tar=2.36'
          'perl-attribute-handlers=1.01'
          'perl-autodie=2.32'
          'perl-autoloader=5.74'
          'perl-autouse=1.11'
          'perl-base=2.27'
          'perl-bignum=0.51'
          'perl-carp=1.50'
          'perl-compress-raw-bzip2=2.093'
          'perl-compress-raw-zlib=2.093'
          'perl-config-perl-v=0.32'
          'perl-constant=1.33'
          'perl-cpan-meta-requirements=2.140'
          'perl-cpan-meta-yaml=0.018'
          'perl-cpan-meta=2.150010'
          'perl-cpan=2.27'
          'perl-data-dumper=2.174_01'
          'perl-db_file=1.853'
          'perl-devel-ppport=3.57'
          'perl-devel-selfstubber=1.06'
          'perl-digest-md5=2.55_01'
          'perl-digest-sha=6.02'
          'perl-digest=1.17_01'
          'perl-dumpvalue=1.21'
          'perl-encode=3.06'
          'perl-encoding-warnings=0.13'
          'perl-env=1.04'
          'perl-experimental=0.020'
          'perl-exporter=5.74'
          'perl-extutils-cbuilder=0.280234'
          'perl-extutils-constant=0.25'
          'perl-extutils-install=2.14'
          'perl-extutils-makemaker=7.44'
          'perl-extutils-manifest=1.72'
          'perl-extutils-parsexs=3.40'
          'perl-file-fetch=0.56'
          'perl-file-path=2.16'
          'perl-file-temp=0.2309'
          'perl-filter-simple=0.96'
          'perl-filter-util-call=1.59'
          'perl-findbin=1.51'
          'perl-getopt-long=2.51'
          'perl-http-tiny=0.076'
          'perl-i18n-collate=1.02'
          'perl-i18n-langtags=0.44'
          'perl-if=0.0608'
          'perl-io-compress=2.093'
          'perl-io-socket-ip=0.39'
          'perl-io-zlib=1.10'
          'perl-io=1.43'
          'perl-ipc-cmd=1.04'
          'perl-ipc-sysv=2.07'
          'perl-json-pp=4.04'
          'perl-lib=0.65'
          'perl-libnet=3.11'
          'perl-locale-maketext-simple=0.21_01'
          'perl-locale-maketext=1.29'
          'perl-math-bigint-fastcalc=0.5009'
          'perl-math-bigint=1.999818'
          'perl-math-bigrat=0.2614'
          'perl-math-complex=1.5901'
          'perl-memoize=1.03_01'
          'perl-mime-base64=3.15'
          'perl-module-corelist=5.20210123'
          'perl-module-load-conditional=0.70'
          'perl-module-load=0.34'
          'perl-module-loaded=0.08'
          'perl-module-metadata=1.000037'
          'perl-net-ping=2.72'
          'perl-params-check=0.38'
          'perl-parent=0.238'
          'perl-pathtools=3.78'
          'perl-perl-ostype=1.010'
          'perl-perlfaq=5.20200523'
          'perl-perlio-via-quotedprint=0.08'
          'perl-pod-checker=1.73'
          'perl-pod-escapes=1.07'
          'perl-pod-perldoc=3.2801'
          'perl-pod-simple=3.40'
          'perl-pod-usage=1.69'
          'perl-podlators=5.008'
          'perl-safe=2.41_01'
          'perl-scalar-list-utils=1.55'
          'perl-search-dict=1.07'
          'perl-selfloader=1.26'
          'perl-socket=2.029'
          'perl-storable=3.21'
          'perl-sys-syslog=0.36'
          'perl-term-ansicolor=5.01'
          'perl-term-cap=1.17'
          'perl-term-complete=1.403'
          'perl-term-readline=1.17'
          'perl-test-harness=3.42'
          'perl-test-simple=1.302175'
          'perl-test=1.31'
          'perl-text-abbrev=1.02'
          'perl-text-balanced=2.03'
          'perl-text-parsewords=3.30'
          'perl-text-tabs=2013.0523'
          'perl-thread-queue=3.14'
          'perl-thread-semaphore=2.13'
          'perl-threads-shared=1.61'
          'perl-threads=2.25'
          'perl-tie-file=1.06'
          'perl-tie-refhash=1.39'
          'perl-time-hires=1.9764'
          'perl-time-local=1.28'
          'perl-time-piece=1.3401'
          'perl-unicode-collate=1.27'
          'perl-unicode-normalize=1.27'
          'perl-version=0.9924'
          'perl-xsloader=0.30')
source=("https://www.cpan.org/src/5.0/perl-${pkgver}.tar.xz"
        'perlbin.sh'
        'perlbin.csh')
options=('makeflags' '!purge' 'emptydirs')
sha256sums=('57cc47c735c8300a8ce2fa0643507b44c4ae59012bfdad0121313db639e02309'
            '984f140969a38bf9015b773b20d352905d5f00f7f0c8417229293c11d74e64b0'
            'fd5d38ce3d624dcac5736d34af4b71edab447dceb4123dd1a2ad821f487151c2')

build() {
  cd ${pkgname}-${pkgver}

  ./Configure -des -Dusethreads -Duseshrplib -Doptimize="${CFLAGS}" \
    -Dprefix=/usr -Dvendorprefix=/usr \
    -Dprivlib=/usr/share/perl5/core_perl \
    -Darchlib=/usr/lib/perl5/core_perl \
    -Dsitelib=/usr/share/perl5/site_perl \
    -Dsitearch=/usr/lib/perl5/site_perl \
    -Dvendorlib=/usr/share/perl5/vendor_perl \
    -Dvendorarch=/usr/lib/perl5/vendor_perl \
    -Dscriptdir=/usr/bin/core_perl \
    -Dsitescript=/usr/bin/site_perl \
    -Dvendorscript=/usr/bin/vendor_perl \
    -Dinc_version_list=none \
    -Dman1dir=/usr/share/man/man1 -Dman3dir=/usr/share/man/man3 \
    -Dman1ext=1perl -Dman3ext=3perl -Dcccdlflags='-fPIC' \
    -Dlddlflags="-shared ${LDFLAGS}" -Dldflags="${LDFLAGS}"

  make
}

check() {
  cd ${pkgname}-${pkgver}
 
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  LD_PRELOAD="/usr/lib/libfakeroot/libfakeroot.so" 
  make DESTDIR=${pkgdir} install

  ### Perl Settings ###
  # Change man page extensions for site and vendor module builds, no email address
  sed -e '/^man1ext=/ s/1perl/1p/' -e '/^man3ext=/ s/3perl/3pm/' \
      -e "/^cf_email=/ s/'.*'/''/" \
      -e "/^perladmin=/ s/'.*'/''/" \
      -i ${pkgdir}/usr/lib/perl5/core_perl/Config_heavy.pl

  ### CPAN Settings ###
  # Set CPAN default config to use the site directories.
  sed -e '/(makepl_arg =>/   s/""/"INSTALLDIRS=site"/' \
      -e '/(mbuildpl_arg =>/ s/""/"installdirs=site"/' \
      -i ${pkgdir}/usr/share/perl5/core_perl/CPAN/FirstTime.pm

  # Profile script to set paths to perl scripts.
  install -D -m755 ${srcdir}/perlbin.sh \
                   ${pkgdir}/etc/profile.d/perlbin.sh
  # Profile script to set paths to perl scripts on csh.  
  install -D -m755 ${srcdir}/perlbin.csh \
                  ${pkgdir}/etc/profile.d/perlbin.csh
  
  # add default dir                
  install -d -m755 ${pkgdir}/usr/bin/vendor_perl
  install -d -m755 ${pkgdir}/usr/bin/site_perl
  
  rm ${pkgdir}/usr/bin/perl$pkgver
  (cd ${pkgdir}/usr/bin/core_perl;  ln -sf c2ph pstruct; ln -sf s2p psed)

  # Remove all pod files *except* those under /usr/share/perl5/core_perl/pod/
  rm -f ${pkgdir}/usr/share/perl5/core_perl/*.pod
  for d in ${pkgdir}/usr/share/perl5/core_perl/*; do
    if [ -d $d -a $(basename $d) != "pod" ]; then
      find $d -name *.pod -delete
    fi
  done

  find ${pkgdir}/usr/lib -name *.pod -delete
  find ${pkgdir} -name .packlist -delete
}
