
pkgname=perl
pkgver=5.38.2
pkgrel=1
pkgdesc="A highly capable, feature-rich programming language"
arch=('x86_64')
license=('GPL' 'PerlArtistic')
url="https://www.perl.org"
groups=('base')
depends=('gdbm' 'coreutils' 'glibc' 'sh' 'libxcrypt')
provides=('perl-archive-tar=2.40'
          'perl-attribute-handlers=1.03'
          'perl-autodie=2.36'
          'perl-autoloader=5.74'
          'perl-autouse=1.11'
          'perl-base=2.27'
          'perl-bignum=0.66'
          'perl-carp=1.54'
          'perl-compress-raw-bzip2=2.204_001'
          'perl-compress-raw-zlib=2.204_001'
          'perl-config-perl-v=0.36'
          'perl-constant=1.33'
          'perl-cpan-meta-requirements=2.140'
          'perl-cpan-meta-yaml=0.018'
          'perl-cpan-meta=2.150010'
          'perl-cpan=2.36'
          'perl-data-dumper=2.188'
          'perl-db_file=1.858'
          'perl-devel-ppport=3.71'
          'perl-devel-selfstubber=1.06'
          'perl-digest-md5=2.58_01'
          'perl-digest-sha=6.04'
          'perl-digest=1.20'
          'perl-dumpvalue=1.21'
          'perl-encode=3.19'
          'perl-encoding-warnings=0.14'
          'perl-env=1.06'
          'perl-experimental=0.031'
          'perl-exporter=5.77'
          'perl-extutils-cbuilder=0.280238'
          'perl-extutils-constant=0.25'
          'perl-extutils-install=2.22'
          'perl-extutils-makemaker=7.70'
          'perl-extutils-manifest=1.73'
          'perl-extutils-parsexs=3.51'
          'perl-extutils-pl2bat=0.005'
          'perl-file-fetch=1.04'
          'perl-file-path=2.18'
          'perl-file-temp=0.2311'
          'perl-filter-simple=0.96'
          'perl-filter-util-call=1.64'
          'perl-findbin=1.53'
          'perl-getopt-long=2.54'
          'perl-http-tiny=0.086'
          'perl-i18n-collate=1.02'
          'perl-i18n-langtags=0.45'
          'perl-if=0.0610'
          'perl-io-compress=2.204'
          'perl-io-socket-ip=0.41_01'
          'perl-io-zlib=1.14'
          'perl-io=1.52'
          'perl-ipc-cmd=1.04'
          'perl-ipc-sysv=2.09'
          'perl-json-pp=4.16'
          'perl-lib=0.65'
          'perl-libnet=3.15'
          'perl-locale-maketext-simple=0.21_01'
          'perl-locale-maketext=1.33'
          'perl-math-bigint-fastcalc=0.5013'
          'perl-math-bigint=1.999837'
          'perl-math-bigrat=0.2624'
          'perl-math-complex=1.62'
          'perl-memoize=1.16'
          'perl-mime-base64=3.16_01'
          'perl-module-corelist=5.20231129'
          'perl-module-load-conditional=0.74'
          'perl-module-load=0.36'
          'perl-module-loaded=0.08'
          'perl-module-metadata=1.000037'
          'perl-net-ping=2.76'
          'perl-params-check=0.38'
          'perl-parent=0.241'
          'perl-pathtools=3.89'
          'perl-perl-ostype=1.010'
          'perl-perlfaq=5.20210520'
          'perl-perlio-via-quotedprint=0.10'
          'perl-pod-checker=1.75'
          'perl-pod-escapes=1.07'
          'perl-pod-perldoc=3.2801'
          'perl-pod-simple=3.43'
          'perl-pod-usage=2.03'
          'perl-podlators=5.010'
          'perl-safe=2.44'
          'perl-scalar-list-utils=1.63'
          'perl-search-dict=1.07'
          'perl-selfloader=1.26'
          'perl-socket=2.036'
          'perl-storable=3.32'
          'perl-sys-syslog=0.36'
          'perl-term-ansicolor=5.01'
          'perl-term-cap=1.18'
          'perl-term-complete=1.403'
          'perl-term-readline=1.17'
          'perl-test-harness=3.44'
          'perl-test-simple=1.302194'
          'perl-test=1.31'
          'perl-text-abbrev=1.02'
          'perl-text-balanced=2.06'
          'perl-text-parsewords=3.31'
          'perl-text-tabs=2021.0814'
          'perl-thread-queue=3.14'
          'perl-thread-semaphore=2.13'
          'perl-threads-shared=1.68'
          'perl-threads=2.36'
          'perl-tie-file=1.07'
          'perl-tie-refhash=1.40'
          'perl-time-hires=1.9775'
          'perl-time-local=1.30'
          'perl-time-piece=1.3401_01'
          'perl-unicode-collate=1.31'
          'perl-unicode-normalize=1.32'
          'perl-version=0.9929'
          'perl-xsloader=0.32')
source=("https://www.cpan.org/src/5.0/perl-${pkgver}.tar.xz"
        'perlbin.sh'
        'perlbin.csh'
        "https://github.com/Perl/perl5/commit/5bc1e5fdd87aa205011512cd1e6cc655bcf677fd.diff")
options=('makeflags' '!purge' 'emptydirs')
sha256sums=('d91115e90b896520e83d4de6b52f8254ef2b70a8d545ffab33200ea9f1cf29e8'
            '984f140969a38bf9015b773b20d352905d5f00f7f0c8417229293c11d74e64b0'
            'fd5d38ce3d624dcac5736d34af4b71edab447dceb4123dd1a2ad821f487151c2'
            '2f700172cb523c87d935aba7838d48622685496d23c29ee1be7f57a8170b7631')

build() {
  cd ${pkgname}-${pkgver}
  # test suite failure with gdbm 1.20 & up
  #patch -p1 -i ${srcdir}/5bc1e5fdd87aa205011512cd1e6cc655bcf677fd.diff

  ./Configure -des -Dusethreads -Duseshrplib -Doptimize="${CFLAGS}" \
    -Dprefix=/usr -Dvendorprefix=/usr \
    -Dprivlib=/usr/share/perl5/core_perl \
    -Darchlib=/usr/lib/perl5/core_perl \
    -Dsitelib=/usr/share/perl5/site_perl \
    -Dsitearch=/usr/lib/perl5/site_perl \
    -Dvendorlib=/usr/share/perl5/vendor_perl \
    -Dvendorarch=/usr/lib/perl5/vendor_perl \
    -Dscriptdir=/usr/bin/core_perl \
    -Dsitescript=/usr/bin/site_perl \
    -Dvendorscript=/usr/bin/vendor_perl \
    -Dinc_version_list=none \
    -Dman1dir=/usr/share/man/man1 -Dman3dir=/usr/share/man/man3 \
    -Dman1ext=1perl -Dman3ext=3perl -Dcccdlflags='-fPIC' \
    -Dlddlflags="-shared ${LDFLAGS}" -Dldflags="${LDFLAGS}"

  make
}

check() {
  cd ${pkgname}-${pkgver}
  # 1 of 2623 failed:
  # t/porting/regen .............. 'META.json-new' and 'META.json' differ
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  LD_PRELOAD="/usr/lib/libfakeroot/libfakeroot.so" 
  make DESTDIR=${pkgdir} install

  ### Perl Settings ###
  # Change man page extensions for site and vendor module builds, no email address
  sed -e '/^man1ext=/ s/1perl/1p/' -e '/^man3ext=/ s/3perl/3pm/' \
      -e "/^cf_email=/ s/'.*'/''/" \
      -e "/^perladmin=/ s/'.*'/''/" \
      -i ${pkgdir}/usr/lib/perl5/core_perl/Config_heavy.pl

  ### CPAN Settings ###
  # Set CPAN default config to use the site directories.
  sed -e '/(makepl_arg =>/   s/""/"INSTALLDIRS=site"/' \
      -e '/(mbuildpl_arg =>/ s/""/"installdirs=site"/' \
      -i ${pkgdir}/usr/share/perl5/core_perl/CPAN/FirstTime.pm

  # Profile script to set paths to perl scripts.
  install -D -m755 ${srcdir}/perlbin.sh \
                   ${pkgdir}/etc/profile.d/perlbin.sh
  # Profile script to set paths to perl scripts on csh.  
  install -D -m755 ${srcdir}/perlbin.csh \
                  ${pkgdir}/etc/profile.d/perlbin.csh
  
  # add default dir                
  install -d -m755 ${pkgdir}/usr/bin/vendor_perl
  install -d -m755 ${pkgdir}/usr/bin/site_perl
  
  rm ${pkgdir}/usr/bin/perl$pkgver
  (cd ${pkgdir}/usr/bin/core_perl;  ln -sf c2ph pstruct; ln -sf s2p psed)

  # Remove all pod files *except* those under /usr/share/perl5/core_perl/pod/
  rm -f ${pkgdir}/usr/share/perl5/core_perl/*.pod
  for d in ${pkgdir}/usr/share/perl5/core_perl/*; do
    if [ -d $d -a $(basename $d) != "pod" ]; then
      find $d -name *.pod -delete
    fi
  done

  find ${pkgdir}/usr/lib -name *.pod -delete
  find ${pkgdir} -name .packlist -delete
}
