#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=libmpc
pkgver=0.9
pkgrel=2
# dash the next line if you want to skip the tests
_test=1
pkgdesc="Library for the arithmetic of complex numbers with arbitrarily high precision"
arch=('i686' 'x86_64')
url="http://www.multiprecision.org"
license=('LGPL')
depends=('mpfr>=3.1.0')
options=('!libtool' 'log')
install=libmpc.install
source=(http://www.multiprecision.org/mpc/download/mpc-${pkgver/_/-}.tar.gz
        libmpc-0.9-configure_cflags_egrep_issue.patch)
sha1sums=('229722d553030734d49731844abfef7617b64f1a'
          '6fec4485925b538852d1889f26cc51ec6f820917')

build() {
  cd "${srcdir}/mpc-${pkgver}"

  # http://lists.gforge.inria.fr/pipermail/mpc-discuss/2011-February/000805.html
  patch -Np1 -i $srcdir/libmpc-0.9-configure_cflags_egrep_issue.patch
  autoreconf
  
  ./configure --prefix=/usr
  make

  if [ -n "${_test}" ]; then
     echo "<<< STARTING TESTS >>>"
     make check
     echo "<<< ENDING TESTS >>>"
  fi
}

package() {
  cd "${srcdir}/mpc-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mv ${pkgdir}/usr/share/info/{mpc,libmpc}.info
}
