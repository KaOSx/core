
pkgname=libmpc
pkgver=1.0.3
pkgrel=2
pkgdesc="Library for the arithmetic of complex numbers with arbitrarily high precision"
arch=('x86_64')
url="http://www.multiprecision.org"
license=('LGPL')
depends=('mpfr>=3.1.3')
options=('!libtool')
install=libmpc.install
source=("http://www.multiprecision.org/mpc/download/mpc-${pkgver}.tar.gz")
md5sums=('d6a1d5f8ddea3abd2cc3e98f58352d26')

build() {
  cd "${srcdir}/mpc-${pkgver}"
  
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/mpc-${pkgver}"
  
  make check
}

package() {
  cd "${srcdir}/mpc-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  mv ${pkgdir}/usr/share/info/{mpc,libmpc}.info
}
