
pkgname=perl-locale-gettext
_pkgname=Locale-gettext
pkgver=1.07
pkgrel=3
pkgdesc="Permits access from Perl to the gettext() family of functions"
arch=('x86_64')
license=('GPL' 'PerlArtistic')
url="https://search.cpan.org/dist/${_pkgname}/"
depends=('gettext' 'perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/P/PV/PVANDRY/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('909d47954697e7c04218f972915b787bd1244d75e3bd01620bc167d5bbc49c15')

build() {
  cd ${_pkgname}-${pkgver}

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_pkgname}-${pkgver}
  
  make test
}

package() {
  cd ${_pkgname}-${pkgver}
  make install DESTDIR=${pkgdir}

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}
