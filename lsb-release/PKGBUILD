
# code at https://github.com/LinuxStandardBase/lsb
pkgname=lsb-release
pkgver=1.4
pkgrel=13
pkgdesc="LSB version query program."
arch=('x86_64')
url="https://wiki.linuxfoundation.org/lsb/start"
makedepends=('perl') # to generate the man page
license=('GPL2')
# newer versions at https://www.linuxbase.org/download
source=("https://downloads.sourceforge.net/lsb/${pkgname}-${pkgver}.tar.gz")
md5sums=('30537ef5a01e0ca94b7b8eb6a36bb1e4')

build() {
  cd ${pkgname}-${pkgver}
  
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix=${pkgdir}/usr mandir=${pkgdir}/usr/share/man install

  install -d -m755 ${pkgdir}/etc
  echo LSB_VERSION=${pkgver} >> ${pkgdir}/etc/lsb-release
  echo DISTRIB_ID=KaOS >> ${pkgdir}/etc/lsb-release
  echo DISTRIB_RELEASE=rolling >> ${pkgdir}/etc/lsb-release
  echo DISTRIB_DESCRIPTION=\"KaOS\" >> ${pkgdir}/etc/lsb-release
}

